create or replace PACKAGE body SD_UI_REPORTS_PKG
/**************************************************************** 
This package will be used by UI for reporting purpose
created : 11/03/2014 jxc517 CCN Project....
changed :
*****************************************************************/
AS

V_DUMMY_DATE             DATE := '01-JAN-2222';

PROCEDURE STORE_DRAFTS_FILTER_S_SP (
/*******************************************************************************
	STORE_DRAFTS_FILTER_S_SP

	This procedure is intended to select records from STORE_DRAFTS & UNATTACHED_MNL_DRFT_DTL table
  with all fields part of the filtering

Created : 10/27/2014 jxc517 CCN Project....
Changed : 02/27/2015 jxc517 CCN Project....
          Added Stop_Pay_Date start and end logic
*******************************************************************************/
IN_TABLE_NAME IN     VARCHAR2
,IN_ROW_DATA  IN     CLOB
,OUT_REF_CUR     OUT SD_TABLE_IU_PKG.REF_CURSOR)
IS
    V_CODE NUMBER;
    V_ERRM VARCHAR2(500);

    V_TEMP_ROW               STORE_DRAFTS%ROWTYPE;
    V_CHECK_RANGE_START      STORE_DRAFTS.DRAFT_NUMBER%TYPE;
    V_CHECK_RANGE_END        STORE_DRAFTS.DRAFT_NUMBER%TYPE;
    V_TRANSACTION_DATE_START STORE_DRAFTS.TRANSACTION_DATE%TYPE;
    V_TRANSACTION_DATE_END   STORE_DRAFTS.TRANSACTION_DATE%TYPE;
    V_PAID_DATE_START        STORE_DRAFTS.PAID_DATE%TYPE;
    V_PAID_DATE_END          STORE_DRAFTS.PAID_DATE%TYPE;
    V_ISSUE_DATE_START       STORE_DRAFTS.ISSUE_DATE%TYPE;
    V_ISSUE_DATE_END         STORE_DRAFTS.ISSUE_DATE%TYPE;
    V_STOP_PAY_DATE_START    STORE_DRAFTS.STOP_PAY_DATE%TYPE;
    V_STOP_PAY_DATE_END      STORE_DRAFTS.STOP_PAY_DATE%TYPE;
    V_VOID_DATE_START        STORE_DRAFTS.VOID_DATE%TYPE;
    V_VOID_DATE_END          STORE_DRAFTS.VOID_DATE%TYPE;
    V_STORE_DRAFT_INDICATOR  VARCHAR2(100);
    V_TRANSACTION_CODE       VARCHAR2(100);
    V_GL_ACCOUNT_NUMBER      VARCHAR(8);

    V_OUTSTANDING_IND        VARCHAR2(1);
    V_OUTSTANDING_DATE_START DATE;
    V_OUTSTANDING_DATE_END   DATE;
    V_ALL_IND                VARCHAR2(1);
    V_ALL_DATE_START         DATE;
    V_ALL_DATE_END           DATE;
    V_CHANGED_IND            VARCHAR2(1);
    V_CHANGED_DATE_START     DATE;
    V_CHANGED_DATE_END       DATE;

    LTAB_LNAME DBMS_UTILITY.LNAME_ARRAY;
    LN_LEN     BINARY_INTEGER;
BEGIN
    --Get the status indicators (comma separated)
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/STORE_DRAFT_INDICATOR/text()') = 1) THEN
        V_STORE_DRAFT_INDICATOR   := UPPER(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/STORE_DRAFT_INDICATOR/text()').GETSTRINGVAL());
    END IF;
    --Get the transaction types (comma separated)
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/TRANSACTION_CODE/text()') = 1) THEN
        V_TRANSACTION_CODE   := UPPER(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/TRANSACTION_CODE/text()').GETSTRINGVAL());
    END IF;
    --Get the GL Prime and Sub account numbers, after validating the input GL_ACCOUNT_NUMBER (inside the XML as a tag)
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/GL_ACCOUNT_NUMBER/text()') = 1) THEN
        V_GL_ACCOUNT_NUMBER   := UPPER(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/GL_ACCOUNT_NUMBER/text()').GETSTRINGVAL());
        SD_BUSINESS_RULES_PKG.SD_GL_ACCOUNT_VALIDATION(V_GL_ACCOUNT_NUMBER); --business rules
    END IF;

    SD_TABLE_IU_PKG.STORE_DRAFTS_ROWTYPE_SP(IN_TABLE_NAME,
                                            IN_ROW_DATA,
                                            V_TEMP_ROW);

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/CHECK_RANGE_START/text()') = 1) THEN
        V_CHECK_RANGE_START := XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/CHECK_RANGE_START/text()').GETSTRINGVAL();
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/CHECK_RANGE_END/text()') = 1) THEN
        V_CHECK_RANGE_END := XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/CHECK_RANGE_END/text()').GETSTRINGVAL();
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/TRANSACTION_DATE_START/text()') = 1) THEN
        V_TRANSACTION_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/TRANSACTION_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/TRANSACTION_DATE_END/text()') = 1) THEN
        V_TRANSACTION_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/TRANSACTION_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/ISSUE_DATE_START/text()') = 1) THEN
        V_ISSUE_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/ISSUE_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/ISSUE_DATE_END/text()') = 1) THEN
        V_ISSUE_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/ISSUE_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/PAID_DATE_START/text()') = 1) THEN
        V_PAID_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/PAID_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/PAID_DATE_END/text()') = 1) THEN
        V_PAID_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/PAID_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/STOP_PAY_DATE_START/text()') = 1) THEN
        V_STOP_PAY_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/STOP_PAY_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/STOP_PAY_DATE_END/text()') = 1) THEN
        V_STOP_PAY_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/STOP_PAY_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/VOID_DATE_START/text()') = 1) THEN
        V_VOID_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/VOID_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/VOID_DATE_END/text()') = 1) THEN
        V_VOID_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/VOID_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/OUTSTANDING_DATE_START/text()') = 1) THEN
        V_OUTSTANDING_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/OUTSTANDING_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/OUTSTANDING_DATE_END/text()') = 1) THEN
        V_OUTSTANDING_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/OUTSTANDING_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/ALL_DATE_START/text()') = 1) THEN
        V_ALL_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/ALL_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/ALL_DATE_END/text()') = 1) THEN
        V_ALL_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/ALL_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/CHANGED_DATE_START/text()') = 1) THEN
        V_CHANGED_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/CHANGED_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/CHANGED_DATE_END/text()') = 1) THEN
        V_CHANGED_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/CHANGED_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    --over write the values given by the rowtype procedure as we need to set them here for querying
    V_TEMP_ROW.PAY_INDICATOR  := NULL;
    V_TEMP_ROW.VOID_INDICATOR := NULL;
    V_TEMP_ROW.STOP_INDICATOR := NULL;

    --Below process supports UI to pass comma seperated indicators like 'OPEN,PAID,OUTSTANDING'
    IF V_STORE_DRAFT_INDICATOR IS NOT NULL THEN
        FOR rec IN (WITH DATA AS(SELECT V_STORE_DRAFT_INDICATOR str FROM dual)
                        SELECT regexp_substr(str,'[^,]+',1,level) str
                          FROM DATA
                       CONNECT BY regexp_substr(str, '[^,]+', 1, level) IS NOT NULL) LOOP
            CASE rec.str 
                WHEN 'PAID' THEN
                    V_TEMP_ROW.PAY_INDICATOR  := 'Y';
                WHEN 'VOID' THEN
                   V_TEMP_ROW.VOID_INDICATOR := 'Y';
                WHEN 'STOP' THEN
                    V_TEMP_ROW.STOP_INDICATOR := 'Y';
                WHEN 'ALL' THEN
                    V_ALL_IND := 'Y';
                WHEN 'CHANGED' THEN
                    V_CHANGED_IND := 'Y';
                WHEN 'OUTSTANDING' THEN
                    V_OUTSTANDING_IND := 'Y';
                ELSE NULL;
            END CASE;
        END LOOP;
    END IF;

    IF V_ALL_IND = 'Y' OR V_STORE_DRAFT_INDICATOR IS NULL THEN
        OPEN OUT_REF_CUR FOR
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM STORE_DRAFTS SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'))
            UNION
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM UNATTACHED_MNL_DRFT_DTL SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'));
    ELSIF V_CHANGED_IND = 'Y' THEN
        OPEN OUT_REF_CUR FOR
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM STORE_DRAFTS SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.ORIGINAL_NET_AMOUNT,-1)             <> NVL(SD.NET_AMOUNT,-1)
               AND (SD.STOP_PAY_DATE > V_CHANGED_DATE_END OR SD.STOP_PAY_DATE IS NULL)
               AND (SD.VOID_DATE > V_CHANGED_DATE_END OR SD.VOID_DATE IS NULL)
            UNION
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM UNATTACHED_MNL_DRFT_DTL SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.ORIGINAL_NET_AMOUNT,-1)             <> NVL(SD.NET_AMOUNT,-1)
               AND (SD.STOP_PAY_DATE > V_CHANGED_DATE_END OR SD.STOP_PAY_DATE IS NULL)
               AND (SD.VOID_DATE > V_CHANGED_DATE_END OR SD.VOID_DATE IS NULL);
    ELSIF V_OUTSTANDING_IND = 'Y' THEN
        OPEN OUT_REF_CUR FOR
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM STORE_DRAFTS SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'))
               AND (SD.PAID_DATE > V_OUTSTANDING_DATE_END OR SD.PAID_DATE IS NULL)
               AND (SD.STOP_PAY_DATE > V_OUTSTANDING_DATE_END OR SD.STOP_PAY_DATE IS NULL)
               AND (SD.VOID_DATE > V_OUTSTANDING_DATE_END OR SD.VOID_DATE IS NULL)
            UNION
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM UNATTACHED_MNL_DRFT_DTL SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'))
               AND (SD.PAID_DATE > V_OUTSTANDING_DATE_END OR SD.PAID_DATE IS NULL)
               AND (SD.STOP_PAY_DATE > V_OUTSTANDING_DATE_END OR SD.STOP_PAY_DATE IS NULL)
               AND (SD.VOID_DATE > V_OUTSTANDING_DATE_END OR SD.VOID_DATE IS NULL);
    ELSIF (V_TEMP_ROW.PAY_INDICATOR = 'Y'
           OR V_TEMP_ROW.VOID_INDICATOR = 'Y'
           OR V_TEMP_ROW.STOP_INDICATOR = 'Y') THEN  --Return manual drafts and any drafts with requested flags
        OPEN OUT_REF_CUR FOR
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM STORE_DRAFTS SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'))
               AND (
                   (NVL(SD.PAY_INDICATOR,'N')                 = NVL(V_TEMP_ROW.PAY_INDICATOR,'XXX') AND
                    NVL(SD.PAID_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_PAID_DATE_START,NVL(SD.PAID_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_PAID_DATE_END,NVL(SD.PAID_DATE,TRUNC(V_DUMMY_DATE)))) OR
                   (NVL(SD.STOP_INDICATOR,'N')                = NVL(V_TEMP_ROW.STOP_INDICATOR,'XXX') AND
                    NVL(SD.STOP_PAY_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_STOP_PAY_DATE_START,NVL(SD.STOP_PAY_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_STOP_PAY_DATE_END,NVL(SD.STOP_PAY_DATE,TRUNC(V_DUMMY_DATE)))) OR
                   (NVL(SD.VOID_INDICATOR,'N')                   = NVL(V_TEMP_ROW.VOID_INDICATOR,'XXX') AND
                    NVL(SD.VOID_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_VOID_DATE_START,NVL(SD.VOID_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_VOID_DATE_END,NVL(SD.VOID_DATE,TRUNC(V_DUMMY_DATE))))
                   )
            UNION
            SELECT DISTINCT SD.*, 
                   DECODE(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,
                          '-',NULL,
                          SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER) AS GL_ACCOUNT_NUMBER
              FROM UNATTACHED_MNL_DRFT_DTL SD,
                   STORE_DRAFTS_DETAIL SDD
             WHERE NVL(SD.COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(SD.COST_CENTER_CODE,'XXX'))
               AND NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_ISSUE_DATE_START,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_ISSUE_DATE_END,NVL(SD.ISSUE_DATE,TRUNC(V_DUMMY_DATE)))
               AND NVL(SD.DRAFT_NUMBER,'XXX') BETWEEN NVL(V_CHECK_RANGE_START,NVL(SD.DRAFT_NUMBER,'XXX')) 
                                                            AND NVL(V_CHECK_RANGE_END,NVL(SD.DRAFT_NUMBER,'XXX'))
               AND NVL(SD.TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(SD.TRANSACTION_NUMBER,'XXX'))
               AND INSTR(NVL(V_TRANSACTION_CODE,NVL(SD.POS_TRANSACTION_CODE,'XXX'))||',',NVL(SD.POS_TRANSACTION_CODE,'XXX')||',') > 0
               AND NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(SD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
               AND NVL(SD.CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(SD.CUSTOMER_JOB_NUMBER,'XXX'))
               AND SD.COST_CENTER_CODE                        = SDD.COST_CENTER_CODE(+)
               AND SD.CHECK_SERIAL_NUMBER                     = SDD.CHECK_SERIAL_NUMBER(+)
               AND SD.TRANSACTION_DATE                        = SDD.TRANSACTION_DATE(+)
               AND NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX') 
                                                              = NVL(V_GL_ACCOUNT_NUMBER,NVL(SDD.GL_PRIME_ACCOUNT_NUMBER||'-'||SDD.GL_SUB_ACCOUNT_NUMBER,'XXX'))
               AND (
                   (NVL(SD.PAY_INDICATOR,'N')                 = NVL(V_TEMP_ROW.PAY_INDICATOR,'XXX') AND
                    NVL(SD.PAID_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_PAID_DATE_START,NVL(SD.PAID_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_PAID_DATE_END,NVL(SD.PAID_DATE,TRUNC(V_DUMMY_DATE)))) OR
                   (NVL(SD.STOP_INDICATOR,'N')                = NVL(V_TEMP_ROW.STOP_INDICATOR,'XXX') AND
                    NVL(SD.STOP_PAY_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_STOP_PAY_DATE_START,NVL(SD.STOP_PAY_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_STOP_PAY_DATE_END,NVL(SD.STOP_PAY_DATE,TRUNC(V_DUMMY_DATE)))) OR
                   (NVL(SD.VOID_INDICATOR,'N')                   = NVL(V_TEMP_ROW.VOID_INDICATOR,'XXX') AND
                    NVL(SD.VOID_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_VOID_DATE_START,NVL(SD.VOID_DATE,TRUNC(V_DUMMY_DATE))) 
                                                            AND NVL(V_VOID_DATE_END,NVL(SD.VOID_DATE,TRUNC(V_DUMMY_DATE))))
                   );
    END IF;

EXCEPTION
    WHEN OTHERS THEN 
        V_CODE  := SQLCODE;
        V_ERRM  := SUBSTR(SQLERRM,1,200);
        ERRPKG.RAISE_ERR(V_CODE, 'STORE_DRAFTS_FILTER_S_SP, ' || ' ' || 'IN_TABLE_NAME IS ' || IN_TABLE_NAME, V_ERRM);
END STORE_DRAFTS_FILTER_S_SP;

PROCEDURE STORE_DRAFTS_DTL_FILTER_S_SP (
/*******************************************************************************
	STORE_DRAFTS_DTL_FILTER_S_SP
  
	This procedure is intended to select records from STORE_DRAFTS_DETAIL table

<?xml version="1.0" encoding="UTF-8" ?>
<STORE_DRAFTS_DETAIL_UI_SP xmlns:xsi="http://www.w3.org/2001/XMLSchema" xsi:nonamespaceSchemaLocation="http://www.oracle.com/Employee.xsd">
<STORE_DRAFTS_DETAIL>
<STORE_DRAFTS_DETAIL_ID>XXXXXXXXXXXXXXXXXXXXXX</STORE_DRAFTS_DETAIL_ID>
<COST_CENTER_CODE>XXXXXXXXXXXXXXXXXXXXXXXX</COST_CENTER_CODE>
<CHECK_SERIAL_NUMBER>XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX</CHECK_SERIAL_NUMBER>
  <TRANSACTION_DATE>03-30-2012</TRANSACTION_DATE>
  <TRANSACTION_DATE_START>03-30-2012</TRANSACTION_DATE_START>
<TERMINAL_NUMBER>XXXXXXXXXXXXXXXXXXXX</TERMINAL_NUMBER>
<TRANSACTION_NUMBER>XXXXXXXXXXXXXXXXXXXX</TRANSACTION_NUMBER>
<CUSTOMER_ACCOUNT_NUMBER>XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX</CUSTOMER_ACCOUNT_NUMBER>
<CUSTOMER_JOB_NUMBER>XXXXXXXX</CUSTOMER_JOB_NUMBER>
<GL_PRIME_ACCOUNT_NUMBER>XXXXXXXXXXXXXXXX</GL_PRIME_ACCOUNT_NUMBER>
<GL_SUB_ACCOUNT_NUMBER>XXXXXXXXXXXX</GL_SUB_ACCOUNT_NUMBER>
<ITEM_QUANTITY>XXXXXXXXXXXXXXXXXXXXXX</ITEM_QUANTITY>
<ITEM_PRICE>XXXXXXXXXXXXXXXXXXXXXX</ITEM_PRICE>
<ITEM_EXT_AMOUNT>XXXXXXXXXXXXXXXXXXXXXX</ITEM_EXT_AMOUNT>
<BOOK_DATE_SEQUENCE>XXXXXXXXXXXXXXXXXXXX</BOOK_DATE_SEQUENCE>
<LBR_TRANSACTION_DATE>MM-DD-RRRR</LBR_TRANSACTION_DATE>
<LBR_TERMINAL_NUMBER>XXXXXXXXXXXXXXXXXXXX</LBR_TERMINAL_NUMBER>
<LBR_TRANSACTION_NUMBER>XXXXXXXXXXXXXXXXXXXX</LBR_TRANSACTION_NUMBER>
</STORE_DRAFTS_DETAIL>
</STORE_DRAFTS_DETAIL_UI_SP>

Created : 11/03/2014 jxc517 CCN Project....
Changed : 
*******************************************************************************/
IN_TABLE_NAME IN     VARCHAR2
,IN_ROW_DATA  IN     CLOB
,OUT_REF_CUR     OUT SD_TABLE_IU_PKG.REF_CURSOR)
IS
    V_CODE NUMBER;
    V_ERRM VARCHAR2(500);

    V_TEMP_ROW       STORE_DRAFTS_DETAIL%ROWTYPE;
    V_CHECK_RANGE_START      STORE_DRAFTS.DRAFT_NUMBER%TYPE;
    V_CHECK_RANGE_END        STORE_DRAFTS.DRAFT_NUMBER%TYPE;
    V_TRANSACTION_DATE_START STORE_DRAFTS.TRANSACTION_DATE%TYPE;
    V_TRANSACTION_DATE_END   STORE_DRAFTS.TRANSACTION_DATE%TYPE;
BEGIN

    SD_TABLE_IU_PKG.STORE_DRAFTS_DETAIL_ROWTYPE_SP(IN_TABLE_NAME,
                                                   IN_ROW_DATA,
                                                   V_TEMP_ROW);

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/CHECK_RANGE_START/text()') = 1) THEN
        V_CHECK_RANGE_START := XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/CHECK_RANGE_START/text()').GETSTRINGVAL();
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/CHECK_RANGE_END/text()') = 1) THEN
        V_CHECK_RANGE_END := XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/CHECK_RANGE_END/text()').GETSTRINGVAL();
    END IF;

    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/TRANSACTION_DATE_START/text()') = 1) THEN
        V_TRANSACTION_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/TRANSACTION_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/STORE_DRAFTS/TRANSACTION_DATE_END/text()') = 1) THEN
        V_TRANSACTION_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/STORE_DRAFTS/TRANSACTION_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    OPEN OUT_REF_CUR FOR
        SELECT *
          FROM STORE_DRAFTS_DETAIL
         WHERE NVL(STORE_DRAFTS_DETAIL_ID,-1)              = NVL(V_TEMP_ROW.STORE_DRAFTS_DETAIL_ID,NVL(STORE_DRAFTS_DETAIL_ID,-1))
           AND NVL(COST_CENTER_CODE,'XXX')                 = NVL(V_TEMP_ROW.COST_CENTER_CODE,NVL(COST_CENTER_CODE,'XXX'))
           AND NVL(CHECK_SERIAL_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CHECK_SERIAL_NUMBER,NVL(CHECK_SERIAL_NUMBER,'XXX'))
           --AND NVL(TRANSACTION_DATE,TRUNC(V_DUMMY_DATE))        = NVL(V_TEMP_ROW.TRANSACTION_DATE,NVL(TRANSACTION_DATE,TRUNC(V_DUMMY_DATE)))
           AND NVL(TRANSACTION_DATE,TRUNC(V_DUMMY_DATE)) BETWEEN NVL(V_TRANSACTION_DATE_START,NVL(TRANSACTION_DATE,TRUNC(V_DUMMY_DATE))) 
                                                        AND NVL(V_TRANSACTION_DATE_END,NVL(TRANSACTION_DATE,TRUNC(V_DUMMY_DATE)))
           AND NVL(TERMINAL_NUMBER,'XXX')                  = NVL(V_TEMP_ROW.TERMINAL_NUMBER,NVL(TERMINAL_NUMBER,'XXX'))
           AND NVL(TRANSACTION_NUMBER,'XXX')               = NVL(V_TEMP_ROW.TRANSACTION_NUMBER,NVL(TRANSACTION_NUMBER,'XXX'))
           AND NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'))
           AND NVL(CUSTOMER_JOB_NUMBER,'XXX')              = NVL(V_TEMP_ROW.CUSTOMER_JOB_NUMBER,NVL(CUSTOMER_JOB_NUMBER,'XXX'))
           AND NVL(GL_PRIME_ACCOUNT_NUMBER,'XXX')          = NVL(V_TEMP_ROW.GL_PRIME_ACCOUNT_NUMBER,NVL(GL_PRIME_ACCOUNT_NUMBER,'XXX'))
           AND NVL(GL_SUB_ACCOUNT_NUMBER,'XXX')            = NVL(V_TEMP_ROW.GL_SUB_ACCOUNT_NUMBER,NVL(GL_SUB_ACCOUNT_NUMBER,'XXX'))
           AND NVL(ITEM_QUANTITY,-1)                       = NVL(V_TEMP_ROW.ITEM_QUANTITY,NVL(ITEM_QUANTITY,-1))
           AND NVL(ITEM_PRICE,-1)                          = NVL(V_TEMP_ROW.ITEM_PRICE,NVL(ITEM_PRICE,-1))
           AND NVL(ITEM_EXT_AMOUNT,-1)                     = NVL(V_TEMP_ROW.ITEM_EXT_AMOUNT,NVL(ITEM_EXT_AMOUNT,-1))
           AND NVL(BOOK_DATE_SEQUENCE,'XXX')               = NVL(V_TEMP_ROW.BOOK_DATE_SEQUENCE,NVL(BOOK_DATE_SEQUENCE,'XXX'))
           AND NVL(LBR_TRANSACTION_DATE,TRUNC(V_DUMMY_DATE))    = NVL(V_TEMP_ROW.LBR_TRANSACTION_DATE,NVL(LBR_TRANSACTION_DATE,TRUNC(V_DUMMY_DATE)))
           AND NVL(LBR_TERMINAL_NUMBER,'XXX')              = NVL(V_TEMP_ROW.LBR_TERMINAL_NUMBER,NVL(LBR_TERMINAL_NUMBER,'XXX'))
           AND NVL(LBR_TRANSACTION_NUMBER,'XXX')           = NVL(V_TEMP_ROW.LBR_TRANSACTION_NUMBER,NVL(LBR_TRANSACTION_NUMBER,'XXX'));

EXCEPTION
    WHEN OTHERS THEN
        V_CODE  := SQLCODE;
        V_ERRM  := SUBSTR(SQLERRM,1,200);
        ERRPKG.RAISE_ERR(V_CODE, 'STORE_DRAFTS_DETAIL_S_SP, ' || ' ' || 'IN_TABLE_NAME IS ' || IN_TABLE_NAME, V_ERRM);
END STORE_DRAFTS_DTL_FILTER_S_SP;

PROCEDURE LOAD_FILTER_TABLE (
/*******************************************************************************
	This procedure is intended to load the object table that will be eventually
  returned as table type ref cursor to UI

Created : 03/03/2015 jxc517 CCN Project....
Changed : 
*******************************************************************************/
IN_REF_CURSOR          IN     SD_TABLE_IU_PKG.REF_CURSOR
,IN_OBJECT_TABLE_COUNT IN OUT NUMBER
,IN_OUT_OBJECT_TABLE   IN OUT SD_FILTER_TABLE_TYPE)
IS
    V_SD_FILTER_RECORD SD_UI_REPORTS_PKG.SD_FILTER_RECORD_TYPE;
BEGIN
    LOOP
        FETCH IN_REF_CURSOR INTO V_SD_FILTER_RECORD;
        EXIT WHEN IN_REF_CURSOR%NOTFOUND;
        IN_OUT_OBJECT_TABLE.EXTEND;
        IN_OBJECT_TABLE_COUNT := IN_OBJECT_TABLE_COUNT + 1;
        IN_OUT_OBJECT_TABLE(IN_OBJECT_TABLE_COUNT) := SD_FILTER_OBJECT_TYPE(
                                 V_SD_FILTER_RECORD.COST_CENTER_CODE,
                                 V_SD_FILTER_RECORD.CHECK_SERIAL_NUMBER,
                                 V_SD_FILTER_RECORD.TRANSACTION_DATE,
                                 V_SD_FILTER_RECORD.TERMINAL_NUMBER,
                                 V_SD_FILTER_RECORD.TRANSACTION_NUMBER,
                                 V_SD_FILTER_RECORD.CUSTOMER_ACCOUNT_NUMBER,
                                 V_SD_FILTER_RECORD.CUSTOMER_JOB_NUMBER,
                                 V_SD_FILTER_RECORD.POS_TRANSACTION_CODE,
                                 V_SD_FILTER_RECORD.POS_TRANSACTION_TIME,
                                 V_SD_FILTER_RECORD.BANK_NUMBER,
                                 V_SD_FILTER_RECORD.BANK_ACCOUNT_NUMBER,
                                 V_SD_FILTER_RECORD.BANK_REFERENCE,
                                 V_SD_FILTER_RECORD.PAYEE_NAME,
                                 V_SD_FILTER_RECORD.ADDRESS_LINE_1,
                                 V_SD_FILTER_RECORD.ADDRESS_LINE_2,
                                 V_SD_FILTER_RECORD.CITY,
                                 V_SD_FILTER_RECORD.STATE_CODE,
                                 V_SD_FILTER_RECORD.ZIP_CODE_PREFIX,
                                 V_SD_FILTER_RECORD.ZIP_CODE_SUFFIX,
                                 V_SD_FILTER_RECORD.PHONE_AREA_CODE,
                                 V_SD_FILTER_RECORD.PHONE_NUMBER,
                                 V_SD_FILTER_RECORD.EMPLOYEE_NUMBER,
                                 V_SD_FILTER_RECORD.ISSUE_DATE,
                                 V_SD_FILTER_RECORD.PAID_DATE,
                                 V_SD_FILTER_RECORD.STOP_PAY_DATE,
                                 V_SD_FILTER_RECORD.STOP_PAY_REMOVE_DATE,
                                 V_SD_FILTER_RECORD.VOID_DATE,
                                 V_SD_FILTER_RECORD.AMOUNT_CHANGE_DATE,
                                 V_SD_FILTER_RECORD.GROSS_AMOUNT,
                                 V_SD_FILTER_RECORD.RETAIN_AMOUNT,
                                 V_SD_FILTER_RECORD.NET_AMOUNT,
                                 V_SD_FILTER_RECORD.ORIGINAL_NET_AMOUNT,
                                 V_SD_FILTER_RECORD.BANK_PAID_AMOUNT,
                                 V_SD_FILTER_RECORD.TRANSACTION_SOURCE,
                                 V_SD_FILTER_RECORD.CHANGE_DATE,
                                 V_SD_FILTER_RECORD.CHANGE_SOURCE,
                                 V_SD_FILTER_RECORD.SLS_BOOK_DATE,
                                 V_SD_FILTER_RECORD.CYCLE_RUN_NUMBER,
                                 V_SD_FILTER_RECORD.BOOK_DATE_SEQUENCE,
                                 V_SD_FILTER_RECORD.REASON_CODE,
                                 V_SD_FILTER_RECORD.DRAFT_NUMBER,
                                 V_SD_FILTER_RECORD.VOID_INDICATOR,
                                 V_SD_FILTER_RECORD.PAY_INDICATOR,
                                 V_SD_FILTER_RECORD.STOP_INDICATOR,
                                 V_SD_FILTER_RECORD.OPEN_INDICATOR,
                                 V_SD_FILTER_RECORD.LOAD_DATE,
                                 V_SD_FILTER_RECORD.GL_ACCOUNT_NUMBER);
    END LOOP;
    CLOSE IN_REF_CURSOR;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'LOAD_FILTER_TABLE', SUBSTR(SQLERRM,1,200));
END LOAD_FILTER_TABLE;

END SD_UI_REPORTS_PKG;

