create or replace PACKAGE body SD_PAID_DETAILS_LOAD
/**************************************************************** 
This package will update paid details in the existing CCN Store Drafts Oracle Database
created : 07/23/2014 jxc517 CCN Project....
changed :
*****************************************************************/
AS

PROCEDURE CCN_SD_PAID_LOAD_SP
/******************************************************************
This Procedure is a wrapper for the Paid details update of the store drafts tables

created : 07/23/2014 jxc517 CCN Project....
changed :
******************************************************************/
AS
    
    CURSOR TEMP_CUR is
        SELECT PAID_DATE_PAID PAID_DATE,
               CCN_COMMON_TOOLS.RETURN_NUMBER(PAID_AMONUT_ITEMS,10,2) AMOUNT,
               PAID_CHECK_SERIAL_NUMBER CHECK_SERIAL_NUMBER
          FROM TEMP_PAID_DETAILS_SUNTRUST
        UNION
        SELECT PAID_DATE PAID_DATE,
               CCN_COMMON_TOOLS.RETURN_NUMBER(AMOUNT,11,2) AMOUNT,
               FILLER||CHECK_NUMBER_CC||CHECK_NUMBER_SQ||CHECK_NUMBER_CK CHECK_SERIAL_NUMBER
          FROM TEMP_PAID_DETAILS_ROYAL;

    V_COMMIT     NUMBER := 0;
    V_TEMP_ROW   STORE_DRAFTS%ROWTYPE;
    V_CONTEXT    VARCHAR2(200);
    V_START_TIME NUMBER;
    
BEGIN

      V_START_TIME := DBMS_UTILITY.GET_TIME;
      V_CONTEXT := 'Update STORE_DRAFTS paid details ';

    FOR rec IN TEMP_CUR LOOP
        BEGIN
            BEGIN
                SELECT *
                  INTO V_TEMP_ROW
                  FROM STORE_DRAFTS
                 WHERE CHECK_SERIAL_NUMBER = CCN_COMMON_TOOLS.VALIDATE_DATA_BEFORE_LOAD(rec.CHECK_SERIAL_NUMBER);

                V_TEMP_ROW.PAID_DATE           := CCN_COMMON_TOOLS.GET_DATE_VALUE(rec.PAID_DATE, 'MMDDYY');
                V_TEMP_ROW.GROSS_AMOUNT        := rec.AMOUNT;
                V_TEMP_ROW.NET_AMOUNT          := rec.AMOUNT;
                V_TEMP_ROW.ORIGINAL_NET_AMOUNT := rec.AMOUNT;
                V_TEMP_ROW.BANK_PAID_AMOUNT    := rec.AMOUNT;
                SD_BUSINESS_RULES_PKG.SET_STORE_DRAFT_FLAGS(V_TEMP_ROW);
            EXCEPTION
                WHEN OTHERS THEN
                    V_TEMP_ROW := NULL;
            END;

            IF V_TEMP_ROW.CHECK_SERIAL_NUMBER IS NOT NULL THEN
                UPDATE STORE_DRAFTS
                   SET ROW = V_TEMP_ROW
                 WHERE CHECK_SERIAL_NUMBER = V_TEMP_ROW.CHECK_SERIAL_NUMBER;
            END IF;

            V_TEMP_ROW := NULL ;
            V_COMMIT := V_COMMIT + 1;
            IF V_COMMIT > 500 THEN
                COMMIT;
                V_COMMIT := 0;
            END IF;   
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE,
                                           'LOAD_SUNTRUST_PAID_DETAILS',
                                           SQLERRM,
                                           NULL, --cost_center_code
                                           NVL(rec.CHECK_SERIAL_NUMBER,''));
        END;
    END LOOP;
    COMMIT;
      dbms_output.put_line('STORE_DRAFTS paid details updated in : '|| (dbms_utility.get_time - v_start_time)/100 || ' Seconds');

EXCEPTION
    WHEN OTHERS THEN
         ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE,
                                    'CCN_SD_PAID_LOAD_SP',
                                    SQLERRM,
                                    '000000',
                                    '0000000000'); 
END CCN_SD_PAID_LOAD_SP;

END SD_PAID_DETAILS_LOAD;

