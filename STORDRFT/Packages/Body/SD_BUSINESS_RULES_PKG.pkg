create or replace PACKAGE body SD_BUSINESS_RULES_PKG
/****************************************************************************** 
This package BODY will do the business rules validations:
  
created : 06/30/2014 
changed :
******************************************************************************/
AS

PROCEDURE SET_STORE_DRAFT_FLAGS(
/*****************************************************************************
	SET_STORE_DRAFT_FLAGS

	This function will get set the flag values for store drafts table

created : 07/10/2014 
changed :
*****************************************************************************/
    IN_ROW_TYPE     IN OUT    STORE_DRAFTS%ROWTYPE)
IS
BEGIN
    IF IN_ROW_TYPE.VOID_DATE IS NULL AND IN_ROW_TYPE.STOP_PAY_DATE IS NULL THEN
        IN_ROW_TYPE.OPEN_INDICATOR := 'Y';
    ELSE
        IN_ROW_TYPE.OPEN_INDICATOR := 'N';
    END IF;
    IF IN_ROW_TYPE.PAID_DATE IS NOT NULL THEN
        IN_ROW_TYPE.PAY_INDICATOR := 'Y';
    ELSE
        IN_ROW_TYPE.PAY_INDICATOR := 'N';
    END IF;
    IF IN_ROW_TYPE.STOP_PAY_DATE IS NOT NULL THEN
        IN_ROW_TYPE.STOP_INDICATOR := 'Y';
    ELSE
        IN_ROW_TYPE.STOP_INDICATOR := 'N';
    END IF;
    IF IN_ROW_TYPE.VOID_DATE IS NOT NULL THEN
        IN_ROW_TYPE.VOID_INDICATOR := 'Y';
    ELSE
        IN_ROW_TYPE.VOID_INDICATOR := 'N';
    END IF;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'SET_STORE_DRAFT_FLAGS', SUBSTR(SQLERRM,1,200));
END SET_STORE_DRAFT_FLAGS;

FUNCTION GET_OPEN_FLAG(
/*****************************************************************************
	GET_OPEN_FLAG

	This function will get the flag value for open store drafts  

created : 06/30/2014 
changed :
*****************************************************************************/
    IN_ROW_TYPE     IN    STORE_DRAFTS%ROWTYPE) RETURN VARCHAR2
IS
    V_RETURN_VALUE VARCHAR2(1) := 'N';
BEGIN
    IF IN_ROW_TYPE.VOID_DATE IS NULL AND IN_ROW_TYPE.STOP_PAY_DATE IS NULL THEN
        V_RETURN_VALUE := 'Y';
    END IF;
    RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_RETURN_VALUE;
END GET_OPEN_FLAG;

FUNCTION GET_STOP_FLAG(
/*****************************************************************************
	GET_STOP_FLAG

	This function will get the flag value for stop store drafts  

created : 06/30/2014 
changed :
*****************************************************************************/
    IN_ROW_TYPE     IN    STORE_DRAFTS%ROWTYPE) RETURN VARCHAR2
IS
    V_RETURN_VALUE VARCHAR2(1) := 'N';
BEGIN
    IF IN_ROW_TYPE.STOP_PAY_DATE IS NOT NULL THEN
        V_RETURN_VALUE := 'Y';
    END IF;
    RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_RETURN_VALUE;
END GET_STOP_FLAG;

FUNCTION GET_VOID_FLAG(
/*****************************************************************************
	GET_VOID_FLAG

	This function will get the flag value for void store drafts  

created : 06/30/2014 
changed :
*****************************************************************************/
    IN_ROW_TYPE     IN    STORE_DRAFTS%ROWTYPE) RETURN VARCHAR2
IS
    V_RETURN_VALUE VARCHAR2(1) := 'N';
BEGIN
    IF IN_ROW_TYPE.VOID_DATE IS NOT NULL THEN
        V_RETURN_VALUE := 'Y';
    END IF;
    RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_RETURN_VALUE;
END GET_VOID_FLAG;

FUNCTION GET_PAY_FLAG(
/*****************************************************************************
	GET_PAY_FLAG

	This function will get the flag value for pay store drafts  

created : 06/30/2014 
changed :
*****************************************************************************/
    IN_ROW_TYPE     IN    STORE_DRAFTS%ROWTYPE) RETURN VARCHAR2
IS
    V_RETURN_VALUE VARCHAR2(1) := 'N';
BEGIN
    IF IN_ROW_TYPE.PAID_DATE IS NOT NULL THEN
        V_RETURN_VALUE := 'Y';
    END IF;
    RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_RETURN_VALUE;
END GET_PAY_FLAG;

FUNCTION IS_UNATTACHED_STORE_DRAFT(
/*****************************************************************************
	IS_UNATTACHED_STORE_DRAFT

	This function will return if the passed record is unattached or not based on some ruels.  

created : 07/01/2014 jxc517 CCN Project Team.....
changed : 12/10/2014 jxc517 CCN Project Team.....
          Modified code to handle updates on unattached manual drafts
*****************************************************************************/
    IN_STORE_DRAFTS_RECORD     IN    STORE_DRAFTS%ROWTYPE) RETURN BOOLEAN
IS
    V_RETURN_VALUE BOOLEAN := FALSE;
BEGIN
    IF IN_STORE_DRAFTS_RECORD.COST_CENTER_CODE IS NULL
            OR IN_STORE_DRAFTS_RECORD.CHECK_SERIAL_NUMBER IS NULL
            OR IN_STORE_DRAFTS_RECORD.TRANSACTION_DATE IS NULL THEN
        V_RETURN_VALUE := TRUE;
    END IF;
    RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_RETURN_VALUE;
END IS_UNATTACHED_STORE_DRAFT;

FUNCTION GET_CHECK_SERIAL_NUMBER(
/*****************************************************************************
	GET_CHECK_SERIAL_NUMBER

	This function will calculate the 10 digit check serial number based on passed cost center id
  and the 4 digit check serial number

created : 09/10/2014 jxc517 CCN Project . . .
changed :
*****************************************************************************/
    IN_COST_CENTER_CODE IN    STORE_DRAFTS.COST_CENTER_CODE%TYPE,
    IN_CHECK_NUMBER     IN    VARCHAR2) RETURN VARCHAR2
IS
    V_CHECK_NUMBER VARCHAR2(4);
    V_COST_CENTER  VARCHAR2(4);
    V_CHECK_SERIAL_NUMBER  VARCHAR2(10);
BEGIN

    --Get the last 4 characters of the input check number and the cost center code
    IF LENGTH(IN_CHECK_NUMBER) = 10 THEN
        V_CHECK_SERIAL_NUMBER := IN_CHECK_NUMBER;
    ELSIF LENGTH(IN_CHECK_NUMBER) = 4 THEN
        V_COST_CENTER  := SUBSTR(IN_COST_CENTER_CODE,-4,4);
        V_CHECK_NUMBER := IN_CHECK_NUMBER;
        V_CHECK_SERIAL_NUMBER := '0'||V_COST_CENTER||V_CHECK_NUMBER||CCN_COMMON_TOOLS.MODULUS_10('0'||V_COST_CENTER||V_CHECK_NUMBER);
    ELSIF LENGTH(IN_CHECK_NUMBER) > 4 THEN
        V_COST_CENTER  := SUBSTR(IN_COST_CENTER_CODE,-4,4);
        V_CHECK_NUMBER := SUBSTR(IN_CHECK_NUMBER,-5,4);
        V_CHECK_SERIAL_NUMBER := '0'||V_COST_CENTER||V_CHECK_NUMBER||CCN_COMMON_TOOLS.MODULUS_10('0'||V_COST_CENTER||V_CHECK_NUMBER);
    END IF;

    RETURN V_CHECK_SERIAL_NUMBER;

EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END GET_CHECK_SERIAL_NUMBER;

PROCEDURE UNATTACHED_MNL_DRFT_DTL_SP(
/*****************************************************************************
	UNATTACHED_MNL_DRFT_DTL_SP

	This procedure will do the bussiness rules validations for UNATTACHED_MNL_DRFT_DTL table

created : 09/12/2014 jxc517 CCN Project . . .
changed :
*****************************************************************************/
    IN_RECORD IN    UNATTACHED_MNL_DRFT_DTL%ROWTYPE)
IS
    mnl_drft_check_nbr_err EXCEPTION;
BEGIN
    IF NVL(LENGTH(IN_RECORD.CHECK_SERIAL_NUMBER),0) <> 4  THEN
        RAISE mnl_drft_check_nbr_err;
    END IF;
EXCEPTION
    WHEN mnl_drft_check_nbr_err THEN
        errpkg.raise_err(errnums.en_mnl_drft_check_nbr_err, 
                         'Size of the draft check number should be exactly 4 characters');
    WHEN OTHERS THEN 
        ERRPKG.RAISE_ERR(SQLCODE, 'SD_BUSINESS_RULES_PKG.UNATTACHED_MNL_DRFT_DTL_SP, ', SUBSTR(SQLERRM,1,200));	
END UNATTACHED_MNL_DRFT_DTL_SP;

PROCEDURE STORE_DRAFTS_SP(
/*****************************************************************************
	STORE_DRAFTS_SP

	This procedure will do the bussiness rules validations for STORE_DRAFTS table

created : 09/12/2014 jxc517 CCN Project . . .
changed :
*****************************************************************************/
    IN_RECORD IN    STORE_DRAFTS%ROWTYPE)
IS
    pay_indicator_set_err EXCEPTION;
    V_PAY_INDICATOR       STORE_DRAFTS.PAY_INDICATOR%TYPE;
BEGIN
    BEGIN
        SELECT PAY_INDICATOR
          INTO V_PAY_INDICATOR
          FROM STORE_DRAFTS
         WHERE CHECK_SERIAL_NUMBER = IN_RECORD.CHECK_SERIAL_NUMBER;
    EXCEPTION
        WHEN OTHERS THEN
            --Not found the record in store_drafts table, so might be unattached manual draft
            BEGIN
                SELECT PAY_INDICATOR
                  INTO V_PAY_INDICATOR
                  FROM UNATTACHED_MNL_DRFT_DTL
                 WHERE CHECK_SERIAL_NUMBER = IN_RECORD.CHECK_SERIAL_NUMBER;
            EXCEPTION
                WHEN OTHERS THEN
                    NULL;
            END;
    END;
    IF  NVL(V_PAY_INDICATOR,'N') = 'Y' THEN
        RAISE pay_indicator_set_err;
    END IF;
EXCEPTION
    WHEN pay_indicator_set_err THEN
        errpkg.raise_err(errnums.en_pay_indicator_set_err, 
                         'Draft with pay indicator already set can not be updated');
    WHEN OTHERS THEN 
        ERRPKG.RAISE_ERR(SQLCODE, 'SD_BUSINESS_RULES_PKG.STORE_DRAFTS_SP, ', SUBSTR(SQLERRM,1,200));	
END STORE_DRAFTS_SP;

PROCEDURE SD_BANK_FILE_SENT_DETAILS_SP(
/*****************************************************************************
	SD_BANK_FILE_SENT_DETAILS_SP

	This procedure will do the bussiness rules validations for SD_BANK_FILE_SENT_DETAILS table

created : 09/29/2014 jxc517 CCN Project . . .
changed : 12/10/2014 jxc517 CCN Project . . .
          Modified logic to treat manual drafts differently from regular drafts
          Multiple maintenance on manual draft on the same day is allowed, with warning
          Multiple maintenance on regular draft on the same day is NOT allowed, and a error will be raised
          12/12/2014 axk326 CCN Project . . .
          Modified logic in STORE_DRAFTS_I_SP() to save the change amount in regular drafts though they were
          maintained more than once on the same day. . . 
*****************************************************************************/
    IN_COST_CENTER_CODE    IN    SD_BANK_FILE_SENT_DETAILS.COST_CENTER_CODE%TYPE,
    IN_CHECK_SERIAL_NUMBER IN    SD_BANK_FILE_SENT_DETAILS.CHECK_SERIAL_NUMBER%TYPE)
IS
    mntnc_not_allwd_twice_err EXCEPTION;
    V_COUNT                   NUMBER := 0;
BEGIN

    BEGIN
        SELECT COUNT(*)
          INTO V_COUNT
          FROM SD_BANK_FILE_SENT_DETAILS
         WHERE COST_CENTER_CODE    = IN_COST_CENTER_CODE
           AND CHECK_SERIAL_NUMBER = IN_CHECK_SERIAL_NUMBER
           AND TRUNC(PROCESS_DATE) = TRUNC(SYSDATE);
    EXCEPTION
        WHEN OTHERS THEN
            NULL;
    END;
    IF V_COUNT > 0 THEN
         --If manual draft, regular draft and maintained twice on the same day as it is created => WARNING 
         ERRPKG.STORE_WARNINGS(errnums.en_mntnc_not_allwd_twice_wrn,
                               'Same draft can not be maintained twice on a single day');
    END IF;
EXCEPTION

    WHEN OTHERS THEN 
        ERRPKG.RAISE_ERR(SQLCODE, 'SD_BUSINESS_RULES_PKG.SD_BANK_FILE_SENT_DETAILS_SP, ', SUBSTR(SQLERRM,1,200));	
END SD_BANK_FILE_SENT_DETAILS_SP;

PROCEDURE SD_GL_ACCOUNT_VALIDATION(
/*****************************************************************************
	SD_GL_ACCOUNT_VALIDATION

	This procedure will do the bussiness rules validations for GL_ACCOUNT_NUMBER

created : 11/04/2014 jxc517 CCN Project . . .
changed :
*****************************************************************************/
    IN_GL_ACCOUNT_NUMBER    IN    VARCHAR2)
IS
    gl_prime_accnt_nbr_err    EXCEPTION;
    gl_sub_accnt_nbr_err      EXCEPTION;
    V_COUNT                   NUMBER := 0;
BEGIN
    IF IN_GL_ACCOUNT_NUMBER IS NOT NULL AND INSTR(IN_GL_ACCOUNT_NUMBER,'-') > 0 THEN
    --Do the below validatoin only if GL_ACCOUNT_NUMBER is passed as xxxx-xxx and is not null
        IF LENGTH(SUBSTR(IN_GL_ACCOUNT_NUMBER,1,INSTR(IN_GL_ACCOUNT_NUMBER,'-')-1)) <> 4 THEN
            RAISE gl_prime_accnt_nbr_err;
        ELSIF LENGTH(SUBSTR(IN_GL_ACCOUNT_NUMBER,INSTR(IN_GL_ACCOUNT_NUMBER,'-')+1)) <> 3 THEN
            RAISE gl_sub_accnt_nbr_err;
        END IF;
    END IF;
EXCEPTION
    WHEN gl_prime_accnt_nbr_err THEN
        errpkg.raise_err(errnums.en_gl_prime_accnt_nbr_err, 
                         'GL Prime Account Number should be of 4 characters');
    WHEN gl_sub_accnt_nbr_err THEN
        errpkg.raise_err(errnums.en_gl_sub_accnt_nbr_err, 
                         'GL Prime Account Number should be of 3 characters');
    WHEN OTHERS THEN 
        ERRPKG.RAISE_ERR(SQLCODE, 'SD_BUSINESS_RULES_PKG.SD_GL_ACCOUNT_VALIDATION, ', SUBSTR(SQLERRM,1,200));	
END SD_GL_ACCOUNT_VALIDATION;

PROCEDURE DUPLICATE_STORE_DRAFTS_SP(
/*****************************************************************************
	This procedure will determine if a draft is duplciate or not during insert process from UI

created : 12/30/2014 jxc517 CCN Project . . .
changed :
*****************************************************************************/
    IN_CHECK_SERIAL_NUMBER IN    STORE_DRAFTS.CHECK_SERIAL_NUMBER%TYPE)
IS
    dup_store_draft_err EXCEPTION;
    V_COUNT             NUMBER := 0;
BEGIN
    BEGIN
        SELECT COUNT(*)
          INTO V_COUNT
          FROM STORE_DRAFTS
         WHERE CHECK_SERIAL_NUMBER = IN_CHECK_SERIAL_NUMBER;
    EXCEPTION
        WHEN OTHERS THEN
            --Not found the record in store_drafts table, so might be unattached manual draft
            BEGIN
                SELECT COUNT(*)
                  INTO V_COUNT
                  FROM UNATTACHED_MNL_DRFT_DTL
                 WHERE CHECK_SERIAL_NUMBER = IN_CHECK_SERIAL_NUMBER;
            EXCEPTION
                WHEN OTHERS THEN
                    NULL;
            END;
    END;
    IF  NVL(V_COUNT,0) > 0 THEN
        RAISE dup_store_draft_err;
    END IF;
EXCEPTION
    WHEN dup_store_draft_err THEN
        errpkg.raise_err(errnums.en_dup_store_draft_err, 
                         'Draft already exists in the system, please check the draft number once');
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'SD_BUSINESS_RULES_PKG.DUPLICATE_STORE_DRAFTS_SP, ', SUBSTR(SQLERRM,1,200));	
END DUPLICATE_STORE_DRAFTS_SP;

END SD_BUSINESS_RULES_PKG;

