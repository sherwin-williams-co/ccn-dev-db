create or replace package body CCN_MARKETING_SQ_FT_LOAD AS 
/****************************************************************************** 
This package is intended update the data in Marketing table 

Created : 12/05/2017 axt754 -- CCN Project Team...
Changed : 
******************************************************************************/

PROCEDURE UPDATE_MARKETING(
/****************************************************************************** 
This procedures takes care of following things 
  # Updates the Marketing table with TOTAL_SQ_FT,SALES_SQ_FT and WAREHOUSE_SQ_FT
  # This will update all rows in marketing for a given COST_CENTER_CODE irrespective
    of EFFECTIVE_DATE

Created : 12/05/2017 axt754 -- CCN Project Team
Changes :
******************************************************************************/
IN_COST_CENTER_CODE   IN MARKETING.COST_CENTER_CODE%TYPE
,IN_TOTAL_SQ_FT       IN MARKETING.TOTAL_SQ_FT%TYPE
,IN_SALES_SQ_FT       IN MARKETING.SALES_SQ_FT%TYPE)
IS 
BEGIN
     UPDATE MARKETING
        SET TOTAL_SQ_FT = IN_TOTAL_SQ_FT
            ,SALES_SQ_FT = IN_SALES_SQ_FT
            ,WAREHOUSE_SQ_FT = IN_TOTAL_SQ_FT - IN_SALES_SQ_FT
      WHERE UPPER(COST_CENTER_CODE) = UPPER(IN_COST_CENTER_CODE);
EXCEPTION
    WHEN OTHERS THEN
      DBMS_OUTPUT.PUT_LINE(SQLCODE ||' UPDATE_MARKETING ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
      ERRPKG.RAISE_ERR(SQLCODE, 'UPDATE_MARKETING ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
END UPDATE_MARKETING;

PROCEDURE PROCESS_SQ_FT_LD(
/****************************************************************************** 
This procedures takes care of following things as part of batch load process from UI
  # Get the data from UI
  # Upload the file on DB SERVER
  # call the batch process

Created : 12/05/2017 axt754 -- CCN Project Team
Changes :
******************************************************************************/
IN_BLOB            IN       BLOB)
IS 
    V_FILE_NM   VARCHAR2(100)   := 'CCN_MARKETING_SQ_FT_UPD.csv';
    V_ARC_FILE_NM VARCHAR2(100) := 'CCN_MARKETING_SQ_FT_UPD_'||TO_CHAR(SYSDATE,'RRRRMMDDHHMISS')||'.csv';
BEGIN
    -- Upload file to DB server
    CCN_BATCH_PKG.FILE_UPLOAD(IN_BLOB,V_FILE_NM);
    
    -- Start Batch Process
    FOR REC IN (SELECT *
                  FROM CCN_MARKETING_SQ_FT_UPD) LOOP
        UPDATE_MARKETING(REC.COST_CENTER_CODE
                         ,REC.TOTAL_SQ_FT
                         ,REC.SALES_SQ_FT);
    END LOOP;        
    -- Commit The transactions 
    COMMIT;
    
    --Rename file on server for history
    CCN_BATCH_PKG.FILE_RENAME(V_FILE_NM,V_ARC_FILE_NM);
EXCEPTION
    WHEN OTHERS THEN
        -- Rename file on server for history
        CCN_BATCH_PKG.FILE_RENAME(V_FILE_NM, V_ARC_FILE_NM);
        ERRPKG.RAISE_ERR(SQLCODE, 'PROCESS_SQ_FT_LD ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
END PROCESS_SQ_FT_LD;

PROCEDURE BATCH_PROCESS_SQ_FT_LD
/****************************************************************************** 
This procedures takes care of following things as part of batch load process
  # Get the data from SWC_PN_SQFT_INT_V
  # Compares the data
  # updates the data if there are any changes

Created : 02/09/2018 axt754 -- CCN Project Team
Changes :
******************************************************************************/
IS 
    V_SALES_SQ_FT MARKETING.SALES_SQ_FT%TYPE;
    V_TOTAL_SQ_FT MARKETING.TOTAL_SQ_FT%TYPE;
    V_COST_CENTER COST_CENTER%ROWTYPE;
    V_CLOB        CLOB;
BEGIN
    
    -- Start Batch Process
    FOR REC IN (SELECT *
                  FROM SWC_PN_SQFT_INT_V) LOOP
        V_COST_CENTER := NULL;
        IF REC.LEASE_OR_OWNED IS NOT NULL THEN
            BEGIN
                SELECT *
                  INTO V_COST_CENTER
                  FROM COST_CENTER
                 WHERE COST_CENTER_CODE = REC.COST_CENTER;
            EXCEPTION
                WHEN OTHERS THEN
                    NULL;
            END;
            IF NVL(V_COST_CENTER.COST_CENTER_CODE,'XXX') = 'XXX' THEN
                V_CLOB := REC.COST_CENTER ||CHR(10)|| V_CLOB;
            END IF;
            -- Update Lease own code
            BEGIN
                IF (NVL(V_COST_CENTER.LEASE_OWN_CODE,'X') <> REC.LEASE_OR_OWNED) THEN            
                    UPDATE COST_CENTER
                       SET LEASE_OWN_CODE = REC.LEASE_OR_OWNED
                     WHERE COST_CENTER_CODE = REC.COST_CENTER;
                END IF;
            EXCEPTION
                WHEN OTHERS THEN
                    NULL;
            END;
            
            BEGIN
                SELECT SALES_SQ_FT, TOTAL_SQ_FT
                  INTO V_SALES_SQ_FT,V_TOTAL_SQ_FT
                  FROM MARKETING
                 WHERE COST_CENTER_CODE = REC.COST_CENTER
                   AND EXPIRATION_DATE IS NULL;
            EXCEPTION
                WHEN OTHERS THEN
                   NULL;
            END;
            
            IF (NVL(V_SALES_SQ_FT,-1) <> NVL(REC.SALES_AREA,-1)
            AND NVL(V_TOTAL_SQ_FT,-1) <> NVL(REC.PREMISES_SQ_FT,-1)) THEN
                UPDATE_MARKETING(REC.COST_CENTER
                                 ,REC.PREMISES_SQ_FT
                                 ,REC.SALES_AREA);
            END IF;
        END IF;
    END LOOP;
    
    IF V_CLOB <> EMPTY_CLOB() THEN
        V_CLOB := 'COST_CENTER_CODE'||CHR(10)||V_CLOB;
        MAIL_PKG.SEND_MAIL('SQ_FT_CC_NOT_EXISTS_IN_CCN', NULL, NULL,V_CLOB);
    END IF;
    
    -- Commit The transactions 
    COMMIT;
    
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'BATCH_PROCESS_SQ_FT_LD ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
END BATCH_PROCESS_SQ_FT_LD;

END CCN_MARKETING_SQ_FT_LOAD;