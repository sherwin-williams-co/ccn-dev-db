create or replace PACKAGE BODY CCN_DESCARTES_PROCESS AS

FUNCTION GET_STORE_MGR_DTLS(
/**********************************************************
This function will return the store manager for the cost center passed in
for Dispatch Temrinals it returns Home Store Managers details

Created : 10/19/2017 jxc517 CCN Project Team...
Changed : 10/31/2017 jxc517 CCN Project Team...
          Modified query to pass the home store as input instead of actual driver store
**********************************************************/
IN_COST_CENTER_CODE     IN       VARCHAR2)
RETURN EMPLOYEE_DETAILS%ROWTYPE
IS
    V_RETURN_VAL EMPLOYEE_DETAILS%ROWTYPE;
BEGIN
    SELECT *
      INTO V_RETURN_VAL
      FROM (SELECT EMP.*
              FROM EMPLOYEE_DETAILS EMP
             WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
               AND  UPPER(EMP_PAYROLL_STATUS) = 'ACTIVE'
               AND UPPER(JOB_TITLE) IN (SELECT DISTINCT UPPER(JOB_TITLE) 
                                          FROM JOB_TITLE_GROUP 
                                         WHERE NONMGR_IND = 'Y'
                                           AND JOB_GRP_EXCLD = 'N')
             ORDER BY HIRE_DATE)
         WHERE ROWNUM < 2;
    RETURN V_RETURN_VAL;
EXCEPTION
    WHEN OTHERS THEN
         RETURN V_RETURN_VAL;
END GET_STORE_MGR_DTLS;

PROCEDURE INS_DESCARTES_HRCHY_FEED(
/**********************************************************
This procedure will insert record into the DESCARTES_HIERARCHY_FEED table
to track the control sequence numbers being sent

Created : 10/31/2017 jxc517 CCN Project Team...
Changed : 
**********************************************************/
    OUT_CTRL_SEQ_NUMBER      OUT       NUMBER)
IS
BEGIN
    INSERT INTO DESCARTES_HIERARCHY_FEED VALUES (
                         (SELECT NVL(MAX(CTRL_SEQ_NBR),0)+1 FROM DESCARTES_HIERARCHY_FEED),  --CTRL_SEQ_NBR
                          NULL,                                                              --FEED_CLOB
                          SYSDATE                                                            --PROCESS_DATE
                          )
         RETURNING CTRL_SEQ_NBR INTO OUT_CTRL_SEQ_NUMBER;
END INS_DESCARTES_HRCHY_FEED;

PROCEDURE UPD_DESCARTES_HRCHY_FEED(
/**********************************************************
This procedure will update DESCARTES_HIERARCHY_FEED table
with the clob being sent

Created : 10/31/2017 jxc517 CCN Project Team...
Changed : 
**********************************************************/
    IN_CTRL_SEQ_NUMBER      IN       NUMBER,
    IN_FEED_CLOB            IN       CLOB)
IS
BEGIN
        UPDATE DESCARTES_HIERARCHY_FEED
           SET FEED_CLOB    = IN_FEED_CLOB
         WHERE CTRL_SEQ_NBR = IN_CTRL_SEQ_NUMBER;
END UPD_DESCARTES_HRCHY_FEED;

PROCEDURE GENERATE_DESCARTES_HRCHY_FILE
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 09/28/2017 rxa457 CCN project Team...
           Time format changed to hh24 on the header as per the document
        : 10/19/2017 nxk927/jxc517 CCN Project Team...
          Removed the cursor that was getting the manager details.
          Getting the manager details from the function GET_STORE_MGR_DTLS
        : 10/25/2017 nxk927 CCN Project Team...
          Hard coded the values for SENDER_ID, RECEIVER_ID and DOC_TYPE as per Descates
Changed : 10/31/2017 jxc517 CCN Project Team...
          Modified code to add comments, fix home store issue for territory/dispatch terminals
Changed : 11/7/2017 jxc517 CCN Project Team...
          As per email from 11/6/2017, we are supposed to send home stores hierarchy details and
          not actual dispatch/terminal stores hierarchy details
        : 11/09/2017 nxk927 CCN Project Team...
          Restricting the drivers with the Dispatch terminal cost centers only.
          Hierarchy details needs to be based on the home store
        : 03/20/2019 ASP-1207 mxs216 CCN Project....
          Updated varibale declaration without referencing BATCH_JOB table columns.
**********************************************************/
IS
    CURSOR driver_cur IS
        SELECT ED.*,
               C.CATEGORY
          FROM EMPLOYEE_DETAILS ED,
               COST_CENTER C
         WHERE UPPER(ED.EMP_PAYROLL_STATUS)    = 'ACTIVE'
           AND UPPER(ED.JOB_TITLE)             = 'DRIVER'
           AND C.CATEGORY                      = 'D'
           AND ED.COST_CENTER_CODE             = C.COST_CENTER_CODE
           AND C.COUNTRY_CODE                  IN ('USA','CAN','PRI')
          ORDER BY EMPLOYEE_NAME;

    CURSOR glbl_hrchy_cur(IN_COST_CENTER_CODE    IN    VARCHAR2) IS
        SELECT *
          FROM GLOBAL_HIERARCHY_ATTRBT_VW
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;

    PATH                      VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
    FILENAME                  VARCHAR2(50) := 'DESCARTES_HRCHY_FEED_' || TO_CHAR(SYSDATE,'MMDDRRRR') || '.TXT';
    OUTPUT_FILE               UTL_FILE.FILE_TYPE;
    V_CLOB                    CLOB;
    V_DELIMITER               VARCHAR2(1) := '|';
    V_GLBL_HRCHY_REC          glbl_hrchy_cur%ROWTYPE;
    V_HOME_STR_MGR_REC        EMPLOYEE_DETAILS%ROWTYPE;
    V_DRIVER_EMAIL            EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_HOME_STORE_MGR_EMAIL    EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_HOME_STORE_EMAIL        EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_CITY_MGR_EMAIL          EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_DISTRICT_MGR_EMAIL      EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_AREA_MGR_EMAIL          EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_DIVISION_MGR_EMAIL      EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_GRP_MGR_EMAIL           EMPLOYEE_DETAILS.EMPLOYEE_EMAIL_ADDRESS%TYPE;
    V_DRIVER_USER_ID          EMPLOYEE_DETAILS.USER_ID%TYPE;
    V_HOME_STR_MGR_USER_ID    EMPLOYEE_DETAILS.USER_ID%TYPE;
    V_CITY_MGR_USER_ID        EMPLOYEE_DETAILS.USER_ID%TYPE;
    V_DISTRICT_MGR_USER_ID    EMPLOYEE_DETAILS.USER_ID%TYPE;
    V_AREA_MGR_USER_ID        EMPLOYEE_DETAILS.USER_ID%TYPE;
    V_DIVISION_MGR_USER_ID    EMPLOYEE_DETAILS.USER_ID%TYPE;
    V_GRP_MGR_USER_ID         EMPLOYEE_DETAILS.USER_ID%TYPE;
    V_SENDER_ID               VARCHAR2(100) := '004206397';
    V_RECEIVER_ID             VARCHAR2(100) := '004206397';
    V_DOC_TYPE                VARCHAR2(100) := '816';
    V_FEED_CLOB               CLOB;
    V_GROUP_MGR_NAME          VARCHAR2(3000);
    V_GROUP_MGR_GEMS_ID       VARCHAR2(20);
    V_CTRL_SEQ_NBR            NUMBER;
    V_HOME_STORE              COST_CENTER.COST_CENTER_CODE%TYPE;
    V_BATCH_NUMBER            NUMBER;
    V_TRANS_STATUS            VARCHAR2(20) := 'SUCCESSFUL';
    V_BATCH_JOB_STATUS        VARCHAR2(20) := 'PROCESSING';
BEGIN
    --Log the batch start time
    CCN_BATCH_PKG.INSERT_BATCH_JOB('DESCARTES_HRCHY_FEED', V_BATCH_NUMBER);
    CCN_BATCH_PKG.LOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;

    BEGIN
        --Insert record into DESCARTES_HIERARCHY_FEED to hold the control sequence number for future runs
        INS_DESCARTES_HRCHY_FEED(V_CTRL_SEQ_NBR);

        OUTPUT_FILE := UTL_FILE.FOPEN (PATH
                                      ,FILENAME
                                      , 'W' --BINARY
                                      , 32767);

        --Descartes Header information to identify the source company
        V_CLOB := 
                  --DFH Header (hard coded as requested by Descartes)
                  'DFH'                       || V_DELIMITER ||
                  --Syntax Version (hard coded but need to confirm which one to use 001 or 002 ?)
                  '001'                       || V_DELIMITER ||
                  --Sender Identifier (hard coded as requested by Descartes)
                  V_SENDER_ID                 || V_DELIMITER ||
                  --Receiver Identifier (hard coded as requested by Descartes)
                  V_RECEIVER_ID               || V_DELIMITER ||
                  --Date
                  TO_CHAR(SYSDATE,'YYYYMMDD') || V_DELIMITER ||
                  --Time
                  TO_CHAR(SYSDATE,'HH24MISS') || V_DELIMITER ||
                  --Control number (SEQUENCE)***
                  V_CTRL_SEQ_NBR              || V_DELIMITER ||
                  --Doc Type (hard coded as requested by Descartes)
                  V_DOC_TYPE                  || V_DELIMITER ||
                  --future_use (hard coded as requested by Descartes)
                  '0'                         || V_DELIMITER ||
                  --test_indicator.. can be T(test) or P (production)
                  CASE
                      WHEN SYS_CONTEXT('USERENV','DB_NAME') = 'STCCNP' THEN
                          'P'
                      ELSE
                          'T'
                  END                         || V_DELIMITER ||
                  --DFH format L(labeled), T(tagged) and unlabeled (anything else)?
                  'L'
                  ;

        V_FEED_CLOB := V_CLOB;

        UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);

        --Fields Headers for Descartes to identify the values populated in the file
        V_CLOB := 'DriverSHWID'                       || V_DELIMITER ||  --Driver SHWID
                  'DriverName'                        || V_DELIMITER ||  --Driver Name
                  'StoreNumber'                       || V_DELIMITER ||  --Driver Cost Center (Dispatch Terminal Home Store)
                  'StoreManagerSHWID'                 || V_DELIMITER ||  --Home Store Manager SHWID
                  'StoreManagerName'                  || V_DELIMITER ||  --Home Store Manager Name
                  'StoreEmail'                        || V_DELIMITER ||  --Home Store Email ID
                  'CityCode'                          || V_DELIMITER ||  --City
                  'CityDesc'                          || V_DELIMITER ||  --City Description
                  'CityManagerName'                   || V_DELIMITER ||  --City Manager Name
                  'CityManagerSHWID'                  || V_DELIMITER ||  --City Manager SHWID
                  'CityManagerEmail'                  || V_DELIMITER ||  --City Manager Email ID
                  'DistrictCode'                      || V_DELIMITER ||  --District
                  'DistrictDesc'                      || V_DELIMITER ||  --District Description
                  'DistrictManagerName'               || V_DELIMITER ||  --District Manager Name
                  'DistrictManagerSHWID'              || V_DELIMITER ||  --District Manager SHWID
                  'DistrictManagerEmail'              || V_DELIMITER ||  --District Manager Email ID
                  'AreaCode'                          || V_DELIMITER ||  --Area
                  'AreaDesc'                          || V_DELIMITER ||  --Area Description
                  'AreaManagerName'                   || V_DELIMITER ||  --Area Manager Name
                  'AreaManagerSHWID'                  || V_DELIMITER ||  --Area Manager SHWID
                  'AreaManagerEmail'                  || V_DELIMITER ||  --Area Manager Email ID
                  'DivisionCode'                      || V_DELIMITER ||  --Division
                  'DivisionDesc'                      || V_DELIMITER ||  --Division Description
                  'DivisionManagerName'               || V_DELIMITER ||  --Division Manager Name
                  'DivisionManagerSHWID'              || V_DELIMITER ||  --Division Manager SHWID
                  'DivisionManagerEmail'              || V_DELIMITER ||  --Division Manager Email ID
                  'GroupCode'                         || V_DELIMITER ||  --Group
                  'GroupDesc'                         || V_DELIMITER ||  --Group Description
                  'GroupManagerName'                  || V_DELIMITER ||  --Group Manager Name will always be blank
                  'GroupManagerSHWID'                 || V_DELIMITER ||  --Group Manager SHWID
                  'GroupManagerEmail'                                    --Group Manager Email ID
                  ;
        V_FEED_CLOB := V_FEED_CLOB || CHR(10)|| V_CLOB;

        UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);

        --Loop through all the drivers we have from GEMS and start generating the pipe delimited file
        FOR rec IN driver_cur LOOP
            --Get the home store for the drivers cost center
            V_HOME_STORE       := NVL(COMMON_TOOLS.GET_HOMESTORE(rec.COST_CENTER_CODE, rec.CATEGORY), rec.COST_CENTER_CODE);

            --need to get the manager based on drivers home store
            V_HOME_STR_MGR_REC := GET_STORE_MGR_DTLS(V_HOME_STORE);

            --Need to get the home store hierarchy details instead of dispatch terminal/territory hierarchy details
            FOR rec1 IN glbl_hrchy_cur(V_HOME_STORE) LOOP
                V_GLBL_HRCHY_REC := rec1;
            END LOOP;

            V_GROUP_MGR_NAME    := CCN_HIERARCHY.GET_RQSTD_ATTRIBUTE_VALUE(V_GLBL_HRCHY_REC.GROUP_UPPER_VALUE, 'ManagerName');
            V_GROUP_MGR_GEMS_ID := CCN_HIERARCHY.GET_RQSTD_ATTRIBUTE_VALUE(V_GLBL_HRCHY_REC.GROUP_UPPER_VALUE, 'GEMS_ID');

            --as per 11/10/2017 meeting decided to send as sw(4 digit store)#@sherwin.com
            --If its a store there wont be home store, so we need to pass that as it is
            --Get Driver details
            CCN_EMPLOYEE_DETAILS_PKG.GET_EMPLOYEE_DTLS(rec.EMPLOYEE_NUMBER,V_DRIVER_EMAIL, V_DRIVER_USER_ID );

            --Get Store Manager details
            CCN_EMPLOYEE_DETAILS_PKG.GET_EMPLOYEE_DTLS(V_HOME_STR_MGR_REC.EMPLOYEE_NUMBER, V_HOME_STORE_MGR_EMAIL, V_HOME_STR_MGR_USER_ID );

            --Get City Manager details
            CCN_EMPLOYEE_DETAILS_PKG.GET_EMPLOYEE_DTLS(V_GLBL_HRCHY_REC.CITY_MGR_GEMS_ID,V_CITY_MGR_EMAIL, V_CITY_MGR_USER_ID );

            --Get District Manager details
            CCN_EMPLOYEE_DETAILS_PKG.GET_EMPLOYEE_DTLS(V_GLBL_HRCHY_REC.DISTRICT_MGR_GEMS_ID,V_DISTRICT_MGR_EMAIL,V_DISTRICT_MGR_USER_ID);

            --Get Area Manager details
            CCN_EMPLOYEE_DETAILS_PKG.GET_EMPLOYEE_DTLS(V_GLBL_HRCHY_REC.AREA_MGR_GEMS_ID,V_AREA_MGR_EMAIL,V_AREA_MGR_USER_ID );

            --Get Division Manager details
            CCN_EMPLOYEE_DETAILS_PKG.GET_EMPLOYEE_DTLS(V_GLBL_HRCHY_REC.DIV_MGR_GEMS_ID, V_DIVISION_MGR_EMAIL, V_DIVISION_MGR_USER_ID);

            --Get Group Manager details
            CCN_EMPLOYEE_DETAILS_PKG.GET_EMPLOYEE_DTLS(V_GROUP_MGR_GEMS_ID,V_GRP_MGR_EMAIL, V_GRP_MGR_USER_ID);

            V_HOME_STORE_EMAIL   := 'sw' || SUBSTR(V_HOME_STORE,3) || '@sherwin.com';

            V_CLOB := V_DRIVER_USER_ID                           || V_DELIMITER ||  --Driver SHWID
                      rec.EMPLOYEE_NAME                          || V_DELIMITER ||  --Driver Name
                      --as per 11/10/2017 meeting this should be dispatch terminals home store
                      --as per 11/10/2017 meeting DAD should still be actual dispatch terminals DAD and not home stores DAD
                      --If its a store there wont be home store, so we need to pass that as it is
                      V_HOME_STORE                               || V_DELIMITER ||  --Driver Cost Center (Dispatch Terminal Home Store)
                      V_HOME_STR_MGR_USER_ID                     || V_DELIMITER ||  --Home Store Manager SHWID
                      V_HOME_STR_MGR_REC.EMPLOYEE_NAME           || V_DELIMITER ||  --Home Store Manager Name
                      V_HOME_STORE_EMAIL                         || V_DELIMITER ||  --Home Store Email ID
                      V_GLBL_HRCHY_REC.CITY_SALES_MANAGER        || V_DELIMITER ||  --City
                      V_GLBL_HRCHY_REC.CITY_SALES_MANAGER_NAME   || V_DELIMITER ||  --City Description
                      V_GLBL_HRCHY_REC.CITY_MGR_NAME             || V_DELIMITER ||  --City Manager Name
                      V_CITY_MGR_USER_ID                         || V_DELIMITER ||  --City Manager SHWID
                      V_CITY_MGR_EMAIL                           || V_DELIMITER ||  --City Manager Email ID
                      V_GLBL_HRCHY_REC.DISTRICT                  || V_DELIMITER ||  --District
                      V_GLBL_HRCHY_REC.DISTRICT_NAME             || V_DELIMITER ||  --District Description
                      V_GLBL_HRCHY_REC.DISTRICT_MGR_NAME         || V_DELIMITER ||  --District Manager Name
                      V_DISTRICT_MGR_USER_ID                     || V_DELIMITER ||  --District Manager SHWID
                      V_DISTRICT_MGR_EMAIL                       || V_DELIMITER ||  --District Manager Email ID
                      V_GLBL_HRCHY_REC.AREA                      || V_DELIMITER ||  --Area
                      V_GLBL_HRCHY_REC.AREA_NAME                 || V_DELIMITER ||  --Area Description
                      V_GLBL_HRCHY_REC.AREA_MGR_NAME             || V_DELIMITER ||  --Area Manager Name
                      V_AREA_MGR_USER_ID                         || V_DELIMITER ||  --Area Manager SHWID
                      V_AREA_MGR_EMAIL                           || V_DELIMITER ||  --Area Manager Email ID
                      V_GLBL_HRCHY_REC.DIVISION                  || V_DELIMITER ||  --Division
                      V_GLBL_HRCHY_REC.DIVISION_NAME             || V_DELIMITER ||  --Division Description
                      V_GLBL_HRCHY_REC.DIV_MGR_NAME              || V_DELIMITER ||  --Division Manager Name
                      V_DIVISION_MGR_USER_ID                     || V_DELIMITER ||  --Division Manager SHWID
                      V_DIVISION_MGR_EMAIL                       || V_DELIMITER ||  --Division Manager Email ID
                      V_GLBL_HRCHY_REC."GROUP"                   || V_DELIMITER ||  --Group
                      V_GLBL_HRCHY_REC.GROUP_NAME                || V_DELIMITER ||  --Group Description
                      V_GROUP_MGR_NAME                           || V_DELIMITER ||  --Group Manager Name will always be blank
                      V_GRP_MGR_USER_ID                          || V_DELIMITER ||  --Group Manager SHWID
                      V_GRP_MGR_EMAIL                                               --Group Manager Email ID
                      ;

            --write the data to the file one record at a time
            IF V_CLOB <> EMPTY_CLOB() THEN
                V_FEED_CLOB := V_FEED_CLOB || CHR(10)|| V_CLOB;
                UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
            END IF;
            V_CLOB := NULL;
        END LOOP;

        UTL_FILE.FCLOSE(OUTPUT_FILE);

        --Save the clob for our future reference
        UPD_DESCARTES_HRCHY_FEED(V_CTRL_SEQ_NBR, V_FEED_CLOB);
    EXCEPTION
       WHEN OTHERS THEN
          UTL_FILE.FCLOSE(OUTPUT_FILE);
          ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CCN_DESCARTES_PROCESS.GENERATE_DESCARTES_HRCHY_FILE', SUBSTR(SQLERRM,1,500), NULL);
          V_TRANS_STATUS := 'ERROR';
          CCN_BATCH_PKG.UPDATE_BATCH_JOB('DESCARTES_HRCHY_FEED', V_BATCH_NUMBER, V_TRANS_STATUS);
          CCN_BATCH_PKG.UNLOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION; 
          RAISE;
    END;
    CCN_BATCH_PKG.UPDATE_BATCH_JOB('DESCARTES_HRCHY_FEED', V_BATCH_NUMBER, V_TRANS_STATUS);
    CCN_BATCH_PKG.UNLOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION; 
EXCEPTION
    WHEN OTHERS THEN
       RAISE;       
END GENERATE_DESCARTES_HRCHY_FILE;

PROCEDURE BUILD_ADDRESS_DATA_HDR(
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 11/13/2018 jxc517 CCN Project Team...
          Descartes logic deletes data if there is no maitenance on store for 120 days
          So update sent after the delete won't work any more, so sending all records as inserts as requested
**********************************************************/
    IN_COST_CENTER_CODE  IN     VARCHAR2,
    IN_INS_UPD_IND       IN     VARCHAR2,
    OUT_CLOB                OUT CLOB)
IS
    V_PASSWORD        VARCHAR2(100);
    V_USER            VARCHAR2(100);
BEGIN
    --Set the user name and password in the header based on environment
    CCN_COMMON_TOOLS.GET_APPLICATION_CREDENTIALS('DESCARTES', 'ADDRESS_FEED', V_USER, V_PASSWORD);

    --Build the header for Descartes address feed xml
    OUT_CLOB := '<DocFWImport>' ||
                    '<Header '||
                        --hard coded value as provided by Descartes
                        ' EchoData="false"' ||
                        --hard coded value (need to talk about handling this)
                        ' Password="' || V_PASSWORD || '"' ||
                        --hard coded value as provided by Descartes
                        ' LoginName="' || V_USER || '"' ||
                        --hard coded value as provided by Descartes
                        ' CompanyName="SHW"' ||
                        --hard coded value as provided by Descartes
                        ' ReceiverID="RP"' ||
                        --hard coded value as provided by Descartes
                        ' SenderID="CCN"' ||
                        ' SendDateTime="' || TO_CHAR(SYSDATE, 'YY-MM-DD HH24:MI:SS') || '"' ||
                        --1002(create record), 1004(update record)
                        ' ExtDocControlID="' || IN_COST_CENTER_CODE || '_' ||
                             CCN_COMMON_TOOLS.GET_TRANSLATED_CODE_DETAIL_VAL('DSCRTS_CTRL_ID_CD', 'INSERT') || '_' ||
                             TO_CHAR(SYSTIMESTAMP,'YYYYMMDDHH24MMSSFF') ||'"/>' ||
                    '<Request>';
END BUILD_ADDRESS_DATA_HDR;

PROCEDURE BUILD_ADDRESS_DATA_FOR_CC(
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 09/28/2017 rxa457 CCN project Team...
           Time format changed to hh24 on the header as per the document
        : 11/09/2017 nxk927 CCN project Team...
          no need to get the home store as the store being passed will be home store
Created : 01/10/2018 jxc517/sxg151 CCN Project Team...
          Modified code to have below logic:
              Dispatch Terminal Home Store => Type=DEPOT, UDFString6=DISPATCH TERMINAL
              Regular Store => Type=CUSTOMER, UDFString6=STORE
Changed : 11/13/2018 jxc517 CCN Project Team...
          Descartes logic deletes data if there is no maitenance on store for 120 days
          So update sent after the delete won't work any more, so sending all records as inserts as requested
**********************************************************/
    IN_COST_CENTER_CODE  IN     VARCHAR2,
    IN_INS_UPD_IND       IN     VARCHAR2,
    OUT_CLOB                OUT CLOB)
IS
    V_ADDRESS_REC             ADDRESS_VW%ROWTYPE;
    V_COST_CENTER_REC         COST_CENTER%ROWTYPE;
    V_HOME_STR_MGR_REC        EMPLOYEE_DETAILS%ROWTYPE;
    V_PHONE_REC               CCN_PHONE_INFO_VW%ROWTYPE;
    V_HOME_STORE              COST_CENTER.COST_CENTER_CODE%TYPE;
	V_DSPTCH_TRMNL_HOME_STORE_IND VARCHAR2(1) := 'N';
BEGIN
    --Get the address details for the input cost center
    --Note: Address View already handled home store conditions to return home stores address
    BEGIN
        SELECT *
          INTO V_ADDRESS_REC
          FROM ADDRESS_VW
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
           AND ADDRESS_TYPE = 'M'
           AND EXPIRATION_DATE IS NULL;
    EXCEPTION
        WHEN OTHERS THEN
            NULL;
    END;

    --If address is present then proceed further, else there eill be nothing to report in the DocLocation file
    --Note: These details are needed only for USA/PRI/CAN cost centers
    IF V_ADDRESS_REC.COST_CENTER_CODE IS NOT NULL
      AND V_ADDRESS_REC.COUNTRY_CODE IN ('USA', 'PRI', 'CAN') THEN
        --Get cost center details for the input cost center
        BEGIN
            SELECT *
              INTO V_COST_CENTER_REC
              FROM COST_CENTER
             WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;
        EXCEPTION
            WHEN OTHERS THEN
                NULL;
        END;

        --Get home store for the input cost center
        --V_HOME_STORE       := NVL(COMMON_TOOLS.GET_HOMESTORE(IN_COST_CENTER_CODE, V_COST_CENTER_REC.CATEGORY), IN_COST_CENTER_CODE);

        --Get home store managert details
        V_HOME_STR_MGR_REC := GET_STORE_MGR_DTLS(IN_COST_CENTER_CODE);
        BEGIN
            SELECT *
              INTO V_PHONE_REC
              FROM CCN_PHONE_INFO_VW
             WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;
        EXCEPTION
            WHEN OTHERS THEN
                NULL;
        END;

		BEGIN
            --Check if there are any records in dispatch terminal table for this store
            --If present => its a dispatch terminal home store
            SELECT DECODE(COUNT(*), 0, 'N', 'Y')
              INTO V_DSPTCH_TRMNL_HOME_STORE_IND
              FROM DISPATCH_TERMINAL
             WHERE HOME_STORE = IN_COST_CENTER_CODE;
        EXCEPTION
            WHEN OTHERS THEN
                NULL;
        END;
        --Get all the Dispatch Terminal Home Stores for Initial load process

        --Build the DocLocation tag with all required attribtues as requested by Descrates
        OUT_CLOB := '<DocLocation' ||
                        --Cost Center Name
                        ' Company="' || CCN_COMMON_TOOLS.CONVERT_SPECIAL_CHRCTRS(V_COST_CENTER_REC.COST_CENTER_NAME) || '"' ||
                        --Cost Center Name
                        ' Name="' || CCN_COMMON_TOOLS.CONVERT_SPECIAL_CHRCTRS(V_COST_CENTER_REC.COST_CENTER_NAME) || '"' ||
                        --2 digit country code wher the cost center resides, default is US
                        ' Country="' || NVL(CCN_COMMON_TOOLS.GET_TRANSLATED_CODE_DETAIL_VAL('COUNTRY_CODE',V_ADDRESS_REC.COUNTRY_CODE), 'US') || '"' ||
                        --Zip Code for the cost center
                        CASE
                            WHEN V_ADDRESS_REC.COUNTRY_CODE = 'CAN' THEN
                                ' Zip="' || V_ADDRESS_REC.POSTAL_CODE || '"'
                            ELSE
                                ' Zip="' || V_ADDRESS_REC.ZIP_CODE || '"'
                        END ||
                        --State Code for the cost center
                        CASE
                            WHEN V_ADDRESS_REC.COUNTRY_CODE = 'CAN' THEN
                                ' State="' || V_ADDRESS_REC.PROVINCE_CODE || '"'
                            ELSE
                                ' State="' || V_ADDRESS_REC.STATE_CODE || '"'
                        END ||
                        --City where the cost center is located
                        ' City="' || CCN_COMMON_TOOLS.CONVERT_SPECIAL_CHRCTRS(V_ADDRESS_REC.CITY) || '"' ||
                        ' Address2="' || CCN_COMMON_TOOLS.CONVERT_SPECIAL_CHRCTRS(V_ADDRESS_REC.ADDRESS_LINE_2) || '"' ||
                        ' Address="' || CCN_COMMON_TOOLS.CONVERT_SPECIAL_CHRCTRS(V_ADDRESS_REC.ADDRESS_LINE_1) || '"' ||
                        --hard coded value as requested by Descrates
                        ' GeocodingPolicy="2"' ||
                        ' PhoneNumber="' || COMMON_TOOLS.GET_PHONE_NUMBER(IN_COST_CENTER_CODE,'PRI') || '"' ||
			--Type code has been stores under translation table as requested by Descartes
                        --'S' = CUSTOMER/ 'D' = DEPOT <<this no longer holds as per 01/10/2018 meeting with descarets>>
                        --Dispatch Terminal Home Store = 'DEPOT', Regular Store = 'CUSTOMER'
                        CASE
                            WHEN V_DSPTCH_TRMNL_HOME_STORE_IND = 'Y' THEN
                                ' Type="DEPOT"'
                            ELSE
                                ' Type="CUSTOMER"'
                        END ||
                        --hard coded value as requested by Descrates
                        ' ProcessCode="20"' ||
                        --1002(create record), 1004(update record)
                        ' MessagePurpose="' || CCN_COMMON_TOOLS.GET_TRANSLATED_CODE_DETAIL_VAL('DSCRTS_CTRL_ID_CD', 'INSERT') || '"' ||
                        --Cost center category description <<this no longer holds as per 01/10/2018 meeting with descarets>>
                        --Dispatch Terminal Home Store = 'DISPATCH TERMINAL', Regular Store = 'STORE'
                        CASE
                            WHEN V_DSPTCH_TRMNL_HOME_STORE_IND = 'Y' THEN
                                ' UDFString6="DISPATCH TERMINAL"'
                            ELSE
                                ' UDFString6="STORE"'
                        END ||
                        ' ContactFirstName="' || V_HOME_STR_MGR_REC.FIRST_NAME || '"' ||
                        ' ContactLastName="' || V_HOME_STR_MGR_REC.LAST_NAME || '"' ||
                        ' ContactPhone="' || V_PHONE_REC.PRIMARY_PHONE_NUMBER || '"' ||
                        ' ContactEmail="' || 'sw' || SUBSTR(IN_COST_CENTER_CODE,3) || '@sherwin.com' || '"' ||
                        ' LocationRefNo="'|| IN_COST_CENTER_CODE ||'" />';
        --DBMS_OUTPUT.PUT_LINE(V_CLOB);
    END IF;
END BUILD_ADDRESS_DATA_FOR_CC;

PROCEDURE BUILD_ADDRESS_DATA_TRLR(
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 
**********************************************************/
    OUT_CLOB                OUT CLOB)
IS
BEGIN
    OUT_CLOB := '</Request>' ||
              '</DocFWImport>';
END BUILD_ADDRESS_DATA_TRLR;

PROCEDURE INS_DESCARTES_ADRS_FEED_CC(
/**********************************************************
This procedure will insert record into the DESCARTES_ADRS_FEED_CC table
to track the GUID being sent

Created : 10/31/2017 jxc517 CCN Project Team...
Changed : 
**********************************************************/
    IN_COST_CENTER_CODE    IN           VARCHAR2,
    IN_ACTION              IN           VARCHAR2,
    OUT_GUID                  OUT       VARCHAR2)
IS
BEGIN
    INSERT INTO DESCARTES_ADRS_FEED_CC
        (COST_CENTER_CODE,
         TRIGGERED_DATE,
         TRIGGER_ACTION,
         GUID)
    VALUES(IN_COST_CENTER_CODE,
           TRUNC(SYSDATE),
           IN_ACTION,
           REGEXP_REPLACE(RAWTOHEX(SYS_GUID()), '([A-F0-9]{8})([A-F0-9]{4})([A-F0-9]{4})([A-F0-9]{4})([A-F0-9]{12})', '\1-\2-\3-\4-\5'))
    RETURNING GUID INTO OUT_GUID;
END INS_DESCARTES_ADRS_FEED_CC;

PROCEDURE UPD_DESCARTES_ADRS_FEED_CC(
/**********************************************************
This procedure will update UPD_DESCARTES_ADRS_FEED_CC table
with the clob being sent

Created : 10/31/2017 jxc517 CCN Project Team...
Changed : 
**********************************************************/
    IN_COST_CENTER_CODE       IN           VARCHAR2,
    IN_REQUEST                IN           CLOB)
IS
BEGIN
    UPDATE DESCARTES_ADRS_FEED_CC
       SET PROCESS_DATE = SYSDATE,
           REQUEST      = IN_REQUEST
     WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
       AND PROCESS_DATE IS NULL;
END UPD_DESCARTES_ADRS_FEED_CC;

PROCEDURE GNRT_DSCRTS_ADDRESS_INIT_FILE(
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 09/28/2017 rxa457 CCN project Team...
           Time format changed to hh24 on the header as per the document
        : 11/09/2017 nxk927 CCN project Team...
          Only home store should be considered for the address feed
        : 11/13/2017 nxk927 CCN project Team...
          inserting all the store cost center for the initial run.
        : 11/15/2017 nxk927 CCN project Team...
          avoiding duplicate home stores in the initial load xml
Created : 01/10/2018 jxc517/sxg151 CCN Project Team...
          Modified code to include every store in the initial load file
          Also take only cost centers with polling status as P/Q
        : 06/12/2018 pxa852 CCN Project Team....
          EXPIRATION_DATE column name changed to POLL_STATUS_EXP_DT
**********************************************************/
    OUT_CLOB       OUT  CLOB,
    OUT_GUID       OUT  VARCHAR2)
IS
    V_CLOB                    CLOB;
    V_COST_CENTER_CODE        VARCHAR2(6) := 'INITLD';
    V_ACTION                  VARCHAR2(6) := 'INSERT';

    CURSOR store_cur IS
        --Get all the active cost centers in USA/CAN/PRI with category as S
        SELECT COST_CENTER_CODE,
               (SELECT POLLING_STATUS_CODE
                  FROM POLLING P
                 WHERE P.COST_CENTER_CODE = C.COST_CENTER_CODE
                   AND P.POLL_STATUS_EXP_DT IS NULL) AS POLLING_STATUS_CODE
          FROM COST_CENTER C
         WHERE C.CATEGORY = 'S'
           AND C.CLOSE_DATE IS NULL
           AND C.COUNTRY_CODE IN ('USA', 'PRI', 'CAN');
BEGIN
/*
Steps:
1) We need to truncate the trigger/tracking table for initial load
2) Since a store can become a home store at any point just by attaching it to a dispatch terminal,
   we need to store all existing stores as INSERT and Not-Processed
3) Then we need to take all home stores and loop through them and mark them as Processed,
   which helps us identify the subsequent changes as UPDATE requests
4) We need to tie back all initial load processed cost centers with its bulk request GUID
*/
    --Initial load to clean up the trigger/tracking table
    --Do not truncate the data as we need the history stay like that
    --EXECUTE IMMEDIATE 'TRUNCATE TABLE DESCARTES_ADRS_FEED_CC';


    --Insert record into DESCARTES_ADRS_FEED_CC to hold the GUID of the request being sent
    INS_DESCARTES_ADRS_FEED_CC(V_COST_CENTER_CODE, V_ACTION, OUT_GUID);

    --Build the header tags for Descartes DocLocartion xml request
    BUILD_ADDRESS_DATA_HDR(V_COST_CENTER_CODE, V_ACTION, V_CLOB);

    OUT_CLOB := V_CLOB;

    --Insert all store records into DESCARTES_ADRS_FEED_CC table for future reference of INSERT/UPDATE
    --Loop through all the cost centers anf build the DocLocation tags one for each cost center
    FOR rec IN store_cur LOOP
       --Marking home store as processed
       INSERT INTO DESCARTES_ADRS_FEED_CC(COST_CENTER_CODE,
                                          TRIGGERED_DATE,
                                          TRIGGER_ACTION,
                                          GUID)
       VALUES(rec.COST_CENTER_CODE,
              TRUNC(SYSDATE) - 1,
              'INSERT',
              REGEXP_REPLACE(RAWTOHEX(SYS_GUID()), '([A-F0-9]{8})([A-F0-9]{4})([A-F0-9]{4})([A-F0-9]{4})([A-F0-9]{12})', '\1-\2-\3-\4-\5'));

        IF NVL(rec.POLLING_STATUS_CODE, 'X') IN ('P','Q') THEN
            V_CLOB := NULL;
            --Build the DocLocation tag
            BUILD_ADDRESS_DATA_FOR_CC(rec.COST_CENTER_CODE, 'INSERT', V_CLOB);

            IF V_CLOB <> EMPTY_CLOB() THEN
                OUT_CLOB := OUT_CLOB || CHR(10)|| V_CLOB;
            END IF;

            --Update request details for the home store as part of initial load
            --Since the request is not per store, we are referencing using the GUIS of "INITLD" cost center
            UPD_DESCARTES_ADRS_FEED_CC(rec.COST_CENTER_CODE, 'Initial Load : bulk xml can be referenced using GUID : ' || OUT_GUID);
        END IF;
    END LOOP;

    --Build the trailer tags for Descartes DocLocartion xml request
    BUILD_ADDRESS_DATA_TRLR(V_CLOB);
    OUT_CLOB := OUT_CLOB || CHR(10)|| V_CLOB;

    --Save the request XML being sent and mark the initial load as processed
    UPD_DESCARTES_ADRS_FEED_CC(V_COST_CENTER_CODE, OUT_CLOB);
    COMMIT;
EXCEPTION
   WHEN OTHERS THEN  
      ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CCN_DESCARTES_PROCESS.GNRT_DSCRTS_ADDRESS_INIT_FILE', SUBSTR(SQLERRM,1,500), NULL);
      RAISE;
END GNRT_DSCRTS_ADDRESS_INIT_FILE;

PROCEDURE GNRT_DSCRTS_ADDRESS_CC_FILE(
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 
**********************************************************/
    IN_COST_CENTER_CODE  IN      VARCHAR2,
    IN_TRIGGER_ACTION    IN      VARCHAR2,
    OUT_CLOB                OUT  CLOB)
IS
    V_CLOB                    CLOB;
BEGIN
    --Insert record into DESCARTES_ADRS_FEED_CC to hold the GUID of the request being sent
    BUILD_ADDRESS_DATA_HDR(IN_COST_CENTER_CODE, IN_TRIGGER_ACTION, V_CLOB);
    OUT_CLOB := V_CLOB;

    --Build the DocLocation tag
    BUILD_ADDRESS_DATA_FOR_CC(IN_COST_CENTER_CODE, IN_TRIGGER_ACTION, V_CLOB);
    IF V_CLOB <> EMPTY_CLOB() THEN
        OUT_CLOB := OUT_CLOB || V_CLOB;
    END IF;

    --Build the trailer tags for Descartes DocLocartion xml request
    BUILD_ADDRESS_DATA_TRLR(V_CLOB);
    OUT_CLOB := OUT_CLOB || V_CLOB;

    --Save the request XML being sent
    UPD_DESCARTES_ADRS_FEED_CC(IN_COST_CENTER_CODE, OUT_CLOB);
EXCEPTION
   WHEN OTHERS THEN  
      ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CCN_DESCARTES_PROCESS.GNRT_DSCRTS_ADDRESS_CC_FILE', SUBSTR(SQLERRM,1,500), NULL);
      RAISE;
END GNRT_DSCRTS_ADDRESS_CC_FILE;

PROCEDURE DESCARTES_ADRS_FEED_CC_SP(
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 10/18/2017 jxc517 CCN Project Team...
          Based on discussion with Keith we need to send details only for
          Dispatch Terminal and Store category cost centers
        : 11/09/2017 nxk927 CCN Project Team...
          only home store should be considered for the descartes address feed
        : 11/13/2017 nxk927 CCN Project Team...
          Changed the signature to remove the date parameter
          if a store is attached as home store to a dispatch terminal at later point,
          we need to send that cost center as well. We need to send everything that
          is not processed
        : 12/05/2017 jxc517 CCN Project Team...
          Added conditions to consider only active mailing addresses
Created : 01/10/2018 jxc517/sxg151 CCN Project Team...
          consider cost centers with polling status as P/Q
          Also modified mailing address check to consider all stores instead of just home stores
        : 06/12/2018 pxa852 CCN Project Team....
          EXPIRATION_DATE column name changed to POLL_STATUS_EXP_DT
**********************************************************/
    OUT_REF_CUR      OUT  SYS_REFCURSOR)
IS
BEGIN
/*
1) If cost center is added newly and attached to a new dispatch terminal --> INSERT
2) If an existing home store attached to a new dispatch terminal, but no address change on home store --> NOTHING TO SEND
3) If an existing home store attached to a new dispatch terminal, but there is an address change on home store --> UPDATE
4) If existing cost center is attached to a new dispatch terminal --> INSERT
*/
    OPEN OUT_REF_CUR FOR
        --Get all the home store cost centers that got non-processed address added/maintained
        SELECT *
          FROM DESCARTES_ADRS_FEED_CC A
         WHERE COST_CENTER_CODE IN (SELECT C.COST_CENTER_CODE
                                      FROM COST_CENTER C,
                                           ADDRESS_VW AV
                                     WHERE C.COST_CENTER_CODE = A.COST_CENTER_CODE
                                       AND C.CLOSE_DATE IS NULL
                                       AND C.COUNTRY_CODE IN ('USA', 'PRI', 'CAN')
                                       AND C.COST_CENTER_CODE = AV.COST_CENTER_CODE
                                       AND AV.ADDRESS_TYPE = 'M'
                                       AND AV.EXPIRATION_DATE IS NULL)
           AND EXISTS (SELECT 1
                         FROM POLLING P
                        WHERE P.COST_CENTER_CODE = A.COST_CENTER_CODE
                          AND NVL(P.POLLING_STATUS_CODE, 'X') IN ('P','Q')
                          AND P.POLL_STATUS_EXP_DT IS NULL)
           AND PROCESS_DATE IS NULL;
END DESCARTES_ADRS_FEED_CC_SP;

PROCEDURE DESCARTE_ADRS_FEED_RESP_CC_UPD(
/**********************************************************
This process is used to build a pipe delimited hierarchy file for descartes

Created : 09/21/2017 jxc517 CCN Project Team...
Changed : 
**********************************************************/
    IN_GUID       IN      VARCHAR2,
    IN_CLOB       IN      CLOB)
IS
BEGIN
    UPDATE DESCARTES_ADRS_FEED_CC
       SET RESPONSE = IN_CLOB
     WHERE GUID = IN_GUID;
END DESCARTE_ADRS_FEED_RESP_CC_UPD;

PROCEDURE INSERT_DESCARTES_ADRS_FEED_CC (
/**********************************************************
This procedure is intended to insert into INSERT_DESCARTES_ADRS_FEED_CC
table with the details related to the Descartes address details feed run

Created : 10/11/2017 rxa457 CCN project....
Modified: 11/13/2017 nxk927 CCN project....
          Code change to inclide only S category store and only insert record if the old record
          is processed
Modified: 11/15/2017 nxk927 CCN project....
          Modified signature to handle address insert/updates
Modified: 12/05/2017 jxc517 CCN project....
          Modified logic to avoid mutating table error as
          Address table triggers are using this function where we can not reference them
**********************************************************/
    IN_COST_CENTER_CODE            IN  DESCARTES_ADRS_FEED_CC.COST_CENTER_CODE%TYPE)
IS 
    V_DESCARTES_ADRS_FEED_CC DESCARTES_ADRS_FEED_CC%ROWTYPE;
    V_COUNT                  NUMBER := 0;
    V_TRIGGER_ACTION         VARCHAR2(10) := 'INSERT';
    V_CATEGORY               COST_CENTER.CATEGORY%TYPE;
BEGIN
    --Check if the cost center category is "S", if not we do nothing as Descartes cares only about home stores
    BEGIN
        SELECT CATEGORY
          INTO V_CATEGORY
          FROM COST_CENTER C
         WHERE C.COST_CENTER_CODE = IN_COST_CENTER_CODE
           AND C.CLOSE_DATE IS NULL
           AND C.COUNTRY_CODE IN ('USA', 'CAN', 'PRI');
    EXCEPTION
        WHEN OTHERS THEN NULL;
    END;

    IF V_CATEGORY = 'S' THEN
        --Check to see if its an INSERT/UPDATE
        --If record already exists for that cost center its an INSERT, else UPDATE
        SELECT DECODE(COUNT(*), 0, 'INSERT', 'UPDATE')
          INTO V_TRIGGER_ACTION
          FROM DESCARTES_ADRS_FEED_CC
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;

        --Check if the record already exists in our trigger table that is not processed
        --If it does we do not need to insert one more trigger record
        SELECT COUNT(*)
          INTO V_COUNT
          FROM DESCARTES_ADRS_FEED_CC
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
           AND PROCESS_DATE IS NULL;

        IF V_COUNT = 0 THEN
            --If there is not unprocessed record, then we will insert one from trigger
            INSERT INTO DESCARTES_ADRS_FEED_CC(COST_CENTER_CODE,
                                               TRIGGERED_DATE,
                                               TRIGGER_ACTION,
                                               GUID)
            VALUES(IN_COST_CENTER_CODE,
                   TRUNC(SYSDATE),
                   V_TRIGGER_ACTION,
                   REGEXP_REPLACE(RAWTOHEX(SYS_GUID()), '([A-F0-9]{8})([A-F0-9]{4})([A-F0-9]{4})([A-F0-9]{4})([A-F0-9]{12})', '\1-\2-\3-\4-\5'));
        END IF;
    END IF;
EXCEPTION
    WHEN DUP_VAL_ON_INDEX THEN
        --This is OK as we need to use the oldest INSERT/UPDATE trigger for that day
        NULL;
    WHEN OTHERS THEN
        RAISE;
END INSERT_DESCARTES_ADRS_FEED_CC;

END CCN_DESCARTES_PROCESS;