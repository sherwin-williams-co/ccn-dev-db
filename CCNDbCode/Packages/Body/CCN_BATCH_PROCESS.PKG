CREATE OR REPLACE PACKAGE BODY CCN_BATCH_PROCESS AS
/*********************************************************** 
This package body will have procedures and functions related 
to the batch process

Created  : 04/04/2016 nxk927 CCN project
Modified : 05/25/2017 rxa457 CCN PROJECT TEAM.. 
************************************************************/
  
PROCEDURE NEWLY_CRTD_COST_CENTER
/**********************************************************
	This procedure is intended to generate a file with the list of new cost centers
  created on previous day

Created : 04/04/2016 nxk927 CCN project
**********************************************************/
IS
 	V_CLOB        CLOB;

BEGIN
   FOR REC IN (SELECT *
                 FROM COST_CENTER 
                WHERE OPEN_DATE = TRUNC(SYSDATE) -1
                  AND OPEN_DATE <> '01-JAN-2099') LOOP
       V_CLOB := REC.COST_CENTER_CODE 
                 || ',' || REC.COST_CENTER_NAME 
                 || ',' || REC.OPEN_DATE
                 || CHR(10)
                 || V_CLOB;
                 
   END LOOP;
   	
   IF V_CLOB <> EMPTY_CLOB() THEN
      MAIL_PKG.SEND_MAIL('NEWLY_CRTD_COST_CENTER', NULL, NULL,V_CLOB);
   END IF;

END NEWLY_CRTD_COST_CENTER;

FUNCTION GET_HIERARCHY_DETAIL_STRING(
/**********************************************************
	This procedure is intended to generate a file with the list of new selling stores
  created on that day

1 - consider only open/valid store cost centers under USA/CAN/PRI countries
2 - cost center should be a complete cost center => should be part of global, facts and legacy hierarchies
3 - selling store flag should have been set to "Y" on that day

Created : 09/27/2016 jxc517 CCN project Team....
Changed :
**********************************************************/
IN_COST_CENTER_CODE       IN     VARCHAR2)
RETURN VARCHAR2
IS
    V_GLBL_HD_VIEW_REC            HIERARCHY_DETAIL_VIEW%ROWTYPE;
    V_LGCY_HD_VIEW_REC            HIERARCHY_DETAIL_VIEW%ROWTYPE;
    V_FCTS_HD_VIEW_REC            HIERARCHY_DETAIL_VIEW%ROWTYPE;
    V_RETURN_VALUE                VARCHAR2(32000);
BEGIN
    FOR hd_rec IN (SELECT *
                     FROM HIERARCHY_DETAIL_VIEW
                    WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
                      AND HRCHY_HDR_NAME IN ('GLOBAL_HIERARCHY', 'FACTS_DIVISION', 'LEGACY_GL_DIVISION')) LOOP
        IF hd_rec.HRCHY_HDR_NAME = 'GLOBAL_HIERARCHY' THEN
            V_GLBL_HD_VIEW_REC := hd_rec;
        ELSIF hd_rec.HRCHY_HDR_NAME = 'LEGACY_GL_DIVISION' THEN
            V_LGCY_HD_VIEW_REC := hd_rec;
        ELSIF hd_rec.HRCHY_HDR_NAME = 'FACTS_DIVISION' THEN
            V_FCTS_HD_VIEW_REC := hd_rec;
        END IF;
    END LOOP;

    V_RETURN_VALUE := '"' || V_LGCY_HD_VIEW_REC.DIVISION || ' ~ ' || V_LGCY_HD_VIEW_REC.DIVISION_NAME || '",' ||
                      '"' || V_FCTS_HD_VIEW_REC.DIVISION || ' ~ ' || V_FCTS_HD_VIEW_REC.DIVISION_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC.DOMAIN || ' ~ ' || V_GLBL_HD_VIEW_REC.DOMAIN_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC."GROUP" || ' ~ ' || V_GLBL_HD_VIEW_REC.GROUP_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC.DIVISION || ' ~ ' || V_GLBL_HD_VIEW_REC.DIVISION_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC.AREA || ' ~ ' || V_GLBL_HD_VIEW_REC.AREA_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC.DISTRICT || ' ~ ' || V_GLBL_HD_VIEW_REC.DISTRICT_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC.CITY_SALES_MANAGER || ' ~ ' || V_GLBL_HD_VIEW_REC.CITY_SALES_MANAGER_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC.ZONE || ' ~ ' || V_GLBL_HD_VIEW_REC.ZONE_NAME || '",' ||
                      '"' || V_GLBL_HD_VIEW_REC.SPECIAL_ROLES || ' ~ ' || V_GLBL_HD_VIEW_REC.SPECIAL_ROLES_NAME;
    RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_RETURN_VALUE;
END GET_HIERARCHY_DETAIL_STRING;

PROCEDURE GENERATE_USA_SELLING_STR_FILE(
/**********************************************************
This process takes care of generating USA/PRI selling store file

Database type ADDRESS_USA_OT is used to create header and details.
1 - The CONSTRUCTOR FUNCTION address_usa_ot is used to initialize member variables.
2 - The MEMBER FUNCTION print_header_delimited is used to create a Header for the DELIMITER passed
3 - The MEMBER FUNCTION print_values_delimited prints the values with the DELIMITER passed

Created : 09/27/2016 jxc517 CCN project Team....
Changed : 04/03/2017 gxg192 Changes to send email with file as an attachment
          04/04/2017 gxg192 Added comments for address_usa_ot.
          05/05/2017 sxp130 - ASP-783 - Include Phone Number in Selling Store E-Mail Notification.
**********************************************************/
IN_DATE       IN     DATE)
IS
    CURSOR curs IS
        SELECT CC.*, COMMON_TOOLS.GET_PHONE_NUMBER(CC.COST_CENTER_CODE,'PRI') AS PHONE_NUMBER  --ASP-783
          FROM COST_CENTER CC
         WHERE CLOSE_DATE IS NULL
           AND NVL(OPEN_DATE,SYSDATE) <> '01-JAN-2099'
           AND CATEGORY = 'S'
           AND COUNTRY_CODE IN ('USA', 'PRI')
           AND GLOBAL_HIERARCHY_IND = 'Y'
           AND EXISTS (SELECT 1
                         FROM STORE S
                        WHERE CC.COST_CENTER_CODE = S.COST_CENTER_CODE
                          AND NVL(SELLING_STORE_FLAG, 'N') = 'Y'
                          AND SELLING_STORE_FLAG_MARK_DT = IN_DATE); 

    V_CLOB                        CLOB;
    V_CLOB_FOR_EMAIL              CLOB;
    V_ADDRESS_USA_OT              ADDRESS_USA_OT;

    PATH        	  VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
    FILENAME  	    VARCHAR2(50) := 'USA_SELLING_STORES_' || TO_CHAR(SYSDATE,'MMDDRR') || '.csv';
    OUTPUT_FILE     UTL_FILE.FILE_TYPE;
BEGIN

    OUTPUT_FILE := UTL_FILE.FOPEN (PATH
                                   ,FILENAME
                                   ,'W' --BINARY
                                   ,32767);

    V_ADDRESS_USA_OT := ADDRESS_USA_OT('DUMMY', 'M');

    V_CLOB := 'SELLING_STORE_MARKED_ON,' ||
              'STORE_NUMBER,' ||
              'STORE_NAME,' ||
              'LEGACY_GL_DIVISION,' ||
              'FACTS_DIVISION,' ||
              'DOMAIN,' ||
              'GROUP,' ||
              'DIVISION,' ||
              'AREA,' ||
              'DISTRICT,' ||
              'CITY_SALES_MANAGER,' ||
              'ZONE,' ||
              'SPECIAL_ROLES,' ||
              V_ADDRESS_USA_OT.print_header_delimited(',') ||',' ||
              'PHONE_NUMBER'  --ASP-783
              ;

    V_CLOB_FOR_EMAIL := V_CLOB;
    UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);

    FOR cc_rec IN curs LOOP
        V_ADDRESS_USA_OT := ADDRESS_USA_OT(cc_rec.COST_CENTER_CODE, 'M');

        V_CLOB := '"' || IN_DATE || '",' ||
                  '"' || cc_rec.COST_CENTER_CODE || '",' ||
                  '"' || cc_rec.COST_CENTER_NAME || '",' ||
                  GET_HIERARCHY_DETAIL_STRING(cc_rec.COST_CENTER_CODE) || '",' ||
                  V_ADDRESS_USA_OT.print_values_delimited(',') || ',' ||
                  '"' || cc_rec.PHONE_NUMBER || '"'     --ASP-783
                  ;

        V_CLOB_FOR_EMAIL := V_CLOB_FOR_EMAIL || CHR(10) || V_CLOB;
        UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
    END LOOP;

    UTL_FILE.FCLOSE(OUTPUT_FILE);
    MAIL_PKG.SEND_MAIL('SELLING_STORES_MAIL_USA', NULL, NULL,V_CLOB_FOR_EMAIL);

END GENERATE_USA_SELLING_STR_FILE;

PROCEDURE GENERATE_CAN_SELLING_STR_FILE(
/**********************************************************
This process takes care of generating CAN selling store file

Database type ADDRESS_CAN_OT is used to create header and details.
1 - The CONSTRUCTOR FUNCTION address_can_ot is used to initialize member variables.
2 - The MEMBER FUNCTION print_header_delimited is used to create a Header for the DELIMITER passed
3 - The MEMBER FUNCTION print_values_delimited prints the values with the DELIMITER passed

Created : 09/27/2016 jxc517 CCN project Team....
Changed : 04/03/2017 gxg192 Changes to send email with file as an attachment
          04/04/2017 gxg192 Added comments for address_can_ot.
          05/05/2017 sxp130 - ASP-783 - Include Phone Number in Selling Store E-Mail Notification.
**********************************************************/
IN_DATE       IN     DATE)
IS
    CURSOR curs IS
        SELECT CC.*, COMMON_TOOLS.GET_PHONE_NUMBER(CC.COST_CENTER_CODE,'PRI') AS PHONE_NUMBER  --ASP-783
          FROM COST_CENTER CC
         WHERE CLOSE_DATE IS NULL
           AND NVL(OPEN_DATE,SYSDATE) <> '01-JAN-2099'
           AND CATEGORY = 'S'
           AND COUNTRY_CODE = 'CAN'
           AND GLOBAL_HIERARCHY_IND = 'Y'
           AND EXISTS (SELECT 1
                         FROM STORE S
                        WHERE CC.COST_CENTER_CODE = S.COST_CENTER_CODE
                          AND NVL(SELLING_STORE_FLAG, 'N') = 'Y'
                          AND SELLING_STORE_FLAG_MARK_DT = IN_DATE);

    V_CLOB                        CLOB;
    V_CLOB_FOR_EMAIL              CLOB;
    V_ADDRESS_CAN_OT              ADDRESS_CAN_OT;

    PATH        	  VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
    FILENAME  	    VARCHAR2(50) := 'CAN_SELLING_STORES_' || TO_CHAR(SYSDATE,'MMDDRR') || '.csv';
    OUTPUT_FILE     UTL_FILE.FILE_TYPE;
BEGIN

    OUTPUT_FILE := UTL_FILE.FOPEN (PATH
                                   ,FILENAME
                                   ,'W' --BINARY
                                   ,32767);

    V_ADDRESS_CAN_OT := ADDRESS_CAN_OT('DUMMY', 'M');

    V_CLOB := 'SELLING_STORE_MARKED_ON,' ||
              'STORE_NUMBER,' ||
              'STORE_NAME,' ||
              'LEGACY_GL_DIVISION,' ||
              'FACTS_DIVISION,' ||
              'DOMAIN,' ||
              'GROUP,' ||
              'DIVISION,' ||
              'AREA,' ||
              'DISTRICT,' ||
              'CITY_SALES_MANAGER,' ||
              'ZONE,' ||
              'SPECIAL_ROLES,' ||
              V_ADDRESS_CAN_OT.print_header_delimited(',')  ||',' ||
              'PHONE_NUMBER'   --ASP-783
              ;

    V_CLOB_FOR_EMAIL := V_CLOB;
    UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);

    FOR cc_rec IN curs LOOP
        V_ADDRESS_CAN_OT := ADDRESS_CAN_OT(cc_rec.COST_CENTER_CODE, 'M');

        V_CLOB := '"' || IN_DATE || '",' ||
                  '"' || cc_rec.COST_CENTER_CODE || '",' ||
                  '"' || cc_rec.COST_CENTER_NAME || '",' ||
                  GET_HIERARCHY_DETAIL_STRING(cc_rec.COST_CENTER_CODE) || '",' ||
                  V_ADDRESS_CAN_OT.print_values_delimited(',') || ',' ||
                  '"' || cc_rec.PHONE_NUMBER || '"'       --ASP-783
                  ;

        V_CLOB_FOR_EMAIL := V_CLOB_FOR_EMAIL || CHR(10) || V_CLOB;
        UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
    END LOOP;
    UTL_FILE.FCLOSE(OUTPUT_FILE);

    MAIL_PKG.SEND_MAIL('SELLING_STORES_MAIL_CAN', NULL, NULL,V_CLOB_FOR_EMAIL);

END GENERATE_CAN_SELLING_STR_FILE;

PROCEDURE GENERATE_SELLING_STORE_FILE(
/**********************************************************
	This procedure is intended to generate a file with the list of new selling stores
  created on the date passed

1 - consider only open/valid store cost centers under USA/CAN/PRI countries
2 - cost center should be a complete cost center => should be part of global, facts and legacy hierarchies
3 - selling store flag should have been set to "Y" on that day
4 - consider only mailling address for any country code

Created : 09/27/2016 jxc517 CCN project Team....
Changed :
**********************************************************/
IN_DATE       IN     DATE)
IS
BEGIN
    GENERATE_USA_SELLING_STR_FILE(IN_DATE);

    GENERATE_CAN_SELLING_STR_FILE(IN_DATE);

END GENERATE_SELLING_STORE_FILE;


PROCEDURE GENERATE_PCI_MISMATCH_FILE
/**********************************************************
This process takes care of generating PCI_TERMINAL_MISMATCH file

Created : 04/11/2017 pxb712 CCN project Team....
**********************************************************/

IS
    V_CLOB                        CLOB;
    V_BATCH_NUMBER      BATCH_JOB.BATCH_JOB_NUMBER%TYPE;
    V_TRANS_STATUS      BATCH_JOB.TRANS_STATUS%TYPE := 'SUCCESSFUL';
BEGIN

   CCN_BATCH_PKG.INSERT_BATCH_JOB('PCI_TERM_MISMATCH', V_BATCH_NUMBER);
   CCN_BATCH_PKG.LOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;
   
   BEGIN
      FOR REC IN (SELECT * 
                    FROM PCI_TERMINAL_MAIL 
                   WHERE ENTRY_DATE  >= (SELECT MAX(BATCH_JOB_LAST_RUN_DATE)
                                           FROM BATCH_JOB
                                          WHERE BATCH_JOB_NAME = 'PCI_TERM_MISMATCH')) LOOP
					V_CLOB := REC.COST_CENTER_CODE 
							  || ',' || REC.TERMINAL_NUMBER 
                              || ',' || REC.PCI_TERMINAL_ID
                              || CHR(10)
                              || V_CLOB;
                 
     END LOOP;
     
      --Sending mail for the category 'PCI_POS_TERMINAL_MISMATCH'	
      IF V_CLOB <> EMPTY_CLOB() THEN
        MAIL_PKG.SEND_MAIL('PCI_POS_TERMINAL_MISMATCH', NULL, NULL,V_CLOB);
      END IF;
    EXCEPTION
       WHEN OTHERS THEN
          V_TRANS_STATUS := 'ERROR';
   END;
    
   CCN_BATCH_PKG.UPDATE_BATCH_JOB('PCI_TERM_MISMATCH', V_BATCH_NUMBER, V_TRANS_STATUS);
   CCN_BATCH_PKG.UNLOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;

END GENERATE_PCI_MISMATCH_FILE;

PROCEDURE GENERATE_HRCHY_MISMATCH_FILE
/**********************************************************
This process takes care of generating EMAILS FOR HIER_TRNSFR_TERRITORY 
AND HIER_TRNSFR_DAD_CHNG_TERRITORY...

Created : 04/13/2017 pxb712 CCN project Team....
**********************************************************/

IS
    V_CLOB                        CLOB;
    V_DAD_CHANGE_CLOB             CLOB;
    V_BATCH_NUMBER      BATCH_JOB.BATCH_JOB_NUMBER%TYPE;
    V_TRANS_STATUS      BATCH_JOB.TRANS_STATUS%TYPE := 'SUCCESSFUL';
BEGIN

   CCN_BATCH_PKG.INSERT_BATCH_JOB('HRCHY_DAD_MISMATCH', V_BATCH_NUMBER);
   CCN_BATCH_PKG.LOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;

   BEGIN
      FOR REC IN (SELECT * 
                    FROM hierarchy_transfer_mail 
                   WHERE ENTRY_DATE  >= (SELECT MAX(BATCH_JOB_LAST_RUN_DATE)
                                           FROM BATCH_JOB
                                          WHERE BATCH_JOB_NAME = 'HRCHY_DAD_MISMATCH')) LOOP
         IF (REC.PROCESS_NAME = 'HIER_TRNSFR_TERRITORY') THEN          
            V_CLOB := REC.HRCHY_DTL_PREV_LVL_FROM
                      || ',' || REC.HRCHY_DTL_PREV_LVL_TO
                      || ',' || REC.HRCHY_DTL_CURR_ROW_VAL
                      || CHR(10)
                      || V_CLOB;
         ELSE
            V_DAD_CHANGE_CLOB := REC.HRCHY_DTL_PREV_LVL_FROM
                                 || ',' || REC.HRCHY_DTL_PREV_LVL_TO
                                 || ',' || REC.HRCHY_DTL_CURR_ROW_VAL
                                 || CHR(10)
                                 || V_DAD_CHANGE_CLOB;
         END IF;
      END LOOP;

       --Sending mail for the category 'HIER_TRNSFR_TERRITORY'
       IF V_CLOB <> EMPTY_CLOB() THEN
           MAIL_PKG.SEND_MAIL('HIER_TRNSFR_TERRITORY', NULL, NULL, V_CLOB);
       END IF;
       --Sending mail for the change of DAD on territory cost centers
       IF V_DAD_CHANGE_CLOB <> EMPTY_CLOB() THEN
           MAIL_PKG.SEND_MAIL('HIER_TRNSFR_DAD_CHNG_TERRITORY', NULL, NULL, V_DAD_CHANGE_CLOB);
       END IF;
    EXCEPTION
      WHEN OTHERS THEN
         V_TRANS_STATUS := 'ERROR';
    END;
    
   CCN_BATCH_PKG.UPDATE_BATCH_JOB('HRCHY_DAD_MISMATCH', V_BATCH_NUMBER, V_TRANS_STATUS);
   CCN_BATCH_PKG.UNLOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;
END GENERATE_HRCHY_MISMATCH_FILE;

PROCEDURE GENERATE_BCMID_BC_FILE
/******************************************************************************************
This Procedure create file BCMID_BC.CSV on the server
File structure:
MERCHANT_ID,SUBSTR(MERCHANT_ID, 7, 4),PCI_MERCHANT_ID,DISCOVER_ID,PCI_DISCOVER_ID,AMEX_SE_ID
Filters:
POLLING_STATUS_CODE = 'P'
CATEGORY IN ('S','O')

created : 05/25/2017 sxp130 ASP-795
modified:
*******************************************************************************************/
AS
  V_OUT_CLOB  CLOB;
  V_CODE      NUMBER;
  V_ERRM      VARCHAR2(500);

  PATH                VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
  FILENAME            VARCHAR2(50) := 'BCMID_BC_' || TO_CHAR(SYSDATE,'MMDDRR') || '.csv';
  OUTPUT_FILE         UTL_FILE.FILE_TYPE;

 CURSOR CUR IS
     SELECT MERCHANT_ID ||','||
            SUBSTR(MERCHANT_ID, 7, 4) ||','||
            PCI_MERCHANT_ID ||','||
            DISCOVER_ID ||','||
            PCI_DISCOVER_ID ||','||
            AMEX_SE_ID AS V_REC_CLOB
       FROM BANK_CARD
      WHERE POLLING_STATUS_CODE = 'P'
        AND EXPIRATION_DATE IS NULL
        AND EXISTS (SELECT 1
                      FROM COST_CENTER
                     WHERE BANK_CARD.COST_CENTER_CODE = COST_CENTER.COST_CENTER_CODE
                       AND CATEGORY IN ( 'S', 'O' ));

 BEGIN
   OUTPUT_FILE := UTL_FILE.FOPEN (PATH
                                   ,FILENAME
                                   , 'W' --BINARY
                                   , 32767);

FOR REC IN CUR LOOP
V_OUT_CLOB :=  REC.V_REC_CLOB ;
           UTL_FILE.PUT_LINE(OUTPUT_FILE, V_OUT_CLOB, TRUE);
 END LOOP;
UTL_FILE.FCLOSE(OUTPUT_FILE);
EXCEPTION
  when OTHERS then
       V_CODE  := SQLCODE;
       V_ERRM  := SUBSTR(SQLERRM,1,500);
       DBMS_OUTPUT.PUT_LINE('GENERATE_BCMID_BC_FILE FAILED AT '||SQLERRM || ' : ' ||SQLCODE);

END GENERATE_BCMID_BC_FILE;

PROCEDURE GENERATE_BCSERIAL_BC_FILE
/******************************************************************************************
This Procedure create file BCSERIAL_BC.CSV on the server
Filters:
POLLING_STATUS_CODE = 'P'
CATEGORY IN ('S','O')

created : 05/25/2017 sxp130 ASP-795
modified:
*******************************************************************************************/
AS
  V_OUT_CLOB  CLOB;
  V_CODE      NUMBER;
  V_ERRM      VARCHAR2(500);

  PATH                VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
  FILENAME            VARCHAR2(50) := 'BCSERIAL_BC_' || TO_CHAR(SYSDATE,'MMDDRR') || '.dat';
  OUTPUT_FILE         UTL_FILE.FILE_TYPE;

 CURSOR CUR IS
      SELECT '60' AS REC_TYPE,
            RPAD(NVL(SubStr(COST_CENTER.COST_CENTER_CODE,3),' '),4,' ') AS STORE_NO,
            RPAD(' ',12) AS FILLER01,
            RPAD(NVL(COST_CENTER.COST_CENTER_NAME,' '),35,' ') AS STORENAME,
            RPAD(' ',73) AS FILLER02,
            RPAD(NVL(HIERARCHY_DETAIL_VIEW.DIVISION,' '),2,' ') AS DIV_NO,
            RPAD(' ',15) AS FILLER03,
            RPAD(NVL(HIERARCHY_DETAIL_VIEW.AREA,' '),2,' ') AS AREA,
            RPAD(' ',15) AS FILLER04,
            RPAD(NVL(HIERARCHY_DETAIL_VIEW.DISTRICT,' '),2,' ') AS DIST,
            RPAD(' ',3) AS FILLER05,
            RPAD(NVL(BANK_CARD.MERCHANT_ID,' '),11,' ') AS MERCH_ID,
            RPAD(' ',4) AS FILLER06,
            RPAD(NVL(BANK_CARD.DISCOVER_ID,' '),15,' ') AS DISCV_ID,
            RPAD(' ',5) AS FILLER07,
            RPAD(NVL(BANK_CARD.AMEX_SE_ID,' '),10,' ') AS AMEX_ID,
            RPAD(' ',5) AS FILLER08 ,
            RPAD(NVL(ADDRESS_VW.ADDRESS_LINE_1,' '),30,' ') AS ADDRESS1,
            RPAD(' ',2) AS FILLER09,
            RPAD(NVL(ADDRESS_VW.CITY,' '),20,' ') AS CITY,
            RPAD(' ',1) AS FILLER10,
            RPAD(NVL(ADDRESS_VW.STATE_CODE,' '),2,' ') AS STATE,
            RPAD(' ',3) AS FILLER11,
            RPAD(NVL(ADDRESS_VW.ZIP_CODE,' '),10,' ') AS ZIP,
            RPAD(' ',2) AS FILLER12,
            RPAD(NVL(COST_CENTER.STATEMENT_TYPE,' '),2,' ') AS GRP_CODE,
            RPAD(' ',13) AS FILLER13
       FROM BANK_CARD
       JOIN COST_CENTER
         ON (BANK_CARD.COST_CENTER_CODE = COST_CENTER.COST_CENTER_CODE
        AND COST_CENTER.CATEGORY IN ( 'S', 'O' ))
       JOIN HIERARCHY_DETAIL_VIEW
         ON (COST_CENTER.COST_CENTER_CODE = HIERARCHY_DETAIL_VIEW.COST_CENTER_CODE
        AND HRCHY_HDR_NAME IN ('GLOBAL_HIERARCHY'))
       JOIN (SELECT COST_CENTER_CODE, PREMISES AS ADDRESS_LINE_1, DISTRICT AS CITY, NULL AS STATE_CODE, POSTAL_CODE AS ZIP_CODE  FROM ADDRESS_BRB   ---FIELDS NEED TO CONFIRM, NO STATE COLUMN
              WHERE ADDRESS_BRB.EXPIRATION_DATE IS NULL
              AND ADDRESS_TYPE ='M'
             UNION
             SELECT COST_CENTER_CODE, ADDRESS_LINE_1, CITY, STATE_CODE, POSTAL_CODE AS ZIP_CODE FROM ADDRESS_OTHER
              WHERE ADDRESS_OTHER.EXPIRATION_DATE IS NULL
              AND ADDRESS_TYPE ='M'
             UNION
             SELECT COST_CENTER_CODE, ADDRESS_LINE_1, CITY, STATE_CODE, ZIP_CODE FROM ADDRESS_USA
              WHERE ADDRESS_USA.EXPIRATION_DATE IS NULL
              AND ADDRESS_TYPE ='M'
             UNION
             SELECT COST_CENTER_CODE, ADDRESS_LINE_1, CITY, PROVINCE_CODE AS STATE_CODE, POSTAL_CODE AS ZIP_CODE FROM ADDRESS_MEX
              WHERE ADDRESS_MEX.EXPIRATION_DATE IS NULL
              AND ADDRESS_TYPE ='M'
             UNION
             SELECT COST_CENTER_CODE, ADDRESS_LINE_1, CITY, PROVINCE_CODE AS STATE_CODE, POSTAL_CODE AS ZIP_CODE FROM ADDRESS_CAN
              WHERE ADDRESS_CAN.EXPIRATION_DATE IS NULL
              AND ADDRESS_TYPE ='M'
            ) ADDRESS_VW
         ON (COST_CENTER.COST_CENTER_CODE = ADDRESS_VW.COST_CENTER_CODE)
      WHERE BANK_CARD.POLLING_STATUS_CODE = 'P'
        AND BANK_CARD.EXPIRATION_DATE IS NULL;

 BEGIN
   OUTPUT_FILE := UTL_FILE.FOPEN (PATH
                                   ,FILENAME
                                   , 'W' --BINARY
                                   , 32767);

FOR REC IN CUR LOOP
V_OUT_CLOB :=  REC.REC_TYPE || REC.STORE_NO || REC.FILLER01 || REC.STORENAME || REC.FILLER02 || REC.DIV_NO || REC.FILLER03 || REC.AREA || REC.FILLER04 ||
               REC.DIST || REC.FILLER05 || REC.MERCH_ID || REC.FILLER06 || REC.DISCV_ID || REC.FILLER07 || REC.AMEX_ID || REC.FILLER08 || REC.ADDRESS1 || REC.FILLER09 ||
               REC.CITY || REC.FILLER10 || REC.STATE || REC.FILLER11 || REC.ZIP || REC.FILLER12 || REC.GRP_CODE || REC.FILLER13;

           UTL_FILE.PUT_LINE(OUTPUT_FILE, V_OUT_CLOB, TRUE);
 END LOOP;
UTL_FILE.FCLOSE(OUTPUT_FILE);
EXCEPTION
  when OTHERS then
       V_CODE  := SQLCODE;
       V_ERRM  := SUBSTR(SQLERRM,1,500);
       DBMS_OUTPUT.PUT_LINE('GENERATE_BCSERIAL_BC_FILE FAILED AT '||SQLERRM || ' : ' ||SQLCODE);

END GENERATE_BCSERIAL_BC_FILE;

PROCEDURE GENERATE_STORE_DATA_FILE
/**********************************************************
This procedure is intended to generate below files which describes store and merchant details
1. BCMID_BC.CSV
2. BCSERIAL_BC.CSV
Filters:
1 - consider only POLLING_STATUS_CODE = 'P' and CATEGORY IN ( 'S', 'O' )

Created : 05/25/2017 sxp130 CCN project Team.... - ASP-795
Changed :
**********************************************************/
IS
BEGIN
    GENERATE_BCMID_BC_FILE;

    GENERATE_BCSERIAL_BC_FILE;

END GENERATE_STORE_DATA_FILE;

PROCEDURE GENERATE_STORE_PD_HRCHY_FILE
/**********************************************************
This procedure is intended to generate Price District Hierarchy reporting for stores pricing group
STORE_PDH_HRCHY.CSV
The Report is scheduled to run everyday 8am
Filters:
consider only Category = 'S' and stores attached to Price district Hierarchy

Created : 05/25/2017 rxa457 ASP-772 CCN project Team....
**********************************************************/
IS
    CURSOR curs IS
    SELECT CC.*
    FROM COST_CENTER CC
    WHERE GLOBAL_HIERARCHY_IND ='Y' AND 
    CATEGORY = 'S' AND 
    EXISTS(SELECT 'X' FROM  HIERARCHY_DETAIL_VIEW  H WHERE HRCHY_HDR_NAME = 'PRICE_DISTRICT' AND CC.COST_CENTER_CODE = H.COST_CENTER_CODE)
    ORDER BY COST_CENTER_CODE;

CURSOR ADDRESS(P_COST_CENTER_CODE VARCHAR2) IS 
      SELECT * FROM ADDRESS_VW 
      WHERE COST_CENTER_CODE = P_COST_CENTER_CODE AND
      ADDRESS_TYPE = 'M' AND 
      EXPIRATION_DATE IS NULL;

CURSOR PHONE(P_COST_CENTER_CODE VARCHAR2) IS 
      WITH T AS (SELECT PHONE_AREA_CODE,PHONE_NUMBER,PHONE_NUMBER_TYPE FROM PHONE P WHERE PHONE_NUMBER_TYPE IN ('PRI','FAX') AND COST_CENTER_CODE = P_COST_CENTER_CODE)
        SELECT * FROM (
          (SELECT * FROM T)
          PIVOT 
          (MIN(PHONE_AREA_CODE) AS AREA_CODE, MIN(PHONE_NUMBER) AS "NUMBER" FOR (PHONE_NUMBER_TYPE)IN ('PRI' AS PHONE, 'FAX' AS FAX))
                      );

CURSOR HRCHY (P_COST_CENTER_CODE VARCHAR2) IS SELECT * FROM HIERARCHY_DETAIL_VIEW WHERE HRCHY_HDR_NAME = 'GLOBAL_HIERARCHY' AND COST_CENTER_CODE = P_COST_CENTER_CODE;

CURSOR POLLING (P_COST_CENTER_CODE VARCHAR2) IS (SELECT * FROM POLLING WHERE  UPPER(POLLING_STATUS_CODE) IN ('P','Q') AND CURRENT_FLAG ='Y' AND COST_CENTER_CODE = P_COST_CENTER_CODE);

CURSOR TYPE (P_COST_CENTER_CODE VARCHAR2) IS SELECT * FROM TYPE WHERE EXPIRATION_DATE IS NULL AND COST_CENTER_CODE = P_COST_CENTER_CODE;

CURSOR PRICE_DISTRICT(P_COST_CENTER_CODE VARCHAR2) IS SELECT FIRST_VALUE(DISTRICT) IGNORE NULLS OVER (ORDER BY HRCHY_DTL_EFF_DATE DESC) AS PRICE_DISTRICT FROM  HIERARCHY_DETAIL_VIEW
                                                      WHERE HRCHY_HDR_NAME = 'PRICE_DISTRICT' AND COST_CENTER_CODE = P_COST_CENTER_CODE AND  ROWNUM<=1;

    V_CLOB                        CLOB;
    V_CLOB_FOR_EMAIL              CLOB;
    
    PATH        	  VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
    FILENAME  	    VARCHAR2(50) := 'STORE_PDH_HRCHY' || '_'|| TO_CHAR(SYSDATE,'MMDDRR') || '.csv';
    OUTPUT_FILE     UTL_FILE.FILE_TYPE;

    C_ADDRESS                     ADDRESS_VW%ROWTYPE;
    C_PHONE                       PHONE%ROWTYPE;
    C_HRCHY                       HRCHY%ROWTYPE;
    C_POLLING                     POLLING%ROWTYPE;
    C_TYPE                        TYPE%ROWTYPE;
    V_ADDRESS_STREET              VARCHAR2(200);
    V_ADDRESS_ZIP                 VARCHAR2(10);
    V_PHONE_NUMBER                VARCHAR2(14);
    V_FAX_NUMBER                  VARCHAR2(14);
    V_AREA_SHORT_NAME             C_HRCHY.AREA%TYPE;
    V_GNRC_DESC                   CODE_DETAIL.CODE_DETAIL_DESCRIPTION%TYPE;
    V_POLLING_STATUS_CODE_DESC    CODE_DETAIL.CODE_DETAIL_DESCRIPTION%TYPE;
    V_TYPE_CODE_DESC              CODE_DETAIL.CODE_DETAIL_DESCRIPTION%TYPE;
    V_PRICE_DISTRICT              HIERARCHY_DETAIL_VIEW.DISTRICT%TYPE;
    V_DAD                         HIERARCHY_DETAIL_VIEW.DISTRICT%TYPE;
BEGIN

    OUTPUT_FILE := UTL_FILE.FOPEN (PATH
                                   ,FILENAME
                                   ,'W' --BINARY
                                   ,32767);


    V_CLOB := 'Store No,' ||
              'Store No DESC,' ||
              'Store Address-Street,' ||
              'Store Address-City,' ||
              'Store Address-State,' ||
              'Store Address-ZIP,' ||
              'Store Phone-No (###)###-####,' ||
              'Store Fax No,' ||
              'Store Div,' ||
              'Store Div DESC,' ||
              'Store Area,' ||
              'Store Area Name,' ||
              'Store Area Name Short,' ||
              'Store Dad,' ||
              'Store Dad DESC,' ||
              'Store Open Date,' ||
              'Store Close Date,' ||
              'GNRC Code (Current),' ||
              'GNRC Code (Current) DESC,' ||
              'Store Polling Status Code,' ||
              'Store Polling Status Code DESC,' ||
              'Store Type,' ||
              'Store Type DESC,' ||
              'Store Pricing District'
              ;

    V_CLOB_FOR_EMAIL := V_CLOB;
    UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);

    FOR cc_rec IN curs LOOP
    /*Fetch Address Information*/
    IF ADDRESS%ISOPEN THEN
      CLOSE ADDRESS;
    END IF;
    OPEN ADDRESS(cc_rec.COST_CENTER_CODE);
    FETCH ADDRESS INTO C_ADDRESS;
    CLOSE ADDRESS;
    
    IF C_ADDRESS.ADDRESS_LINE_1 IS NOT NULL OR C_ADDRESS.ADDRESS_LINE_2 IS NOT NULL THEN
      V_ADDRESS_STREET := LTRIM(RTRIM(C_ADDRESS.ADDRESS_LINE_1||' '||C_ADDRESS.ADDRESS_LINE_2));
    ELSE
      V_ADDRESS_STREET := NULL;
    END IF;
    
    IF C_ADDRESS.ZIP_CODE IS NOT NULL THEN
      V_ADDRESS_ZIP := LTRIM(RTRIM(C_ADDRESS.ZIP_CODE||' ' ||C_ADDRESS.ZIP_CODE_4));
    ELSE
      V_ADDRESS_ZIP := NULL;
    END IF;
    
    /*Fetch Phone Information*/
    IF PHONE%ISOPEN THEN
      CLOSE PHONE;
    END IF;
    OPEN PHONE(cc_rec.COST_CENTER_CODE);
    FETCH PHONE INTO C_PHONE;
    CLOSE PHONE;
    
    IF C_PHONE.PHONE_AREA_CODE IS NOT NULL AND C_PHONE.PHONE_NUMBER IS NOT NULL THEN
      V_PHONE_NUMBER := '('||C_PHONE.PHONE_AREA_CODE||')'||SUBSTR(C_PHONE.PHONE_NUMBER,1,3)||'-'||SUBSTR(C_PHONE.PHONE_NUMBER,3);
    ELSE
      V_PHONE_NUMBER := NULL;
    END IF;
    
    IF C_PHONE.FAX_AREA_CODE IS NOT NULL AND C_PHONE.FAX_NUMBER IS NOT NULL THEN
      V_FAX_NUMBER   := '('||C_PHONE.FAX_AREA_CODE||')'||SUBSTR(C_PHONE.FAX_NUMBER,1,3)||'-'||SUBSTR(C_PHONE.FAX_NUMBER,3);
    ELSE
      V_FAX_NUMBER := NULL;
    END IF;
    
    /*Fetch Global Hierarchy Information for Area, and DAD*/
    IF HRCHY%ISOPEN THEN
      CLOSE HRCHY;
    END IF;
    OPEN HRCHY(cc_rec.COST_CENTER_CODE);
    FETCH HRCHY INTO C_HRCHY;
    CLOSE HRCHY;
    
    V_AREA_SHORT_NAME := RTRIM(REPLACE(REPLACE(C_HRCHY.AREA_NAME,'AREA',''),'REGION',''));
    V_DAD := C_HRCHY.DIVISION||C_HRCHY.AREA||C_HRCHY.DISTRICT ;
    V_GNRC_DESC := NVL(CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('STATEMENT_TYPE','COD',CC_REC.STATEMENT_TYPE),'N/A');

    /*Fetch Polling Information*/
    IF POLLING%ISOPEN THEN
      CLOSE POLLING;
    END IF;
    OPEN POLLING(cc_rec.COST_CENTER_CODE);
    FETCH POLLING INTO C_POLLING;
    CLOSE POLLING;
    V_POLLING_STATUS_CODE_DESC := NVL(CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('POLLING_STATUS_CODE','COD',C_POLLING.POLLING_STATUS_CODE),'N/A');
    
    /*Fetch Type Information*/
    IF TYPE%ISOPEN THEN
      CLOSE TYPE;
    END IF;
    OPEN TYPE(cc_rec.COST_CENTER_CODE);
    FETCH TYPE INTO C_TYPE;
    CLOSE TYPE;
    V_TYPE_CODE_DESC := NVL(CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('TYPE_CODE','COD',C_TYPE.TYPE_CODE),'N/A');

    /*Fetch price District Information*/
    IF PRICE_DISTRICT%ISOPEN THEN
      CLOSE PRICE_DISTRICT;
    END IF;
    OPEN PRICE_DISTRICT(cc_rec.COST_CENTER_CODE);
    FETCH PRICE_DISTRICT INTO V_PRICE_DISTRICT;
    CLOSE PRICE_DISTRICT;

    V_CLOB := '"' || cc_rec.COST_CENTER_CODE || '",' ||
              '"' || cc_rec.COST_CENTER_NAME || '",' ||
              '"' || V_ADDRESS_STREET || '",' ||
              '"' || C_ADDRESS.CITY || '",' ||
              '"' || C_ADDRESS.STATE_CODE || '",' ||
              '"' || V_ADDRESS_ZIP || '",' ||
              '"' || V_PHONE_NUMBER || '",' ||
              '"' || V_FAX_NUMBER || '",' ||
              '"' || C_HRCHY.DIVISION || '",' ||
              '"' || C_HRCHY.DIVISION_NAME || '",' ||
              '"' || C_HRCHY.AREA || '",' ||
              '"' || C_HRCHY.AREA_NAME || '",' ||
              '"' || V_AREA_SHORT_NAME || '",' ||
              '"' || V_DAD || '",' ||
              '"' || C_HRCHY.DISTRICT_NAME || '",' ||
              '"' || CC_REC.OPEN_DATE || '",' ||
              '"' || CC_REC.CLOSE_DATE || '",' ||
              '"' || CC_REC.STATEMENT_TYPE || '",' ||
              '"' || V_GNRC_DESC || '",' ||
              '"' || C_POLLING.POLLING_STATUS_CODE || '",' ||
              '"' || V_POLLING_STATUS_CODE_DESC || '",' ||
              '"' || C_TYPE.TYPE_CODE || '",' ||
              '"' || V_TYPE_CODE_DESC || '",'||
              '"' || V_PRICE_DISTRICT || '"' 
              ;

        V_CLOB_FOR_EMAIL := V_CLOB_FOR_EMAIL || CHR(10) || V_CLOB;
        UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
    END LOOP;
    
    UTL_FILE.FCLOSE(OUTPUT_FILE);
    MAIL_PKG.SEND_MAIL('STORE_PDH_HRCHY_MAIL', NULL, NULL,V_CLOB_FOR_EMAIL);
EXCEPTION
  WHEN OTHERS THEN
       MAIL_PKG.SEND_MAIL('STORE_PDH_HRCHY_MAIL', NULL, NULL,'GENERATE STORE PRICE DISTRICT HIERARCHY REPORTING FAILED AT '||SQLCODE||' : '||SQLERRM);

END GENERATE_STORE_PD_HRCHY_FILE;

END CCN_BATCH_PROCESS;