create or replace PACKAGE BODY CCN_EPM_FEED_PKG
AS
/****************************************************************************** 
This package is intended to create 2 EPM hierarchy file's as an pipe delimited file
Procedure : GENERATE_CLOB       : to genarate a "stores/reps" data file.
            GENERATE_ADMIN_CLOB : to genarate a "Admin" data file.

Created : 1/3/2017 sxg151 -- CCN coding project
Changed :
******************************************************************************/

PROCEDURE GET_COST_CENTER_DETAILS(
/****************************************************************************** 
This PROCEDURE gets all the cost center details for the given COST_CENTER_CODE

Created : 1/23/2018 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE       IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    OUT_CCN_REC                  OUT CCN_REC)
IS
    CURSOR GET_COST_CENTER_DETAILS_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT COST_CENTER_CODE,
               COST_CENTER_NAME,
               CATEGORY,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('CATEGORY','COD',CATEGORY) CATEGORY_DESC,
               ACQUISITION_CODE,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('ACQUISITION_CODE','COD',ACQUISITION_CODE) ACQUISITION_CODE_DESC,
               STATEMENT_TYPE,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('STATEMENT_TYPE','COD',STATEMENT_TYPE) STATEMENT_TYPE_DESC,
               MISSION_TYPE_CODE,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('MISSION_TYPE_CODE','COD',MISSION_TYPE_CODE) MISSION_TYPE_CODE_DESC,
               OPEN_DATE,
               CLOSE_DATE,
               CURRENCY_CODE,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('CURRENCY_CODE','COD',CURRENCY_CODE) CURRENCY_CODE_DESC,
               BEGIN_DATE
          FROM COST_CENTER
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;

    V_COST_CENTER_REC       GET_COST_CENTER_DETAILS_CUR%ROWTYPE;
BEGIN
    OPEN GET_COST_CENTER_DETAILS_CUR(IN_COST_CENTER_CODE);
    FETCH GET_COST_CENTER_DETAILS_CUR INTO V_COST_CENTER_REC;
    CLOSE GET_COST_CENTER_DETAILS_CUR;

    OUT_CCN_REC := NULL;
    OUT_CCN_REC.COST_CENTER_CODE        := V_COST_CENTER_REC.COST_CENTER_CODE;
    OUT_CCN_REC.COST_CENTER_NAME        := V_COST_CENTER_REC.COST_CENTER_NAME;
    OUT_CCN_REC.CATEGORY                := V_COST_CENTER_REC.CATEGORY;
    OUT_CCN_REC.CATEGORY_DESC           := V_COST_CENTER_REC.CATEGORY_DESC;
    OUT_CCN_REC.ACQUISITION_CODE        := V_COST_CENTER_REC.ACQUISITION_CODE;
    OUT_CCN_REC.ACQUISITION_CODE_DESC   := V_COST_CENTER_REC.ACQUISITION_CODE_DESC;
    OUT_CCN_REC.STATEMENT_TYPE          := V_COST_CENTER_REC.STATEMENT_TYPE;
    OUT_CCN_REC.STATEMENT_TYPE_DESC     := V_COST_CENTER_REC.STATEMENT_TYPE_DESC;
    OUT_CCN_REC.MISSION_TYPE_CODE       := V_COST_CENTER_REC.MISSION_TYPE_CODE;
    OUT_CCN_REC.MISSION_TYPE_CODE_DESC  := V_COST_CENTER_REC.MISSION_TYPE_CODE_DESC;
    OUT_CCN_REC.OPEN_DATE               := V_COST_CENTER_REC.OPEN_DATE;
    OUT_CCN_REC.CLOSE_DATE              := V_COST_CENTER_REC.CLOSE_DATE;
    OUT_CCN_REC.CURRENCY_CODE           := V_COST_CENTER_REC.CURRENCY_CODE;
    OUT_CCN_REC.CURRENCY_CODE_DESC      := V_COST_CENTER_REC.CURRENCY_CODE_DESC;
    OUT_CCN_REC.BEGIN_DATE              := V_COST_CENTER_REC.BEGIN_DATE;
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_COST_CENTER_DETAILS' , SQLERRM, SQLCODE);
        RAISE;
END GET_COST_CENTER_DETAILS;

PROCEDURE GET_MARKETING_DETAILS(
/****************************************************************************** 
This PROCEDURE gets all Marketing details.

Created : 11/30/2017 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE        IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    IO_CCN_REC                 IN OUT CCN_REC)
IS
    CURSOR GET_MARKETING_DETAILS_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT MKT_MISSION,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('MKT_MISSION','COD',MKT_MISSION) MKT_MISSION_DESC
           FROM MARKETING
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;

    V_MARKETING_REC       GET_MARKETING_DETAILS_CUR%ROWTYPE;
BEGIN
    OPEN GET_MARKETING_DETAILS_CUR(IN_COST_CENTER_CODE);
    FETCH GET_MARKETING_DETAILS_CUR INTO V_MARKETING_REC;
    CLOSE GET_MARKETING_DETAILS_CUR;

    IO_CCN_REC.MKT_MISSION       := V_MARKETING_REC.MKT_MISSION;
    IO_CCN_REC.MKT_MISSION_DESC  := V_MARKETING_REC.MKT_MISSION_DESC;
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_MARKETING_DETAILS' , SQLERRM, SQLCODE);
        RAISE;
END GET_MARKETING_DETAILS;

PROCEDURE GET_HIERARCHY_DETAILS(
/******************************************************************************
This PROCEDURE gets all HIERARCHY details

Created : 11/30/2017 sxg151 -- CCN Project Team
Changed
******************************************************************************/
    IN_COST_CENTER_CODE    IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    IO_CCN_REC             IN OUT CCN_REC)
IS
    CURSOR GET_HIERARCHY_DETAILS_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT GLOBAL_DIVISION "DIVISION",
               GLOBAL_DIVISION_NAME "DIVISION_NAME",
               GLOBAL_AREA "AREA",
               GLOBAL_AREA_NAME "AREA_NAME",
               GLOBAL_DISTRICT "DISTRICT",
               GLOBAL_DISTRICT_NAME "DISTRICT_NAME",
               GLOBAL_CITY_SALES_MGR "CITY_SALES_MGR",
               GLOBAL_CITY_SALES_MGR_NAME "CITY_SALES_MGR_NAME",
               COA_DIVISION,
               COA_DIVISION_NAME
          FROM GLBL_FACTS_LEGACY_VIEW
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;

    V_HIERARCHY_REC       GET_HIERARCHY_DETAILS_CUR%ROWTYPE;
BEGIN
    OPEN GET_HIERARCHY_DETAILS_CUR(IN_COST_CENTER_CODE);
    FETCH GET_HIERARCHY_DETAILS_CUR INTO V_HIERARCHY_REC;
    CLOSE GET_HIERARCHY_DETAILS_CUR;

    IO_CCN_REC.DIVISION            := V_HIERARCHY_REC.DIVISION;
    IO_CCN_REC.DIVISION_NAME       := V_HIERARCHY_REC.DIVISION_NAME;
    IO_CCN_REC.AREA                := V_HIERARCHY_REC.AREA;
    IO_CCN_REC.AREA_NAME           := V_HIERARCHY_REC.AREA_NAME;
    IO_CCN_REC.DISTRICT            := V_HIERARCHY_REC.DISTRICT;
    IO_CCN_REC.DISTRICT_NAME       := V_HIERARCHY_REC.DISTRICT_NAME;
    IO_CCN_REC.CITY_SALES_MGR      := V_HIERARCHY_REC.CITY_SALES_MGR;
    IO_CCN_REC.CITY_SALES_MGR_NAME := V_HIERARCHY_REC.CITY_SALES_MGR_NAME;
    IO_CCN_REC.COA_DIVISION        := V_HIERARCHY_REC.COA_DIVISION;
    IO_CCN_REC.COA_DIVISION_NAME   := V_HIERARCHY_REC.COA_DIVISION_NAME;
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_HIERARCHY_DETAILS' , SQLERRM, SQLCODE);
        RAISE;
END GET_HIERARCHY_DETAILS;

PROCEDURE GET_MGR_DETAILS(
/****************************************************************************** 
This PROCEDURE gets all Manager details.

Created : 11/30/2017 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE       IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    IN_CATEGORY               IN     COST_CENTER.CATEGORY%TYPE,
    IO_CCN_REC                IN OUT CCN_REC)
IS
    CURSOR GET_MGR_DETAILS_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT DIV_MGR_NAME,
               AREA_MGR_NAME,
               DISTRICT_MGR_NAME,
               CITY_MGR_NAME CITY_SALES_MANAGER_NAME,
               CCN_HIERARCHY.GET_MGR_NAME_FNC(COST_CENTER_CODE) CC_MGR_NAME
          FROM GLOBAL_HIERARCHY_ATTRBT_VW
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;

    V_MGR_REC       GET_MGR_DETAILS_CUR%ROWTYPE; 
BEGIN
    OPEN GET_MGR_DETAILS_CUR(IN_COST_CENTER_CODE);
    FETCH GET_MGR_DETAILS_CUR INTO V_MGR_REC;
    CLOSE GET_MGR_DETAILS_CUR;

    IO_CCN_REC.DIV_MGR_NAME              := V_MGR_REC.DIV_MGR_NAME;
    IO_CCN_REC.AREA_MGR_NAME             := V_MGR_REC.AREA_MGR_NAME;
    IO_CCN_REC.DISTRICT_MGR_NAME         := V_MGR_REC.DISTRICT_MGR_NAME;
    IO_CCN_REC.CITY_SALES_MANAGER_NAME   := V_MGR_REC.CITY_SALES_MANAGER_NAME;
    IO_CCN_REC.CC_MGR_NAME               := V_MGR_REC.CC_MGR_NAME;

    -- Load Territory Rep as city_sales_manager_name, for cost centers whose category is 'T'
    IF IN_CATEGORY = 'T' THEN
        IO_CCN_REC.TERRITORY_REP         := V_MGR_REC.CITY_SALES_MANAGER_NAME;
    END IF;
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_MGR_DETAILS' , SQLERRM, SQLCODE);
        RAISE;
END GET_MGR_DETAILS;

PROCEDURE GET_STATE_CODE(
/****************************************************************************** 
This PROCEDURE gets STATE AND PROVINCE CODE details.

Created : 11/30/2017 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE        IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    IO_CCN_REC                 IN OUT CCN_REC)
IS
    CURSOR GET_STATE_CODE_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT STATE_CODE,
               STATE_CODE_DESCRIPTION,
               PROVINCE_CODE,
               PROVINCE_CODE_DESCRIPTION
          FROM ADDRESS_VW
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
           AND ADDRESS_TYPE = 'M'
           AND EXPIRATION_DATE IS NULL;
BEGIN
    FOR REC IN GET_STATE_CODE_CUR(IN_COST_CENTER_CODE) LOOP
        BEGIN
            IO_CCN_REC.STATE_CODE           := REC.STATE_CODE;
            IO_CCN_REC.STATE_DESCRIPTION    := REC.STATE_CODE_DESCRIPTION;
            IO_CCN_REC.PROVINCE_CODE        := REC.PROVINCE_CODE;
            IO_CCN_REC.PROVINCE_DESCRIPTION := REC.PROVINCE_CODE_DESCRIPTION;
        EXCEPTION
            WHEN OTHERS THEN
                NULL;
        END;
    END LOOP;
EXCEPTION 
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_STATE_CODE' , SQLERRM, SQLCODE);
        RAISE;
END GET_STATE_CODE;

PROCEDURE GET_STATUS_CODE(
/****************************************************************************** 
This PROCEDURE gets STATUS CODE details.

Created : 11/30/2017 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE        IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    IO_CCN_REC                 IN OUT CCN_REC)
IS
    CURSOR GET_STATUS_CODE_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT STATUS_CODE,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('STATUS_CODE','COD',STATUS_CODE) STATUS_CODE_DESC
          FROM STATUS
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
           AND EXPIRATION_DATE IS NULL;

    V_STATUS_REC       GET_STATUS_CODE_CUR%ROWTYPE; 
BEGIN
    OPEN GET_STATUS_CODE_CUR(IN_COST_CENTER_CODE);
    FETCH GET_STATUS_CODE_CUR INTO V_STATUS_REC;
    CLOSE GET_STATUS_CODE_CUR;

    IO_CCN_REC.STATUS_CODE      := V_STATUS_REC.STATUS_CODE;
    IO_CCN_REC.STATUS_CODE_DESC := V_STATUS_REC.STATUS_CODE_DESC;
EXCEPTION 
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_STATUS_CODE' , SQLERRM, SQLCODE);
        RAISE;
END GET_STATUS_CODE;

PROCEDURE GET_TYPE_CODE(
/******************************************************************************
This PROCEDURE gets STATUS CODE details.

Created : 11/30/2017 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE        IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    IO_CCN_REC                 IN OUT CCN_REC)
IS
    CURSOR GET_TYPE_CODE_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT TYPE_CODE,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('TYPE_CODE','COD',TYPE_CODE) TYPE_CODE_DESC
          FROM TYPE
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE
           AND EXPIRATION_DATE IS NULL;

    V_TYPE_REC       GET_TYPE_CODE_CUR%ROWTYPE;

BEGIN
    OPEN GET_TYPE_CODE_CUR(IN_COST_CENTER_CODE);
    FETCH GET_TYPE_CODE_CUR INTO V_TYPE_REC;
    CLOSE GET_TYPE_CODE_CUR;

    IO_CCN_REC.TYPE_CODE      := V_TYPE_REC.TYPE_CODE;
    IO_CCN_REC.TYPE_CODE_DESC := V_TYPE_REC.TYPE_CODE_DESC;
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_TYPE_CODE' , SQLERRM, SQLCODE);
        RAISE;
END GET_TYPE_CODE;

PROCEDURE GET_TERR_BUSN_CODE(
/****************************************************************************** 
This PROCEDURE gets TERRITORY  BUISINESS CODE details.

Created : 11/30/2017 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE       IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    IO_CCN_REC                IN OUT CCN_REC)
IS
    CURSOR GET_TERR_BUSN_CODE_CUR(IN_COST_CENTER_CODE VARCHAR2) IS
        SELECT TERRITORY_TYPE_BUSN_CODE TERRITORY_BUSN_CODE,
               CCN_PICK_LIST_PKG.GET_CODE_DETAIL_VALUE_DSCRPTN('TERRITORY_TYPE_BUSN_CODE','COD',TERRITORY_TYPE_BUSN_CODE) TERRITORY_BUSN_CODE_DESC
          FROM TERRITORY
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;

    V_TERR_REC       GET_TERR_BUSN_CODE_CUR%ROWTYPE; 
BEGIN
    OPEN GET_TERR_BUSN_CODE_CUR(IN_COST_CENTER_CODE);
    FETCH GET_TERR_BUSN_CODE_CUR INTO V_TERR_REC;
    CLOSE GET_TERR_BUSN_CODE_CUR;

    IO_CCN_REC.TERRITORY_BUSN_CODE      := V_TERR_REC.TERRITORY_BUSN_CODE;
    IO_CCN_REC.TERRITORY_BUSN_CODE_DESC := V_TERR_REC.TERRITORY_BUSN_CODE_DESC;
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_TERR_BUSN_CODE' , SQLERRM, SQLCODE);
        RAISE;
END GET_TERR_BUSN_CODE;

PROCEDURE GET_EPM_DETAILS(
/****************************************************************************** 
This PROCEDURE gets EPM Data details based on COST_CENTER_CODE

Created : 12/15/2017 sxg151 -- CCN Project Team
Changed :
******************************************************************************/
    IN_COST_CENTER_CODE         IN     COST_CENTER.COST_CENTER_CODE%TYPE,
    OUT_CCN_REC                    OUT CCN_REC )
IS 
    V_COUNT         NUMBER := 0;
    V_CCN_REC       CCN_REC;
BEGIN
    --Below  PROCEDURE gets all the cost center details based on COST_CENTER_CODE
    GET_COST_CENTER_DETAILS(IN_COST_CENTER_CODE, V_CCN_REC );
    --Below  PROCEDURE gets all the Marketing details from Marketing table based on COST_CENTER_CODE
    GET_MARKETING_DETAILS(IN_COST_CENTER_CODE, V_CCN_REC );
     --Below  PROCEDURE gets all the Hierarchy details from GLBL_FACTS_LEGACY_VIEW View based on COST_CENTER_CODE
    GET_HIERARCHY_DETAILS(IN_COST_CENTER_CODE, V_CCN_REC );
    --Below  PROCEDURE gets all the Manager details from GLOBAL_HIERARCHY_ATTRBT_VW View based on COST_CENTER_CODE
    GET_MGR_DETAILS(IN_COST_CENTER_CODE, V_CCN_REC.CATEGORY, V_CCN_REC );
    --Below  PROCEDURE gets all the STATE AND PROVINCE CODE details details from ADDRESS_VW View based on COST_CENTER_CODE
    GET_STATE_CODE(IN_COST_CENTER_CODE, V_CCN_REC );
    --Below  PROCEDURE gets all the Status Code details details from STATUS_CODE Table based on COST_CENTER_CODE
    GET_STATUS_CODE(IN_COST_CENTER_CODE, V_CCN_REC );
    --Below  PROCEDURE gets all the TERRITORY  BUISINESS CODE details details from TERRITORY Table based on COST_CENTER_CODE
    GET_TERR_BUSN_CODE(IN_COST_CENTER_CODE, V_CCN_REC );
    --Below  PROCEDURE gets all the TYPE CODE details from TYPE Table based on COST_CENTER_CODE
    GET_TYPE_CODE(IN_COST_CENTER_CODE, V_CCN_REC );

    OUT_CCN_REC :=  V_CCN_REC;
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GET_EPM_DETAILS', SQLERRM, SQLCODE);
        RAISE;
END GET_EPM_DETAILS;

PROCEDURE GENERATE_CLOB(
/****************************************************************************** 
This procedure is intended to create store and reps hierarchy file
This procedure will call "GET_EPM_DETAILS" to get the "Store/reps" to generate a file

Created : 12/13/2017 sxg151 -- CCN coding project
Changed :
******************************************************************************/
    IN_REC             IN        CC_HIER_REC,
    IN_PREV_REC        IN        CC_HIER_REC,
    IN_STORE_REP       IN        VARCHAR2,
    OUT_CLOB              OUT    CLOB)
IS
    V_DLMTR       VARCHAR2(1) := '|';
    V_CCN_REC     CCN_REC;
BEGIN
    --Check if the Legacy Division changed
    --If so we need to start a new listing of that particular Legacy Division
    IF IN_PREV_REC.LEGACY_DIVISION <> IN_REC.LEGACY_DIVISION THEN
       -- OUT_CLOB Ex Data:- R03_Selling|R03_Stores|R03_STORES_REAL ESTATE
        OUT_CLOB := OUT_CLOB || 
                    IN_REC.LEGACY_DIVISION || '_Selling' ||  V_DLMTR ||
                    IN_REC.LEGACY_DIVISION || '_' || IN_STORE_REP ||  V_DLMTR ||
                    IN_REC.LEGACY_DIVISION || '_' || UPPER(IN_STORE_REP) || '_' || UPPER(IN_REC.LEGACY_DIVISION_NAME) ||  CHR(13);
    END IF;

    --Check if the Division/Area changed
    --If so we need to start a new listing of that particular Division
    IF IN_PREV_REC.DIVISION <> IN_REC.DIVISION OR IN_PREV_REC.AREA <> IN_REC.AREA THEN
        --Data Ex:- R03_Stores|R03_0001_Stores|R03_0001_STORES_RETAIL PROPERTY MW
        OUT_CLOB := OUT_CLOB ||
                   IN_REC.LEGACY_DIVISION || '_' || IN_STORE_REP ||  V_DLMTR ||
                   IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || '_' || IN_STORE_REP ||  V_DLMTR || 
                   IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || '_' || UPPER(IN_STORE_REP) || '_' ||
                   UPPER(IN_REC.AREA_NAME) || CHR(13);
    END IF;


    --Check if the District changed
    --If so we need to start a new listing of that particular District
    IF IN_PREV_REC.DIVISION || IN_PREV_REC.DISTRICT <> IN_REC.DIVISION || IN_REC.DISTRICT THEN
        --Data Ex:- R03_0001_Stores|R03_000101_Stores|R03_000101_STORES_REAL ESTATE RETAIL PROPERTY MW
        OUT_CLOB := OUT_CLOB ||
                   IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || '_' || IN_STORE_REP ||  V_DLMTR ||
                   IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || IN_REC.DISTRICT || '_' || IN_STORE_REP || V_DLMTR ||
                   IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || IN_REC.DISTRICT || '_' || UPPER(IN_STORE_REP)|| '_' ||
                   UPPER(IN_REC.DISTRICT_NAME)  || CHR(13);
    END IF;

    --Check if the City changed
    --If so we need to start a new listing of that particular City
    IF IN_PREV_REC.DIVISION || IN_PREV_REC.DISTRICT || IN_PREV_REC.CITY_SALES_MANAGER <> IN_REC.DIVISION || IN_REC.DISTRICT || IN_REC.CITY_SALES_MANAGER THEN
        OUT_CLOB := OUT_CLOB ||
            IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || IN_REC.DISTRICT || '_' || IN_STORE_REP ||  V_DLMTR ||
            IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || IN_REC.DISTRICT || '_' ||IN_REC.CITY_SALES_MANAGER || '_' || IN_STORE_REP || V_DLMTR ||--sxg added"_" before CITY_SALES_MANAGER
            IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || IN_REC.DISTRICT || '_' ||IN_REC.CITY_SALES_MANAGER || '_' || UPPER(IN_STORE_REP)||'_' ||--sxg added"_" before CITY_SALES_MANAGER
            UPPER(IN_REC.CITY_SALES_MANAGER_NAME) ||  CHR(13);
    END IF;

    GET_EPM_DETAILS(IN_REC.COST_CENTER_CODE, V_CCN_REC ) ;

    --List all the cost centers under that City
    OUT_CLOB := OUT_CLOB ||
                IN_REC.LEGACY_DIVISION || '_' || IN_REC.DIVISION || IN_REC.AREA || IN_REC.DISTRICT ||'_' || IN_REC.CITY_SALES_MANAGER || '_' || IN_STORE_REP ||  V_DLMTR ||--sxg added"_" before CITY_SALES_MANAGER
                V_CCN_REC.COST_CENTER_CODE             ||  V_DLMTR ||
                V_CCN_REC.COST_CENTER_NAME             ||  V_DLMTR ||
                V_CCN_REC.DIVISION                     ||  V_DLMTR ||
                V_CCN_REC.AREA                         ||  V_DLMTR ||
                V_CCN_REC.DISTRICT                     ||  V_DLMTR ||
                V_CCN_REC.CITY_SALES_MGR               ||  V_DLMTR ||
                V_CCN_REC.MISSION_TYPE_CODE            ||  V_DLMTR ||
                V_CCN_REC.MISSION_TYPE_CODE_DESC       ||  V_DLMTR ||
                V_CCN_REC.BEGIN_DATE                   ||  V_DLMTR ||
                V_CCN_REC.OPEN_DATE                    ||  V_DLMTR ||
                V_CCN_REC.CLOSE_DATE                   ||  V_DLMTR ||
                V_CCN_REC.TYPE_CODE                    ||  V_DLMTR ||
                V_CCN_REC.TYPE_CODE_DESCRIPTION        ||  V_DLMTR ||
                V_CCN_REC.STATUS_CODE                  ||  V_DLMTR ||
                V_CCN_REC.STATUS_CODE_DESC             ||  V_DLMTR ||
                V_CCN_REC.TERRITORY_BUSN_CODE          ||  V_DLMTR ||
                V_CCN_REC.TERRITORY_BUSN_CODE_DESC     ||  V_DLMTR ||
                V_CCN_REC.MKT_MISSION                  ||  V_DLMTR ||
                V_CCN_REC.MKT_MISSION_DESC             ||  V_DLMTR ||
                V_CCN_REC."CATEGORY"                   ||  V_DLMTR ||
                V_CCN_REC.CATEGORY_DESC                ||  V_DLMTR ||
                V_CCN_REC.ACQUISITION_CODE             ||  V_DLMTR ||
                V_CCN_REC.ACQUISITION_CODE_DESC        ||  V_DLMTR ||
                V_CCN_REC.STATEMENT_TYPE               ||  V_DLMTR ||
                V_CCN_REC.STATEMENT_TYPE_DESC          ||  V_DLMTR ||
                V_CCN_REC.STATE_CODE                   ||  V_DLMTR ||
                V_CCN_REC.STATE_DESCRIPTION            ||  V_DLMTR ||
                V_CCN_REC.PROVINCE_CODE                ||  V_DLMTR ||
                V_CCN_REC.PROVINCE_DESCRIPTION         ||  V_DLMTR ||
                V_CCN_REC.DIVISION_NAME                ||  V_DLMTR ||
                V_CCN_REC.AREA_NAME                    ||  V_DLMTR ||
                V_CCN_REC.DISTRICT_NAME                ||  V_DLMTR ||
                V_CCN_REC.CITY_SALES_MGR_NAME          ||  V_DLMTR ||
                V_CCN_REC.DIV_MGR_NAME                 ||  V_DLMTR ||
                V_CCN_REC.AREA_MGR_NAME                ||  V_DLMTR ||
                V_CCN_REC.DISTRICT_MGR_NAME            ||  V_DLMTR ||
                V_CCN_REC.CITY_SALES_MANAGER_NAME      ||  V_DLMTR ||
                V_CCN_REC.CC_MGR_NAME                  ||  V_DLMTR ||
                V_CCN_REC.TERRITORY_REP                ||  V_DLMTR ||
                -- extra delimiter as we do not have data for admin manager
                                                           V_DLMTR ||
                V_CCN_REC.CURRENCY_CODE                ||  V_DLMTR ||
                V_CCN_REC.CURRENCY_CODE_DESC           ||  V_DLMTR ||
                V_CCN_REC.COA_DIVISION                 ||  V_DLMTR ||
                V_CCN_REC.COA_DIVISION_NAME            ||  V_DLMTR || CHR(13);
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_REC.COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GENERATE_CLOB', SQLERRM, SQLCODE);
        RAISE;
END GENERATE_CLOB;

PROCEDURE GENERATE_ADMIN_CLOB(
/****************************************************************************** 
This package is intended to create "Admin" hierarchy file as an csv

Created : 12/13/2017 sxg151 -- CCN coding project
Changed :
******************************************************************************/
    IN_REC             IN     CC_ADMINHIER_REC,
    IN_PREV_REC        IN     CC_ADMINHIER_REC,
    IN_STORE_REP       IN     VARCHAR2,
    OUT_CLOB              OUT CLOB)
IS
    V_DLMTR               VARCHAR2(1)    := '|';
    V_LAST_CODE           VARCHAR2(250)  := NULL;
    V_PRE_STRING          CHAR(1)        := 'S';
    V_CCN_REC             CCN_REC;
BEGIN
    --Check if the Legacy Division changed
    --If so we need to start a new listing of that particular Legacy Division
    IF (IN_REC.LEGACY_DIVISION IS NOT NULL) AND NVL(IN_PREV_REC.LEGACY_DIVISION,'XX') <> IN_REC.LEGACY_DIVISION THEN
        --OUT_CLOB Ex:- PSG|R13
        OUT_CLOB := OUT_CLOB ||
                    IN_REC.LEGACY_DIVISION || V_DLMTR ||
                    IN_REC.LEGACY_DIVISION || '_' || IN_STORE_REP ||  V_DLMTR ||
                    IN_REC.LEGACY_DIVISION || '_' || UPPER(IN_STORE_REP) ||  CHR(13);
    END IF;
    
    IF (IN_REC.DOMAIN IS NOT NULL) AND ( NVL(IN_PREV_REC.DOMAIN,'XX') <> IN_REC.DOMAIN) THEN
        OUT_CLOB := OUT_CLOB ||
                    IN_REC.LEGACY_DIVISION || '_' || IN_STORE_REP ||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.DOMAIN||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.DOMAIN || '_' || UPPER(IN_REC.DOMAIN_NAME) || CHR(13);
    END IF;

    IF (IN_REC.DIVISION IS NOT NULL) AND ( NVL(IN_PREV_REC.DIVISION,'XX') <> IN_REC.DIVISION) THEN
        OUT_CLOB := OUT_CLOB ||
                    V_PRE_STRING || IN_REC.DOMAIN ||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.DIVISION||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.DIVISION || '_' || UPPER(IN_REC.DIVISION_NAME) || CHR(13);
    END IF;

    --Check if the Division changed
    IF (IN_REC.CONTROL IS NOT NULL) AND ( NVL(IN_PREV_REC.CONTROL,'XX') <> IN_REC.CONTROL) THEN
        OUT_CLOB := OUT_CLOB ||
                   V_PRE_STRING || IN_REC.DIVISION ||  V_DLMTR ||
                   V_PRE_STRING || IN_REC.CONTROL ||  V_DLMTR ||
                   V_PRE_STRING || IN_REC.CONTROL || '_' || UPPER(IN_REC.CONTROL_NAME) || CHR(13);
    END IF;

    IF  (IN_REC.BUDGET IS NOT NULL ) AND (NVL(IN_PREV_REC.BUDGET,'XX') <> IN_REC.BUDGET) THEN
        OUT_CLOB := OUT_CLOB ||
                    V_PRE_STRING || IN_REC.CONTROL ||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.BUDGET  ||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.BUDGET || '_'  || UPPER(IN_REC.BUDGET_NAME) || CHR(13);
    END IF;

    --Check if the District changed
    --If so we need to start a new listing of that particular District
    IF (IN_REC.FUNC1 IS NOT NULL ) AND  (IN_PREV_REC.CONTROL || NVL(IN_PREV_REC.FUNC1,'XX') <> IN_REC.CONTROL || IN_REC.FUNC1) THEN
        OUT_CLOB := OUT_CLOB ||
                    V_PRE_STRING || IN_REC.BUDGET ||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.FUNC1 || V_DLMTR ||
                    V_PRE_STRING || IN_REC.FUNC1 || '_' || UPPER(IN_REC.FUNC1_NAME)  || CHR(13);
    END IF;

    --Check if the City changed
    --If so we need to start a new listing of that particular City
    IF (IN_REC.FUNC2 IS NOT NULL) AND (IN_PREV_REC.CONTROL || IN_PREV_REC.FUNC1 || NVL(IN_PREV_REC.FUNC2,'XX') <> IN_REC.CONTROL || IN_REC.FUNC1 || IN_REC.FUNC2) THEN
        OUT_CLOB := OUT_CLOB ||
                    V_PRE_STRING || IN_REC.FUNC1 ||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.FUNC2 || V_DLMTR ||
                    V_PRE_STRING || IN_REC.FUNC2 || '_' ||UPPER(IN_REC.FUNC2_NAME) ||  CHR(13);
    END IF;

    IF (IN_REC.FUNC3 IS NOT NULL) AND (IN_PREV_REC.CONTROL || IN_PREV_REC.FUNC1 || IN_PREV_REC.FUNC2  || NVL(IN_REC.FUNC3,'XX') <> IN_REC.CONTROL || IN_REC.FUNC1 || IN_REC.FUNC2 || IN_REC.FUNC3) THEN
        OUT_CLOB := OUT_CLOB ||
                    V_PRE_STRING || IN_REC.FUNC2 ||  V_DLMTR ||
                    V_PRE_STRING || IN_REC.FUNC3 || V_DLMTR ||
                    V_PRE_STRING || IN_REC.FUNC3 || '_' ||UPPER(IN_REC.FUNC3_NAME) ||  CHR(13);
    END IF;

    IF (IN_REC.DOMAIN IS NOT NULL) THEN
        V_LAST_CODE := TO_CHAR(IN_REC."DOMAIN");
    END IF;
    IF (IN_REC.DIVISION IS NOT NULL) THEN
        V_LAST_CODE := TO_CHAR(IN_REC.DIVISION);
    END IF;
    IF (IN_REC.CONTROL IS NOT NULL) THEN
        V_LAST_CODE := TO_CHAR(IN_REC.CONTROL);
    END IF;
    IF (IN_REC.BUDGET IS NOT NULL ) THEN
        V_LAST_CODE := TO_CHAR(IN_REC.BUDGET);
    END IF;
    IF (IN_REC.FUNC1 IS NOT NULL ) THEN
      V_LAST_CODE := TO_CHAR(IN_REC.FUNC1);
    END IF;
    IF (IN_REC.FUNC2 IS NOT NULL ) THEN
      V_LAST_CODE := TO_CHAR(IN_REC.FUNC2);
    END IF;
    IF (IN_REC.FUNC3 IS NOT NULL ) THEN
      V_LAST_CODE := TO_CHAR(IN_REC.FUNC3);
    END IF;

    --List all the cost centers under that City
    OUT_CLOB := OUT_CLOB ||
                V_PRE_STRING || V_LAST_CODE                             ||  V_DLMTR ||
                IN_REC.COST_CENTER_CODE                                 ||  V_DLMTR ||
                IN_REC.COST_CENTER_NAME                                 ||  V_DLMTR ||
                CCN_HIERARCHY.GET_MGR_NAME_FNC(IN_REC.COST_CENTER_CODE) ||  V_DLMTR ||  CHR(13);
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR(IN_REC.COST_CENTER_CODE, 'CCN_EPM_FEED_PKG.GENERATE_ADMIN_CLOB', SQLERRM, SQLCODE);
        RAISE;
END GENERATE_ADMIN_CLOB;

PROCEDURE GENERATE_FILE
/****************************************************************************** 
This PROCEDURE generates the EPM hierarchy file as a pipe delimited file

Created : 12/12/2017 sxg151 -- CCN coding project
Changed :
******************************************************************************/
IS
   -- Get the Legacy Divisions which required into cursor :legacy_gl_divisions
   -- As per Andrew request initially added ('L01', 'L02', 'L03', 'L04', 'L06', 'F08') Added : 'R02','R03','R13' on 05102018 As per request.
   -- Added 'F16','F88' as per Andrew's email on 05/14/2018
    CURSOR legacy_gl_divisions IS
        SELECT DISTINCT LEGACY_DIVISION
          FROM HIERARCHY_DETAIL_VIEW
         WHERE HRCHY_HDR_NAME = 'LEGACY_GL_DIVISION'
          AND LEGACY_DIVISION IN ('L01', 'L02', 'L03', 'L04', 'L06', 'F08','R02','R03','R13','F16','F88')
        ORDER BY 1 DESC;

   -- Below cursor "selling_store_cur" will have the Store data which proviced Legacy_divisions
    CURSOR selling_store_cur(IN_LEGACY_DIVISION    VARCHAR2) IS
        SELECT B.LEGACY_DIVISION,
               B.LEGACY_DIVISION_NAME,
               A.DOMAIN AS DOMAIN,
               A.DOMAIN_NAME AS DOMAIN_NAME,
               A."GROUP",
               A.GROUP_NAME,
               A.DIVISION,
               A.DIVISION_NAME,
               A.AREA,
               A.AREA_NAME,
               A.DISTRICT,
               A.DISTRICT_NAME,
               A.CITY_SALES_MANAGER,
               A.CITY_SALES_MANAGER_NAME,
               A."ZONE" AS "ZONE",
               A.ZONE_NAME AS ZONE_NAME,
               A.COST_CENTER_CODE,
               A.COST_CENTER_NAME
          FROM (SELECT * FROM HIERARCHY_DETAIL_VIEW WHERE HRCHY_HDR_NAME = 'GLOBAL_HIERARCHY') A,
               (SELECT * FROM HIERARCHY_DETAIL_VIEW WHERE HRCHY_HDR_NAME = 'LEGACY_GL_DIVISION') B,
               COST_CENTER CC
         WHERE A.COST_CENTER_CODE = B.COST_CENTER_CODE
           AND A.COST_CENTER_CODE = CC.COST_CENTER_CODE
           AND B.LEGACY_DIVISION  = IN_LEGACY_DIVISION
           AND CC.CATEGORY IN ('D','O','S','R') -- included CATEGORY :'R' as per andrew's email on 05.15.2018
           AND NOT EXISTS (SELECT 1
                             FROM CC_DELETION_GUIDS CD 
                            WHERE CD.COST_CENTER_CODE = A.COST_CENTER_CODE)
           AND A.COST_CENTER_CODE NOT LIKE 'O%'
         ORDER BY B.LEGACY_DIVISION, A.DIVISION, A.AREA, A.DISTRICT, A.CITY_SALES_MANAGER, A.COST_CENTER_CODE;

    -- Below cursor "reps_cur" will have the Store data which proviced Legacy divisions
    CURSOR reps_cur(IN_LEGACY_DIVISION    VARCHAR2) IS
        SELECT B.LEGACY_DIVISION,
               B.LEGACY_DIVISION_NAME,
               A.DOMAIN AS DOMAIN,
               A.DOMAIN_NAME AS DOMAIN_NAME,
               A."GROUP",
               A.GROUP_NAME,
               A.DIVISION,
               A.DIVISION_NAME,
               A.AREA,
               A.AREA_NAME,
               A.DISTRICT,
               A.DISTRICT_NAME,
               A.CITY_SALES_MANAGER,
               A.CITY_SALES_MANAGER_NAME,
               A."ZONE" AS "ZONE",
               A.ZONE_NAME AS ZONE_NAME,
               A.COST_CENTER_CODE,
               A.COST_CENTER_NAME
          FROM (SELECT * FROM HIERARCHY_DETAIL_VIEW WHERE HRCHY_HDR_NAME = 'GLOBAL_HIERARCHY') A,
               (SELECT * FROM HIERARCHY_DETAIL_VIEW WHERE HRCHY_HDR_NAME = 'LEGACY_GL_DIVISION') B,
               COST_CENTER CC
         WHERE A.COST_CENTER_CODE = B.COST_CENTER_CODE
           AND A.COST_CENTER_CODE = CC.COST_CENTER_CODE
           AND B.LEGACY_DIVISION  = IN_LEGACY_DIVISION
           AND REGEXP_LIKE(A.CITY_SALES_MANAGER,NVL('[A-Z]+$','*')) 
           AND NOT EXISTS (SELECT 1
                           FROM CC_DELETION_GUIDS CD
                          WHERE CD.COST_CENTER_CODE = A.COST_CENTER_CODE)
           AND A.COST_CENTER_CODE NOT LIKE 'O%'
         ORDER BY B.LEGACY_DIVISION, A.DIVISION, A.AREA, A.DISTRICT, A.CITY_SALES_MANAGER, A.COST_CENTER_CODE;

    V_PREV_REC           CC_HIER_REC;
    V_DEF_REC            CC_HIER_REC;
    V_DLMTR              VARCHAR2(1) := '|';
    V_CLOB               CLOB;
    V_OUT_CLOB           CLOB;

    PATH                  VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
    FILENAME              VARCHAR2(50) := 'NONADMIN_HRCHY_FORMAT_' || TO_CHAR(SYSDATE,'MMDDRR') || '.dat'; --File Name
    OUTPUT_FILE           UTL_FILE.FILE_TYPE;
BEGIN
    --Open the file to write the data
    OUTPUT_FILE := UTL_FILE.FOPEN (PATH, FILENAME, 'W' ,32767);

    --Write header information (column headings)
    V_CLOB :=   'PARENT'                            || V_DLMTR ||
                'CHILD'                             || V_DLMTR ||
                'DESCRIPTION'                       || V_DLMTR ||
                'DIVISION'                          || V_DLMTR ||
                'AREA'                              || V_DLMTR ||
                'DISTRICT'                          || V_DLMTR ||
                'CITY_SALES_MGR'                    || V_DLMTR ||
                'MISSION_TYPE_CODE'                 || V_DLMTR ||
                'MISSION_TYPE_CODE_DESC'            || V_DLMTR ||
                'BEGIN_DATE'                        || V_DLMTR ||
                'OPEN_DATE'                         || V_DLMTR ||
                'CLOSE_DATE'                        || V_DLMTR ||
                'TYPE_CODE'                         || V_DLMTR ||
                'TYPE_CODE_DESCRIPTION'             || V_DLMTR ||
                'STATUS_CODE'                       || V_DLMTR ||
                'STATUS_CODE_DESC'                  || V_DLMTR ||
                'TERRITORY_BUSN_CODE'               || V_DLMTR ||
                'TERRITORY_BUSN_CODE_DESC'          || V_DLMTR ||
                'MKT_MISSION'                       || V_DLMTR ||
                'MKT_MISSION_DESC'                  || V_DLMTR ||
                'CATEGORY'                          || V_DLMTR ||
                'CATEGORY_DESC'                     || V_DLMTR ||
                'ACQUISITION_CODE'                  || V_DLMTR ||
                'ACQUISITION_CODE_DESC'             || V_DLMTR ||
                'STATEMENT_TYPE'                    || V_DLMTR ||
                'STATEMENT_TYPE_DESC'               || V_DLMTR ||
                'STATE_CODE'                        || V_DLMTR ||
                'STATE_DESCRIPTION'                 || V_DLMTR ||
                'PROVINCE_CODE'                     || V_DLMTR ||
                'PROVINCE_DESCRIPTION'              || V_DLMTR ||
                'DIVISION_NAME'                     || V_DLMTR ||
                'AREA_NAME'                         || V_DLMTR ||
                'DISTRICT_NAME'                     || V_DLMTR ||
                'CITY_SALES_MGR_NAME'               || V_DLMTR ||
                'DIV_MGR_NAME'                      || V_DLMTR ||
                'AREA_MGR_NAME'                     || V_DLMTR ||
                'DISTRICT_MGR_NAME'                 || V_DLMTR ||
                'CITY_SALES_MANAGER_NAME'           || V_DLMTR ||
                'CC_MGR_NAME'                       || V_DLMTR ||
                'TERRITORY_REP'                     || V_DLMTR ||
                'ADMIN_CC_MANAGER'                  || V_DLMTR ||
                'CURRENCY_CODE'                     || V_DLMTR ||
                'CURRENCY_CODE_DESC'                || V_DLMTR ||
                'COA_DIVISION'                      || V_DLMTR ||
                'COA_DIVISION_NAME'
                ;

    UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
    V_CLOB := NULL;
    --Loop through the cursor to write the details
    FOR rec IN legacy_gl_divisions LOOP
        V_CLOB :=   'PSG' ||  V_DLMTR ||  REC.LEGACY_DIVISION ||  CHR(13);
        V_CLOB :=   V_CLOB || 
                    REC.LEGACY_DIVISION ||  V_DLMTR ||
                    REC.LEGACY_DIVISION || '_Selling' || CHR(13);

        UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
        V_CLOB := NULL; 
        FOR rec1 IN selling_store_cur(rec.LEGACY_DIVISION) LOOP
            GENERATE_CLOB(rec1, V_PREV_REC, 'Stores', V_CLOB);
            --Writ the data to the file
            IF V_CLOB <> EMPTY_CLOB() THEN
                UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
            END IF;
            V_PREV_REC := rec1;
        END LOOP;

        V_PREV_REC:= V_DEF_REC;
        V_CLOB := NULL;
        FOR rec1 IN reps_cur(rec.LEGACY_DIVISION) LOOP
            GENERATE_CLOB(rec1, V_PREV_REC, 'Reps', V_CLOB);
            --Writ the data to the file
            IF V_CLOB <> EMPTY_CLOB() THEN
                UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
            END IF;
           V_PREV_REC := rec1;
        END LOOP;
        V_PREV_REC:= V_DEF_REC;
        V_CLOB := NULL;
    END LOOP;

    --Close the file once the data write is complete
    UTL_FILE.FCLOSE(OUTPUT_FILE);
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR('000000', 'CCN_EPM_FEED_PKG.GENERATE_FILE', SQLERRM, SQLCODE);
        IF UTL_FILE.IS_OPEN(OUTPUT_FILE) THEN
            UTL_FILE.FCLOSE(OUTPUT_FILE);
        END IF;
        RAISE;
END GENERATE_FILE;

PROCEDURE GENERATE_ADMIN_FILE
/******************************************************************************
This PROCEDURE generates the EPM Admin hierarchy file as an pipe delimited file
--As we discussed in meeting(05/11/2018) with Andrew  Decided to Generate separate file for Admin

Created : 05/11/2018 sxg151 -- CCN Project Team...
Changed :
******************************************************************************/
IS
   -- Get the Legacy Divisions which required into cursor :legacy_gl_divisions
    CURSOR legacy_gl_divisions IS
        SELECT DISTINCT LEGACY_DIVISION
          FROM HIERARCHY_DETAIL_VIEW
         WHERE HRCHY_HDR_NAME = 'LEGACY_GL_DIVISION'
           AND LEGACY_DIVISION IN ('L01', 'L02', 'L03', 'L04', 'L06', 'F08', 'R02', 'R03', 'R13','F16','F88')
        ORDER BY 1 DESC;

   -- Below cursor get the admin data for the given ledacy gl Divisions
    CURSOR admin_cur(IN_LEGACY_DIVISION    VARCHAR2) IS
        SELECT  B.LEGACY_DIVISION AS LEGACY_DIVISION,
                B.LEGACY_DIVISION_NAME AS LEGACY_DIVISION_NAME,
                H.DOMAIN,
                H.DOMAIN_NAME,
                H.DIVISION,
                H.DIVISION_NAME,
                H.CONTROL,
                H.CONTROL_NAME,
                H.BUDGET,
                H.BUDGET_NAME,
                H.FUNCTIONAL_1,
                H.FUNCTIONAL_1_NAME,
                H.FUNCTIONAL_2,
                H.FUNCTIONAL_2_NAME,
                H.FUNCTIONAL_3,
                H.FUNCTIONAL_3_NAME,
                CC.COST_CENTER_CODE,
                CC.COST_CENTER_NAME
           FROM COST_CENTER CC,
               (SELECT * FROM HIERARCHY_DETAIL_VIEW WHERE HRCHY_HDR_NAME = 'LEGACY_GL_DIVISION') B ,
                ADMINORG_HIERARCHY_DTL_VIEW H
        WHERE CC.COST_CENTER_CODE = H.COST_CENTER_CODE
          AND CC.COST_CENTER_CODE = B.COST_CENTER_CODE
          AND B.LEGACY_DIVISION   = IN_LEGACY_DIVISION
          AND NOT EXISTS (SELECT 1 FROM CC_DELETION_GUIDS CD WHERE CD.COST_CENTER_CODE = B.COST_CENTER_CODE)
          AND B.COST_CENTER_CODE NOT LIKE 'O%'
          ORDER BY B.LEGACY_DIVISION, H.DIVISION NULLS LAST, H.CONTROL NULLS LAST, H.BUDGET NULLS LAST, H.FUNCTIONAL_1 NULLS LAST, H.FUNCTIONAL_2 NULLS LAST, H.FUNCTIONAL_3 NULLS LAST, CC.COST_CENTER_CODE ;

    V_PREV_REC           CC_ADMINHIER_REC;
    V_DEF_REC            CC_ADMINHIER_REC;
    V_DLMTR              VARCHAR2(1) := '|';
    V_CLOB               CLOB;
    V_OUT_CLOB           CLOB;

    PATH                  VARCHAR2(50) := 'CCN_DATAFILES'; -- DIRECTORY CREATED IN ORACLE DATABASE
    FILENAME              VARCHAR2(50) := 'ADMIN_HRCHY_FORMAT_' || TO_CHAR(SYSDATE,'MMDDRR') || '.dat'; --File Name>
    OUTPUT_FILE           UTL_FILE.FILE_TYPE;
BEGIN
    --Open the file to write the data
    OUTPUT_FILE := UTL_FILE.FOPEN (PATH, FILENAME, 'W' ,32767);

    --Write header information (column headings) Ex:- PARENT|CHILD|DESCRIPTION|ADMIN_CC_MANAGER
    V_CLOB :=   'PARENT'                            || V_DLMTR ||
                'CHILD'                             || V_DLMTR ||
                'DESCRIPTION'                       || V_DLMTR ||
                'ADMIN_CC_MANAGER'
                ;

    UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
    V_CLOB := NULL;
    --Loop through the cursor to write the details
    FOR rec IN legacy_gl_divisions LOOP
        V_CLOB :=   'PSG' ||  V_DLMTR ||  REC.LEGACY_DIVISION ||  CHR(13);
        V_CLOB :=   V_CLOB ||
                    REC.LEGACY_DIVISION || CHR(13);
--                    ||  V_DLMTR ||
--                    REC.LEGACY_DIVISION || '_Selling' || CHR(13);

        UTL_FILE.PUT_LINE(OUTPUT_FILE, V_CLOB, TRUE);
        V_CLOB := NULL;

        V_PREV_REC:= V_DEF_REC;
        V_CLOB := NULL;

        FOR rec1 IN admin_cur(rec.LEGACY_DIVISION) LOOP
            GENERATE_ADMIN_CLOB(rec1, V_PREV_REC, 'Admin', V_OUT_CLOB);
            --Writ the data to the file
            IF V_OUT_CLOB <> EMPTY_CLOB() THEN
                UTL_FILE.PUT_LINE(OUTPUT_FILE, V_OUT_CLOB, TRUE);
            END IF;
            V_PREV_REC := rec1;
            V_OUT_CLOB := NULL;
        END LOOP;
        V_PREV_REC:= V_DEF_REC;
        V_CLOB := NULL;
    END LOOP;

    --Close the file once the data write is complete
    UTL_FILE.FCLOSE(OUTPUT_FILE);
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR('000000', 'CCN_EPM_FEED_PKG.GENERATE_ADMIN_FILE', SQLERRM, SQLCODE);
        IF UTL_FILE.IS_OPEN(OUTPUT_FILE) THEN
            UTL_FILE.FCLOSE(OUTPUT_FILE);
        END IF;
        RAISE;
END GENERATE_ADMIN_FILE;

END CCN_EPM_FEED_PKG;