create or replace PACKAGE BODY CCN_ADDRESS_FUT_TO_CURR_PKG
AS
/**********************************************************
  This Package contains procedured to move the future address records into
  current

created : 04/18/2016 jxc517 CCN Project....
Changed :
**********************************************************/

PROCEDURE BUILD_ADDRESS_CLOB(
/******************************************************************************
  This procedure will performs the core upsert process for the passed cost center

created : 09/26/2017 axt754 CCN Project....
revised :

*******************************************************************************/
    in_cost_center_code    IN     VARCHAR2,
    in_effective_date      IN     DATE,
    in_table_name          IN     VARCHAR2,
    o_clob                    OUT CLOB)
IS
    v_context          DBMS_XMLGEN.ctxHandle;
    v_ref_cursor       SYS_REFCURSOR;
BEGIN
    OPEN v_ref_cursor FOR
        'SELECT *
           FROM ' || in_table_name || '
          WHERE ADDRESS_TYPE = ''F''
            AND COST_CENTER_CODE = '''|| in_cost_center_code ||'''
            AND EFFECTIVE_DATE <= TRUNC(SYSDATE)
            AND (EXPIRATION_DATE IS NULL OR EXPIRATION_DATE > TRUNC(SYSDATE))
         UNION ALL
         SELECT *
           FROM ' || in_table_name || '
          WHERE ADDRESS_TYPE = ''F''
            AND COST_CENTER_CODE = '''|| in_cost_center_code ||'''
            AND EFFECTIVE_DATE <= TRUNC(SYSDATE)
            AND (EXPIRATION_DATE IS NULL OR EXPIRATION_DATE > TRUNC(SYSDATE))';
    --build the xml context to be used with DBMS_XMLGEN
    --The context will be bult form the result set of the dynamic sql query
    v_context := DBMS_XMLGEN.NEWCONTEXT(v_ref_cursor);
    --Set the rowset tag name as below, else default <ROWSET> tag will be used
    DBMS_XMLGEN.SETROWSETTAG(v_context, 'ADDRESS_UI_UPSERT');
    --Set the row tag name as below, else default <ROW> tag will be used
    DBMS_XMLGEN.SETROWTAG(v_context, in_table_name);
    --Convert the context into a clob xml
    SELECT DBMS_XMLGEN.GETXML(v_context) INTO o_clob FROM DUAL;
    --close the context
    DBMS_XMLGEN.CLOSECONTEXT(v_context);

    --Proceed further only if the XML is generated for upsert
    IF o_clob <> EMPTY_CLOB() THEN
         -- Update EFFECTIVE_DATE IN THE XML to status EFFECTIVE_DATE..
         SELECT  UPDATEXML(XMLTYPE(o_clob),
                           '/ADDRESS_UI_UPSERT/'||in_table_name||'/EFFECTIVE_DATE/text()',
                           TO_CHAR(in_effective_date,'MM-DD-YYYY')).getClobVal()
           INTO  o_clob
           FROM  DUAL;
        --Replace the first future address type "F" to mailing address type "M"
        o_clob := REGEXP_REPLACE(o_clob,
                                '<ADDRESS_TYPE>F</ADDRESS_TYPE>',
                                '<ADDRESS_TYPE>M</ADDRESS_TYPE>',1,1);

        --Replace the next future address type "F" to Physical address type "P"
        o_clob := REPLACE(o_clob,
                          '<ADDRESS_TYPE>F</ADDRESS_TYPE>',
                          '<ADDRESS_TYPE>P</ADDRESS_TYPE>');
    END IF;
END BUILD_ADDRESS_CLOB;

PROCEDURE PROCESS_ADDRESS(
/******************************************************************************
  This procedure will performs the core upsert process for the passed cost center

created : 04/20/2016 jxc517 CCN Project....
revised : 09/20/2017 axt754 CCN Project....
          Removed code to update status code as '3' and added code to insert the future address also as address type "P"
          and changed clob to have status effective date instead future address effective date
*******************************************************************************/
    in_cost_center_code    IN     VARCHAR2,
    in_effective_date      IN     DATE,
    in_table_name          IN     VARCHAR2)
IS
    v_context          DBMS_XMLGEN.ctxHandle;
    v_clob             CLOB;
    v_select_clause    CLOB;
BEGIN
    BUILD_ADDRESS_CLOB(in_cost_center_code,
                       in_effective_date,
                       in_table_name,
                       v_clob);

    IF v_clob <> EMPTY_CLOB() THEN
        CCN_UI_INTERFACE_APP_PKG.ADDRESS_UI_UPSERT_SP(v_clob);

        --Finally delete the future address as it is no longer needed
        EXECUTE IMMEDIATE
            'UPDATE ' || in_table_name || '
                SET EXPIRATION_DATE = EFFECTIVE_DATE
              WHERE ADDRESS_TYPE = ''F''
                AND COST_CENTER_CODE = '''|| in_cost_center_code ||'''';
    END IF;
    COMMIT;
END PROCESS_ADDRESS;

PROCEDURE PROCESS
/******************************************************************************
  This procedure will move the future address records into current mailing address

created : 04/18/2016 jxc517 CCN Project....
revised : 05/16/2016 jxc517 CCN Project Team....
          Added new table ADDRESS_BRB
        : 09/20/2017 axt754 CCN Project Team....
          Changed code to :
          Move future to current if status is '3'
          effective date as the status effective date rather than future address effective date
*******************************************************************************/
IS
   v_batch_number    BATCH_JOB.BATCH_JOB_NUMBER%TYPE;
   v_trans_status    BATCH_JOB.TRANS_STATUS%TYPE := 'SUCCESSFUL';
   v_clob            CLOB;
   V_STATUS_CODE     VARCHAR2(20);
   V_EFFECTIVE_DATE  DATE;
   V_STATUS_REC      STATUS%ROWTYPE;

   CURSOR add_info_cur IS
       SELECT 'ADDRESS_USA' AS TABLE_NAME,
              COST_CENTER_CODE,
              EFFECTIVE_DATE
         FROM ADDRESS_USA
        WHERE ADDRESS_TYPE = 'F'
          AND EFFECTIVE_DATE <= TRUNC(SYSDATE)
          AND (EXPIRATION_DATE IS NULL OR EXPIRATION_DATE > TRUNC(SYSDATE))
       UNION
       SELECT 'ADDRESS_CAN' AS TABLE_NAME,
              COST_CENTER_CODE,
              EFFECTIVE_DATE
         FROM ADDRESS_CAN
        WHERE ADDRESS_TYPE = 'F'
          AND EFFECTIVE_DATE <= TRUNC(SYSDATE)
          AND (EXPIRATION_DATE IS NULL OR EXPIRATION_DATE > TRUNC(SYSDATE))
        UNION
       SELECT 'ADDRESS_MEX' AS TABLE_NAME,
              COST_CENTER_CODE,
              EFFECTIVE_DATE
         FROM ADDRESS_MEX
        WHERE ADDRESS_TYPE = 'F'
          AND EFFECTIVE_DATE <= TRUNC(SYSDATE)
          AND (EXPIRATION_DATE IS NULL OR EXPIRATION_DATE > TRUNC(SYSDATE))
        UNION
       SELECT 'ADDRESS_OTHER' AS TABLE_NAME,
              COST_CENTER_CODE,
              EFFECTIVE_DATE
         FROM ADDRESS_OTHER
        WHERE ADDRESS_TYPE = 'F'
          AND EFFECTIVE_DATE <= TRUNC(SYSDATE)
          AND (EXPIRATION_DATE IS NULL OR EXPIRATION_DATE > TRUNC(SYSDATE))
        UNION
       SELECT 'ADDRESS_BRB' AS TABLE_NAME,
              COST_CENTER_CODE,
              EFFECTIVE_DATE
         FROM ADDRESS_BRB
        WHERE ADDRESS_TYPE = 'F'
          AND EFFECTIVE_DATE <= TRUNC(SYSDATE)
          AND (EXPIRATION_DATE IS NULL OR EXPIRATION_DATE > TRUNC(SYSDATE));
BEGIN
    CCN_BATCH_PKG.INSERT_BATCH_JOB('ADDR_FUTR_TO_MAIL', v_batch_number);
    CCN_BATCH_PKG.LOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;
    BEGIN
        --Loop through all the records in the future table that are going effective today
        FOR rec IN add_info_cur LOOP
            BEGIN
                -- Get Active status row for a cost center
                V_STATUS_REC := COMMON_TOOLS.GET_ACTIVE_STATUS(rec.COST_CENTER_CODE);
                -- check the condition, if the status code is 3, then start
                -- processing the address from future to current.
                IF (NVL(V_STATUS_REC.STATUS_CODE,'XXX') = '3') THEN
                    PROCESS_ADDRESS(rec.COST_CENTER_CODE,
                                    V_STATUS_REC.EFFECTIVE_DATE,
                                    rec.TABLE_NAME);
                END IF;
            EXCEPTION
                WHEN OTHERS THEN
                    COMMON_TOOLS.LOG_ERROR(rec.COST_CENTER_CODE, 'CCN_ADDRESS_FUT_TO_CURR_PKG.PROCESS - ' || rec.TABLE_NAME, SQLERRM, SQLCODE);
            END;
        END LOOP;
    EXCEPTION
        WHEN OTHERS THEN
            v_trans_status := 'ERROR';
            ROLLBACK;
    END;
    CCN_BATCH_PKG.UPDATE_BATCH_JOB('ADDR_FUTR_TO_MAIL', v_batch_number, v_trans_status);
    CCN_BATCH_PKG.UNLOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION; 
EXCEPTION
    WHEN OTHERS THEN
        COMMON_TOOLS.LOG_ERROR('OTHER', 'CCN_ADDRESS_FUT_TO_CURR_PKG.PROCESS', SQLERRM, SQLCODE);
END PROCESS;

END CCN_ADDRESS_FUT_TO_CURR_PKG;