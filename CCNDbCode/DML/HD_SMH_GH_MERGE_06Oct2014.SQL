SET SERVEROUTPUT ON;
DECLARE
    V_COUNT NUMBER := 0;
BEGIN
    CCN_BATCH_PKG.LOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;
    COMMON_TOOLS.ALTER_ALL_TRIGGERS('DISABLE');

    --Delete the 8th and 9th level territory cost center records from GLOBAL_HIERARCHY
    DELETE
      FROM HIERARCHY_DETAIL HD
     WHERE HRCHY_HDR_NAME  = 'GLOBAL_HIERARCHY'
       AND HRCHY_DTL_LEVEL = '9'
       AND EXISTS (SELECT 1
                     FROM COST_CENTER
                    WHERE HD.HRCHY_DTL_CURR_ROW_VAL = COST_CENTER_CODE
                      AND CATEGORY = 'T');
    DELETE
      FROM HIERARCHY_DETAIL HD
     WHERE HRCHY_HDR_NAME  = 'GLOBAL_HIERARCHY'
       AND HRCHY_DTL_LEVEL = '8'
       AND EXISTS (SELECT 1
                     FROM COST_CENTER
                    WHERE SUBSTR(HD.HRCHY_DTL_NEXT_LVL_VAL,-6,6) = COST_CENTER_CODE
                      AND CATEGORY = 'T');
     DELETE
       FROM HIERARCHY_DETAIL HD
     WHERE HRCHY_HDR_NAME  = 'GLOBAL_HIERARCHY'
       AND HRCHY_DTL_LEVEL = '6'
       AND (HRCHY_DTL_CURR_ROW_VAL IN ('##','33') OR REGEXP_LIKE(HRCHY_DTL_CURR_ROW_VAL,'[A-Z]+'));

    --insert all the records into GLOBAL_HIERARCHY from SALES_MANAGER_HIERARCHY
    INSERT INTO HIERARCHY_DETAIL
    SELECT 'GLOBAL_HIERARCHY',
           HRCHY_DTL_LEVEL,
           HRCHY_DTL_PREV_LVL_VAL,
           HRCHY_DTL_CURR_LVL_VAL,
           HRCHY_DTL_NEXT_LVL_VAL,
           HRCHY_DTL_EFF_DATE,
           HRCHY_DTL_EXP_DATE,
           HRCHY_DTL_DESC,
           HRCHY_DTL_CURR_ROW_VAL,
           UPPER_LVL_VER_VALUE
      FROM HIERARCHY_DETAIL
     WHERE HRCHY_HDR_NAME  = 'SALES_MANAGER_HIERARCHY';
     
    --Delete the parents that are not having child records
    FOR rec IN (SELECT DISTINCT HRCHY_DTL_LEVEL,
                                HRCHY_DTL_CURR_LVL_VAL
                  FROM HIERARCHY_DETAIL
                 WHERE HRCHY_HDR_NAME         = 'GLOBAL_HIERARCHY'
                   AND HRCHY_DTL_LEVEL        < '9'
                   AND HRCHY_DTL_NEXT_LVL_VAL <> '~'
                 ORDER BY HRCHY_DTL_LEVEL ASC) LOOP
        SELECT COUNT(*)
          INTO V_COUNT
          FROM HIERARCHY_DETAIL
         WHERE HRCHY_HDR_NAME         = 'GLOBAL_HIERARCHY'
           AND HRCHY_DTL_LEVEL        = rec.HRCHY_DTL_LEVEL + 1
           AND HRCHY_DTL_PREV_LVL_VAL = rec.HRCHY_DTL_CURR_LVL_VAL;
        IF V_COUNT = 0 THEN --Meaning there are no childs for this parent record, and we can delete this
            DELETE
              FROM HIERARCHY_DETAIL
             WHERE HRCHY_HDR_NAME         = 'GLOBAL_HIERARCHY'
               AND HRCHY_DTL_LEVEL        = rec.HRCHY_DTL_LEVEL
               AND HRCHY_DTL_CURR_LVL_VAL = rec.HRCHY_DTL_CURR_LVL_VAL;
        END IF;
    END LOOP;

    --Duplicate parents might have came because of insert from SALES_MANAGER_HIERARCHY into GLOBAL_HIERARCHY
    --Delete the duplicate parents
    FOR REC IN (SELECT HD.HRCHY_HDR_NAME,HD.HRCHY_DTL_CURR_LVL_VAL,HD.HRCHY_DTL_NEXT_LVL_VAL,HD.HRCHY_DTL_LEVEL, COUNT(*) NO_OF_RECS
                  FROM HIERARCHY_DETAIL HD
                 WHERE HD.HRCHY_HDR_NAME  = 'GLOBAL_HIERARCHY'
                   AND HD.HRCHY_DTL_LEVEL <> '6'
              GROUP BY HD.HRCHY_HDR_NAME,HD.HRCHY_DTL_CURR_LVL_VAL,HD.HRCHY_DTL_NEXT_LVL_VAL,HD.HRCHY_DTL_LEVEL
                HAVING COUNT(*) > 1
                 ORDER BY 2) LOOP
        FOR I IN 1..REC.NO_OF_RECS-1 LOOP
            DELETE FROM HIERARCHY_DETAIL 
             WHERE HRCHY_HDR_NAME         = REC.HRCHY_HDR_NAME
               AND HRCHY_DTL_LEVEL        = REC.HRCHY_DTL_LEVEL
               AND HRCHY_DTL_CURR_LVL_VAL = REC.HRCHY_DTL_CURR_LVL_VAL
               AND NVL(HRCHY_DTL_NEXT_LVL_VAL,'XXX') = NVL(REC.HRCHY_DTL_NEXT_LVL_VAL,'XXX')
               AND ROWNUM < 2;  
        END LOOP;
    END LOOP;

    --Delete the records from SALES_MANAGER_HIERARCHY
    DELETE
      FROM HIERARCHY_DETAIL
     WHERE HRCHY_HDR_NAME  = 'SALES_MANAGER_HIERARCHY';

    DELETE
      FROM HIERARCHY_DESCRIPTION
     WHERE HRCHY_HDR_NAME  = 'SALES_MANAGER_HIERARCHY';

    DELETE
      FROM HIERARCHY_HEADER
     WHERE HRCHY_HDR_NAME  = 'SALES_MANAGER_HIERARCHY';

    COMMIT;

    COMMON_TOOLS.ALTER_ALL_TRIGGERS('ENABLE');
    CCN_BATCH_PKG.UNLOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION; 
END;
/
