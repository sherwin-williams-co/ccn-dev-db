create or replace PACKAGE BODY BUSINESS_RULES_PKG
AS
/**********************************************************
  This Package performs all the business rules that are applicable
  for Costomer Deposits application

created : 10/24/2018 sxg151 c517 CCN Project...
Changed :
**********************************************************/

PROCEDURE CHECK_ACCOUNT_NBR(
/******************************************************************************
This procedure is intended to check if any record's sales amount changed from UI

Created : 10/30/2018 sxg151  CCN Project....
        : ASP-1150 : Add business rule to check account number is provided or not..
*******************************************************************************/
    IN_CUSTOMER_ACCOUNT_NUMBER   IN     CUSTOMER_DEPOSIT_HEADER.CUSTOMER_ACCOUNT_NUMBER%TYPE)
IS
    invalid_account_nbr EXCEPTION;
BEGIN
    IF IN_CUSTOMER_ACCOUNT_NUMBER IS NULL THEN
       RAISE invalid_account_nbr;
    END IF;
EXCEPTION
    WHEN invalid_account_nbr THEN 
       errpkg.raise_err(errnums.en_invalid_account_nbr,
                        '"Account Number" should be selected along with "Transaction Date Range"');
    WHEN OTHERS THEN
        errpkg.raise_err(SQLCODE, 'CHECK_ACCOUNT_NBR ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
END CHECK_ACCOUNT_NBR;

PROCEDURE VALIDATE_CUST_DEP_ACCT_TERM_TRAN_NBR(
/******************************************************************************
This procedure is intended to check if any record's sales amount changed from UI

Created : 10/30/2018 sxg151  CCN Project....
Modified: ASP-1150 : Add business rule to check account number is provided or not..
        : 03/13/2019 pxa852 CCN Project Team...
          Add business rule to check the terminal number and transaction number is 99999 or not.
*******************************************************************************/
    IN_HDR_CUST_ACNT_NBR IN     CUSTOMER_DEPOSIT_HEADER.CUSTOMER_ACCOUNT_NUMBER%TYPE,
    IN_DTL_CUST_DPST_REC IN     CUSTOMER_DEPOSIT_TRANSACTION_DTL%ROWTYPE)
IS
    hdr_det_acct_nbr_mismatch_err EXCEPTION;
    invalid_term_and_tran_nbr_err EXCEPTION;
BEGIN
    IF IN_HDR_CUST_ACNT_NBR <> IN_DTL_CUST_DPST_REC.CUSTOMER_ACCOUNT_NUMBER THEN
       RAISE hdr_det_acct_nbr_mismatch_err;
    END IF;
    IF CUSTOMER_DEPOSIT_MAINT_PKG.CHECK_IF_HEADER_RECORD_EXISTS(IN_DTL_CUST_DPST_REC) = 'N' THEN
       IF IN_DTL_CUST_DPST_REC.TERMINAL_NUMBER <> '99999' OR IN_DTL_CUST_DPST_REC.TRANSACTION_NUMBER <> '99999' THEN
          RAISE invalid_term_and_tran_nbr_err;
       END IF;
    END IF;

EXCEPTION
    WHEN hdr_det_acct_nbr_mismatch_err THEN  -- ASP-1150
         errpkg.raise_err(errnums.en_hdr_det_acct_nbr_mismatch_err,
                     '"Account number does not match with the one entered"');
    WHEN invalid_term_and_tran_nbr_err THEN  -- ASP-1211
         errpkg.raise_err(errnums.en_invalid_term_and_tran_nbr,
                     '"Terminal/Transaction is not 99999. Please enter valid Terminal/Transaction number"');
    WHEN OTHERS THEN
         errpkg.raise_err(SQLCODE, 'VALIDATE_HDR_DET_ACCT_NBR ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
END VALIDATE_CUST_DEP_ACCT_TERM_TRAN_NBR;

END BUSINESS_RULES_PKG;