create or replace PACKAGE BODY TABLE_IU_PKG
   AS
/******************************************************************************
This package is intended to control access to the core customer_deposit tables.
created : 02/27/2018 sxh487 

*******************************************************************************/

PROCEDURE CUST_DEPOSIT_HDR_ROWTYPE_SP(
/******************************************************************************
CUST_DEPOSIT_HDR_ROWTYPE_SP

This procedure is intended to build the customer_deposit_details record type
created : 01/16/2018 sxh487 ccn project....
changed : 
*******************************************************************************/
   IN_TABLE_NAME                     IN     VARCHAR2,
   IN_ROW_DATA                       IN     VARCHAR2,
   OUT_CUSTOMER_DEPOSIT_HDR_TYPE     OUT    CUSTOMER_DEPOSIT_HEADER%ROWTYPE)
IS
   CURSOR TABLE_FIELDS_CUR IS
      SELECT COLUMN_NAME,
             DATA_LENGTH COLUMN_SIZE,
             DATA_TYPE
        FROM ALL_TAB_COLUMNS
       WHERE TABLE_NAME = IN_TABLE_NAME
      ORDER BY COLUMN_ID ASC;

   --Variable decalration
   V_CUST_DEPOSIT_HDR_ROW  CUSTOMER_DEPOSIT_HEADER%ROWTYPE;
   V_ROW_VALUE             VARCHAR2(500);
   V_ROW_DATA              SYS.XMLTYPE := XMLTYPE(IN_ROW_DATA);
BEGIN
   FOR TABLE_FIELDS_REC IN TABLE_FIELDS_CUR LOOP
      IF (V_ROW_DATA.EXISTSNODE('/'|| IN_TABLE_NAME|| '/' || TABLE_FIELDS_REC.COLUMN_NAME ||'/text()') = 1) THEN
          V_ROW_VALUE := V_ROW_DATA.EXTRACT(IN_TABLE_NAME|| '/' || TABLE_FIELDS_REC.COLUMN_NAME ||'/text()').GETSTRINGVAL();
         ---v_row_value := common_tools.eliminate_special_chrctrs(v_row_value);
         CASE TABLE_FIELDS_REC.COLUMN_NAME
            WHEN 'CUSTOMER_ACCOUNT_NUMBER' THEN
                V_CUST_DEPOSIT_HDR_ROW.CUSTOMER_ACCOUNT_NUMBER        :=  V_ROW_VALUE;
            WHEN 'CUSTOMER_NAME' THEN
                V_CUST_DEPOSIT_HDR_ROW.CUSTOMER_NAME                  :=  V_ROW_VALUE;
            WHEN 'BILLCONTACT' THEN
                V_CUST_DEPOSIT_HDR_ROW.BILLCONTACT                    :=  V_ROW_VALUE;
            WHEN 'BILLNM' THEN
                V_CUST_DEPOSIT_HDR_ROW.BILLNM                         :=  V_ROW_VALUE;
            WHEN 'BILLADDR1' THEN
                V_CUST_DEPOSIT_HDR_ROW.BILLADDR1                      :=  V_ROW_VALUE;
            WHEN 'BILLADDR2' THEN
                V_CUST_DEPOSIT_HDR_ROW.BILLADDR2                      :=  V_ROW_VALUE;
            WHEN 'BILLCITY' THEN
                V_CUST_DEPOSIT_HDR_ROW.BILLCITY                       :=  V_ROW_VALUE;
            WHEN 'BILLZIP' THEN
                V_CUST_DEPOSIT_HDR_ROW.BILLZIP                        :=  V_ROW_VALUE;
            WHEN 'BILLPHONE' THEN
                V_CUST_DEPOSIT_HDR_ROW.BILLPHONE                      :=  V_ROW_VALUE;
            WHEN 'RLS_RUN_CYCLE' THEN
                V_CUST_DEPOSIT_HDR_ROW.RLS_RUN_CYCLE                  :=  V_ROW_VALUE;
            WHEN 'LOAD_DATE' THEN
                V_CUST_DEPOSIT_HDR_ROW.LOAD_DATE                      :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
            WHEN 'CLEARED_REASON' THEN
                 V_CUST_DEPOSIT_HDR_ROW.CLEARED_REASON                :=  V_ROW_VALUE;
            WHEN 'CLOSED_DATE' THEN
                 V_CUST_DEPOSIT_HDR_ROW.CLOSED_DATE                   :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
            WHEN 'NOTES' THEN
                 V_CUST_DEPOSIT_HDR_ROW.NOTES                         :=  V_ROW_VALUE;
            WHEN 'REFERENCE_NUMBER' THEN
                 V_CUST_DEPOSIT_HDR_ROW.REFERENCE_NUMBER              :=  V_ROW_VALUE;
            ELSE NULL;
         END CASE;
       END IF;
   END LOOP ;
   V_CUST_DEPOSIT_HDR_ROW.LOAD_DATE := SYSDATE;
   OUT_CUSTOMER_DEPOSIT_HDR_TYPE := V_CUST_DEPOSIT_HDR_ROW;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEPOSIT_HDR_ROWTYPE_SP, ' || ' ' || 'IN_TABLE_NAME IS ' || IN_TABLE_NAME, SUBSTR(SQLERRM, 1, 500));
END CUST_DEPOSIT_HDR_ROWTYPE_SP;

PROCEDURE CUSTOMER_DEPOSIT_ROWTYPE_SP(
/******************************************************************************
CUSTOMER_DEPOSIT_ROWTYPE_SP

This procedure is intended to build the customer_deposit_details record type
created : 01/16/2018 sxh487 ccn project....
changed : 
*******************************************************************************/
   IN_TABLE_NAME                 IN     VARCHAR2,
   IN_ROW_DATA                   IN     VARCHAR2,
   OUT_CUSTOMER_DEPOSIT_TYPE     OUT    CUSTOMER_DEPOSIT%ROWTYPE)
IS
   CURSOR TABLE_FIELDS_CUR IS
      SELECT COLUMN_NAME,
             DATA_LENGTH COLUMN_SIZE,
             DATA_TYPE
        FROM ALL_TAB_COLUMNS
       WHERE TABLE_NAME = IN_TABLE_NAME
      ORDER BY COLUMN_ID ASC;

   --Variable decalration
   V_CUSTOMER_DEPOSIT_ROW  CUSTOMER_DEPOSIT%ROWTYPE;
   V_ROW_VALUE             VARCHAR2(500);
   V_ROW_DATA              SYS.XMLTYPE := XMLTYPE(IN_ROW_DATA);
BEGIN
   FOR TABLE_FIELDS_REC IN TABLE_FIELDS_CUR LOOP
      IF (V_ROW_DATA.EXISTSNODE('/'|| IN_TABLE_NAME|| '/' || TABLE_FIELDS_REC.COLUMN_NAME ||'/text()') = 1) THEN
          V_ROW_VALUE := V_ROW_DATA.EXTRACT(IN_TABLE_NAME|| '/' || TABLE_FIELDS_REC.COLUMN_NAME ||'/text()').GETSTRINGVAL();
         ---v_row_value := common_tools.eliminate_special_chrctrs(v_row_value);
         CASE TABLE_FIELDS_REC.COLUMN_NAME
            WHEN 'COST_CENTER_CODE' THEN
                V_CUSTOMER_DEPOSIT_ROW.COST_CENTER_CODE                      :=  V_ROW_VALUE;
            WHEN 'TRANSACTION_DATE' THEN
                V_CUSTOMER_DEPOSIT_ROW.TRANSACTION_DATE                      :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
            WHEN 'TERMINAL_NUMBER' THEN
                V_CUSTOMER_DEPOSIT_ROW.TERMINAL_NUMBER                       :=  V_ROW_VALUE;
            WHEN 'TRANSACTION_NUMBER' THEN
                V_CUSTOMER_DEPOSIT_ROW.TRANSACTION_NUMBER                    :=  V_ROW_VALUE;
            WHEN 'TRANSACTION_GUID' THEN
                V_CUSTOMER_DEPOSIT_ROW.TRANSACTION_GUID                      :=  V_ROW_VALUE;
            WHEN 'CUSTOMER_ACCOUNT_NUMBER' THEN
                V_CUSTOMER_DEPOSIT_ROW.CUSTOMER_ACCOUNT_NUMBER               :=  V_ROW_VALUE;
            WHEN 'POS_TRANSACTION_CODE' THEN
                V_CUSTOMER_DEPOSIT_ROW.POS_TRANSACTION_CODE                  :=  V_ROW_VALUE;
            WHEN 'TRAN_TIMESTAMP' THEN
                V_CUSTOMER_DEPOSIT_ROW.TRAN_TIMESTAMP                        :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY HH12:MI:SS AM');
            WHEN 'NET_SALES' THEN
                V_CUSTOMER_DEPOSIT_ROW.NET_SALES                             :=  V_ROW_VALUE;
            WHEN 'NET_NON_MERCH' THEN
                V_CUSTOMER_DEPOSIT_ROW.NET_NON_MERCH                         :=  V_ROW_VALUE;
            WHEN 'NET_DISCOUNT' THEN
                V_CUSTOMER_DEPOSIT_ROW.NET_DISCOUNT                          :=  V_ROW_VALUE;
            WHEN 'LOAD_DATE' THEN
                V_CUSTOMER_DEPOSIT_ROW.LOAD_DATE                             :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
             ELSE NULL;
         END CASE;
       END IF;
   END LOOP ;
   V_CUSTOMER_DEPOSIT_ROW.LOAD_DATE := SYSDATE;
   OUT_CUSTOMER_DEPOSIT_TYPE := V_CUSTOMER_DEPOSIT_ROW;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUSTOMER_DEPOSIT_ROWTYPE_SP, ' || ' ' || 'IN_TABLE_NAME IS ' || IN_TABLE_NAME, SUBSTR(SQLERRM, 1, 500));
END CUSTOMER_DEPOSIT_ROWTYPE_SP;

PROCEDURE CUST_DEP_DETAILS_ROWTYPE_SP(
/******************************************************************************
CUST_DEP_DETAILS_ROWTYPE_SP

This procedure is intended to build the customer_deposit_details record type
created : 01/16/2018 sxh487 ccn project....
changed : 02/26/2018 sxh487 Added ADJUSTED_DATE for UI adjustments/corrections

*******************************************************************************/
   IN_TABLE_NAME                 IN     VARCHAR2,
   IN_ROW_DATA                   IN     VARCHAR2,
   OUT_CUST_DEP_DET_TYPE         OUT    CUSTOMER_DEPOSIT_DETAILS%ROWTYPE)
IS
   CURSOR TABLE_FIELDS_CUR IS
      SELECT COLUMN_NAME,
             DATA_LENGTH COLUMN_SIZE,
             DATA_TYPE
        FROM ALL_TAB_COLUMNS
       WHERE TABLE_NAME = IN_TABLE_NAME
      ORDER BY COLUMN_ID ASC;

   --Variable decalration
   V_CUST_DEP_DET_ROW  CUSTOMER_DEPOSIT_DETAILS%ROWTYPE;
   V_ROW_VALUE         VARCHAR2(500);
   V_ROW_DATA          SYS.XMLTYPE := XMLTYPE(IN_ROW_DATA);
BEGIN
   FOR TABLE_FIELDS_REC IN TABLE_FIELDS_CUR LOOP
      IF (V_ROW_DATA.EXISTSNODE('/'|| IN_TABLE_NAME|| '/' || TABLE_FIELDS_REC.COLUMN_NAME ||'/text()') = 1) THEN
          V_ROW_VALUE := V_ROW_DATA.EXTRACT(IN_TABLE_NAME|| '/' || TABLE_FIELDS_REC.COLUMN_NAME ||'/text()').GETSTRINGVAL();
         ---v_row_value := common_tools.eliminate_special_chrctrs(v_row_value);
         CASE TABLE_FIELDS_REC.COLUMN_NAME
            WHEN 'COST_CENTER_CODE' THEN
                V_CUST_DEP_DET_ROW.COST_CENTER_CODE                      :=  V_ROW_VALUE;
            WHEN 'TRANSACTION_DATE' THEN
                V_CUST_DEP_DET_ROW.TRANSACTION_DATE                      :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
            WHEN 'TERMINAL_NUMBER' THEN
                V_CUST_DEP_DET_ROW.TERMINAL_NUMBER                       :=  V_ROW_VALUE;
            WHEN 'TRANSACTION_NUMBER' THEN
                V_CUST_DEP_DET_ROW.TRANSACTION_NUMBER                    :=  V_ROW_VALUE;
            WHEN 'TRANSACTION_GUID' THEN
                V_CUST_DEP_DET_ROW.TRANSACTION_GUID                      :=  V_ROW_VALUE;
            WHEN 'CUSTOMER_ACCOUNT_NUMBER' THEN
                V_CUST_DEP_DET_ROW.CUSTOMER_ACCOUNT_NUMBER               :=  V_ROW_VALUE;
            WHEN 'POS_TRANSACTION_CODE' THEN
                V_CUST_DEP_DET_ROW.POS_TRANSACTION_CODE                  :=  V_ROW_VALUE;
            WHEN 'TRAN_TIMESTAMP' THEN
                V_CUST_DEP_DET_ROW.TRAN_TIMESTAMP                        :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY HH12:MI:SS AM');
            WHEN 'TRANSACTION_TYPE' THEN
                V_CUST_DEP_DET_ROW.TRANSACTION_TYPE                      :=  V_ROW_VALUE;
            WHEN 'TOTAL_SALES' THEN
                V_CUST_DEP_DET_ROW.TOTAL_SALES                           :=  V_ROW_VALUE;
            WHEN 'CUSTOMER_NET_BALANCE' THEN
                V_CUST_DEP_DET_ROW.CUSTOMER_NET_BALANCE                  :=  V_ROW_VALUE;
            WHEN 'LOAD_DATE' THEN
                V_CUST_DEP_DET_ROW.LOAD_DATE                             :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
            WHEN 'RLS_RUN_CYCLE' THEN
                V_CUST_DEP_DET_ROW.RLS_RUN_CYCLE                         :=  V_ROW_VALUE;
            WHEN 'ADJUSTED_DATE' THEN
                V_CUST_DEP_DET_ROW.ADJUSTED_DATE                         :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
            WHEN 'REFERENCE_NUMBER' THEN
                V_CUST_DEP_DET_ROW.REFERENCE_NUMBER                      :=  V_ROW_VALUE;
            WHEN 'CLEARED_REASON' THEN
                V_CUST_DEP_DET_ROW.CLEARED_REASON                        :=  V_ROW_VALUE;
            WHEN 'NOTES' THEN
                V_CUST_DEP_DET_ROW.NOTES                                 :=  V_ROW_VALUE;
            WHEN 'CLOSED_DATE' THEN
                V_CUST_DEP_DET_ROW.CLOSED_DATE                           :=  TO_DATE(V_ROW_VALUE,'MM-DD-YYYY');
            WHEN 'GL_DIVISION' THEN
                V_CUST_DEP_DET_ROW.GL_DIVISION                           :=  V_ROW_VALUE;
            ELSE NULL;
         END CASE;
       END IF;
   END LOOP ;
   V_CUST_DEP_DET_ROW.LOAD_DATE := SYSDATE;
   OUT_CUST_DEP_DET_TYPE := V_CUST_DEP_DET_ROW;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_DETAILS_ROWTYPE_SP, ' || ' ' || 'IN_TABLE_NAME IS ' || IN_TABLE_NAME, SUBSTR(SQLERRM, 1, 500));
END CUST_DEP_DETAILS_ROWTYPE_SP;

PROCEDURE CUSTOMER_DEPOSIT_HDR_S_SP (
/*******************************************************************************
	CUSTOMER_DEPOSIT_HDR_S_SP

	This procedure is intended to select records from CUSTOMER_DEPOSIT table

<CUSTOMER_DEPOSIT_HEADER>
  <CUSTOMER_ACCOUNT_NUMBER>280184482</CUSTOMER_ACCOUNT_NUMBER>
</CUSTOMER_DEPOSIT_HEADER>
    
Created : 03/06/2018 sxh487 CCN Project....
*******************************************************************************/
 IN_CUSTOMER_ACCOUNT_NUMBER   IN CUSTOMER_DEPOSIT_HEADER.CUSTOMER_ACCOUNT_NUMBER%TYPE,
 IN_TABLE_NAME                IN     VARCHAR2
,IN_ROW_DATA                  IN     CLOB
,OUT_REF_CUR                  OUT REF_CURSOR)
IS
BEGIN
    OPEN OUT_REF_CUR FOR
        SELECT CUSTOMER_ACCOUNT_NUMBER,
               CUSTOMER_NAME,
               BILLCONTACT,
               BILLNM,
               BILLADDR1,
               BILLADDR2,
               BILLCITY,
               BILLZIP,
               BILLPHONE,
               CLEARED_REASON,
               CLOSED_DATE,
               NOTES,
               REFERENCE_NUMBER,
               COMMON_TOOLS.IS_ACCOUNT_OPEN(IN_CUSTOMER_ACCOUNT_NUMBER) AS ACCT_OPEN_FLAG
          FROM CUSTOMER_DEPOSIT_HEADER
         WHERE NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX')   = NVL(IN_CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'));
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUSTOMER_DEPOSIT_HDR_S_SP, ' || ' ' || 'IN_TABLE_NAME IS ' || IN_TABLE_NAME, SUBSTR(SQLERRM, 1, 500));
END CUSTOMER_DEPOSIT_HDR_S_SP;

PROCEDURE CUST_DEP_DETAILS_S_SP (
/*******************************************************************************
	CUST_DEP_DETAILS_S_SP

	This procedure is intended to select records from CUSTOMER_DEPOSIT_DETAILS table

<CUSTOMER_DEPOSIT_DETAILS>
  <TRANSACTION_DATE_START>08-25-2017</TRANSACTION_DATE_START>
  <TRANSACTION_DATE_END>08-05-2017</TRANSACTION_DATE_END>
  <CUSTOMER_ACCOUNT_NUMBER>280184482</CUSTOMER_ACCOUNT_NUMBER>
</CUSTOMER_DEPOSIT_DETAILS>
    
Created : 01/16/2018 sxh487 CCN Project....
*******************************************************************************/
 IN_CUSTOMER_ACCOUNT_NUMBER   IN CUSTOMER_DEPOSIT_DETAILS.CUSTOMER_ACCOUNT_NUMBER%TYPE,
 IN_TABLE_NAME                IN     VARCHAR2
,IN_ROW_DATA                  IN     CLOB
,OUT_REF_CUR                  OUT REF_CURSOR)
IS
   V_TRANSACTION_DATE_START  CUSTOMER_DEPOSIT_DETAILS.TRANSACTION_DATE%TYPE;
   V_TRANSACTION_DATE_END    CUSTOMER_DEPOSIT_DETAILS.TRANSACTION_DATE%TYPE;
BEGIN
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/'|| IN_TABLE_NAME|| '/'|| 'TRANSACTION_DATE_START/text()') = 1) THEN
        V_TRANSACTION_DATE_START := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/'|| IN_TABLE_NAME|| '/'|| 'TRANSACTION_DATE_START/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;
    IF (XMLTYPE(IN_ROW_DATA).EXISTSNODE('/'|| IN_TABLE_NAME|| '/'|| 'TRANSACTION_DATE_END/text()') = 1) THEN
        V_TRANSACTION_DATE_END   := TO_DATE(XMLTYPE(IN_ROW_DATA).EXTRACT('/'|| IN_TABLE_NAME|| '/'|| 'TRANSACTION_DATE_END/text()').GETSTRINGVAL(),'MM-DD-YYYY');
    END IF;

    OPEN OUT_REF_CUR FOR
         SELECT CDD.COST_CENTER_CODE,
                CDD.TRANSACTION_DATE,
                CDD.TERMINAL_NUMBER,
                CDD.TRANSACTION_NUMBER,
                CDD.CUSTOMER_ACCOUNT_NUMBER,
                CDD.TRAN_TIMESTAMP,
                TRANSACTION_TYPE,
                TOTAL_SALES,
                CUSTOMER_NET_BALANCE,
                ADJUSTED_DATE,
                CDR.ORIGINAL_DEP_TRANS_NBR,
                REFERENCE_NUMBER,
                CLEARED_REASON,
                NOTES,
                CLOSED_DATE,
                GL_DIVISION,
                CDR.ORIGINAL_DEP_TERM_NBR,
                CDR.ORIGINAL_DEP_TRAN_DATE
           FROM CUSTOMER_DEPOSIT_DETAILS CDD, CUST_DEP_REDEMPTION_DETAILS CDR
          WHERE NVL(CDD.CUSTOMER_ACCOUNT_NUMBER,'XXX')    = NVL(IN_CUSTOMER_ACCOUNT_NUMBER,NVL(CDD.CUSTOMER_ACCOUNT_NUMBER,'XXX'))
            AND CDD.CUSTOMER_ACCOUNT_NUMBER = CDR.CUSTOMER_ACCOUNT_NUMBER(+)
            AND CDD.TRANSACTION_DATE = CDR.TRANSACTION_DATE(+)
            AND CDD.TERMINAL_NUMBER  = CDR.TERMINAL_NUMBER(+)
            AND CDD.TRANSACTION_NUMBER = CDR.TRANSACTION_NUMBER(+)
            AND CDD.COST_CENTER_CODE = CDR.COST_CENTER_CODE (+)
            AND NVL(CDD.TRANSACTION_DATE,TRUNC(SYSDATE)) BETWEEN NVL(V_TRANSACTION_DATE_START,NVL(CDD.TRANSACTION_DATE,TRUNC(SYSDATE))) 
                                                    AND NVL(V_TRANSACTION_DATE_END,NVL(CDD.TRANSACTION_DATE,TRUNC(SYSDATE)))
           ORDER BY CDD.CUSTOMER_ACCOUNT_NUMBER, CDD.CLOSED_DATE DESC, CDD.TRAN_TIMESTAMP, CDD.TERMINAL_NUMBER;
            
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_DETAILS_S_SP, ' || ' ' || 'IN_TABLE_NAME IS ' || IN_TABLE_NAME, SUBSTR(SQLERRM, 1, 500));
END CUST_DEP_DETAILS_S_SP;

PROCEDURE CUST_DEPOSIT_HEADER_U_SP (
/*******************************************************************************
    This procedure is intended to updates records in CUSTOMER_DEPOSIT_HEADER table

Created : 04/23/2018 sxh487
Changed : 
*******************************************************************************/
IN_ROW IN     CUSTOMER_DEPOSIT_HEADER%ROWTYPE)
IS
V_ROW         CUSTOMER_DEPOSIT_HEADER_HST%ROWTYPE;
BEGIN
     V_ROW := COMMON_TOOLS.BUILD_DEP_HDR_HST_FROM_CURR(IN_ROW.CUSTOMER_ACCOUNT_NUMBER);
     IF NVL(IN_ROW.CUSTOMER_ACCOUNT_NUMBER,'X') <> NVL(V_ROW.CUSTOMER_ACCOUNT_NUMBER,'X')
       OR NVL(IN_ROW.CUSTOMER_NAME,'X') <> NVL(V_ROW.CUSTOMER_NAME,'X')
       OR NVL(IN_ROW.BILLCONTACT,'X') <> NVL(V_ROW.BILLCONTACT,'X')
       OR NVL(IN_ROW.BILLNM,'X') <> NVL(V_ROW.BILLNM,'X')
       OR NVL(IN_ROW.BILLADDR1,'X') <> NVL(V_ROW.BILLADDR1,'X')
       OR NVL(IN_ROW.BILLADDR2,'X') <> NVL(V_ROW.BILLADDR2,'X')
       OR NVL(IN_ROW.BILLCITY,'X') <> NVL(V_ROW.BILLCITY,'X')
       OR NVL(IN_ROW.BILLZIP,'X') <> NVL(V_ROW.BILLZIP,'X')
       OR NVL(IN_ROW.BILLPHONE,'X') <> NVL(V_ROW.BILLPHONE,'X') THEN
        --inserting in history before updating the current record
        TABLE_IU_PKG.CUST_DEPOSIT_HDR_HIST_I_SP(V_ROW);
        UPDATE CUSTOMER_DEPOSIT_HEADER
           SET ROW = IN_ROW
         WHERE NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX') = NVL(IN_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'));
     ELSIF NVL(IN_ROW.CLOSED_DATE,SYSDATE) <> NVL(V_ROW.CLOSED_DATE,SYSDATE) THEN
          --Audit and close date hist insert
          V_ROW.CLOSED_DATE := IN_ROW.CLOSED_DATE; 
          TABLE_IU_PKG.CUST_DEPOSIT_HDR_HIST_I_SP(V_ROW);
          TABLE_IU_PKG.CUST_DEP_HDR_CLOSING_HST_I_SP(V_ROW);
          UPDATE CUSTOMER_DEPOSIT_HEADER
             SET REFERENCE_NUMBER = NVL(IN_ROW.REFERENCE_NUMBER, REFERENCE_NUMBER),
                 CLEARED_REASON   = NVL(IN_ROW.CLEARED_REASON, CLEARED_REASON),
                 NOTES            = NVL(IN_ROW.NOTES, NOTES),
                 CLOSED_DATE      = NVL(IN_ROW.CLOSED_DATE, CLOSED_DATE)
           WHERE NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX') = NVL(IN_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'));
     END IF;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEPOSIT_HEADER_U_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEPOSIT_HEADER_U_SP;

PROCEDURE CUST_DEP_CREDIT_U_SP (
/*******************************************************************************
    This procedure is intended to updates records in CUST_DEP_CREDIT_DETAILS table

Created : 04/23/2018 sxh487
Changed : 
*******************************************************************************/
IN_ROW IN     CUST_DEP_CREDIT_DETAILS%ROWTYPE)
IS
BEGIN
    UPDATE CUST_DEP_CREDIT_DETAILS
       SET ROW = IN_ROW
     WHERE NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX') = NVL(IN_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'))
       AND NVL(COST_CENTER_CODE,'XXX') =  NVL(IN_ROW.COST_CENTER_CODE,NVL(COST_CENTER_CODE,'XXX'))
       AND NVL(TERMINAL_NUMBER,'XXX')  = NVL(IN_ROW.TERMINAL_NUMBER,NVL(TERMINAL_NUMBER,'XXX'))   
       AND NVL(TRANSACTION_NUMBER,'XXX')  = NVL(IN_ROW.TRANSACTION_NUMBER,NVL(TRANSACTION_NUMBER,'XXX')) 
       AND NVL(TRANSACTION_DATE,TRUNC(SYSDATE)) = NVL(IN_ROW.TRANSACTION_DATE,NVL(TRANSACTION_DATE,TRUNC(SYSDATE)));
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_CREDIT_U_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEP_CREDIT_U_SP;

PROCEDURE CUST_DEP_REDEMPTION_U_SP (
/*******************************************************************************
    This procedure is intended to updates records in CUST_DEP_REDEMPTION_DETAILS table

Created : 04/23/2018 sxh487
Changed : 
*******************************************************************************/
IN_ROW IN     CUST_DEP_REDEMPTION_DETAILS%ROWTYPE)
IS
BEGIN
    UPDATE CUST_DEP_REDEMPTION_DETAILS
       SET ROW = IN_ROW
     WHERE NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX') = NVL(IN_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'))
       AND NVL(COST_CENTER_CODE,'XXX') =  NVL(IN_ROW.COST_CENTER_CODE,NVL(COST_CENTER_CODE,'XXX'))
       AND NVL(TERMINAL_NUMBER,'XXX')  = NVL(IN_ROW.TERMINAL_NUMBER,NVL(TERMINAL_NUMBER,'XXX'))   
       AND NVL(TRANSACTION_NUMBER,'XXX')  = NVL(IN_ROW.TRANSACTION_NUMBER,NVL(TRANSACTION_NUMBER,'XXX')) 
       AND NVL(TRANSACTION_DATE,TRUNC(SYSDATE)) = NVL(IN_ROW.TRANSACTION_DATE,NVL(TRANSACTION_DATE,TRUNC(SYSDATE)));
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_REDEMPTION_U_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEP_REDEMPTION_U_SP;

PROCEDURE CUSTOMER_DEPOSIT_DETAILS_U_SP (
/*******************************************************************************
    This procedure is intended to updates records in CUSTOMER_DEPOSIT_DETAILS table

Created : 04/23/2018 sxh487
Changed : 
*******************************************************************************/
IN_ROW IN     CUSTOMER_DEPOSIT_DETAILS%ROWTYPE)
IS
V_ROW         CUSTOMER_DEPOSIT_DETAILS_HIST%ROWTYPE;
BEGIN
     V_ROW := COMMON_TOOLS.BUILD_DEP_DET_HST_FROM_CURR(IN_ROW);
    --inserting in history before updating the current record
    IF NVL(IN_ROW.CLOSED_DATE,SYSDATE) <> NVL(V_ROW.CLOSED_DATE,SYSDATE) THEN
       --Audit and close date hist insert
       V_ROW.CLOSED_DATE := IN_ROW.CLOSED_DATE;
       TABLE_IU_PKG.CUST_DEP_DET_HST_I_SP(V_ROW);
       TABLE_IU_PKG.CUST_DEP_DET_CLOSING_HST_I_SP(V_ROW);
       UPDATE CUSTOMER_DEPOSIT_DETAILS
          SET REFERENCE_NUMBER = NVL(IN_ROW.REFERENCE_NUMBER, REFERENCE_NUMBER),
              CLEARED_REASON   = NVL(IN_ROW.CLEARED_REASON, CLEARED_REASON),
              NOTES            = NVL(IN_ROW.NOTES, NOTES),
              CLOSED_DATE      = NVL(IN_ROW.CLOSED_DATE, CLOSED_DATE)
       WHERE NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX') = NVL(IN_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'));
    ELSE
       TABLE_IU_PKG.CUST_DEP_DET_HST_I_SP(V_ROW);
       UPDATE CUSTOMER_DEPOSIT_DETAILS
          SET ROW = IN_ROW
        WHERE NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX') = NVL(IN_ROW.CUSTOMER_ACCOUNT_NUMBER,NVL(CUSTOMER_ACCOUNT_NUMBER,'XXX'))
          AND NVL(COST_CENTER_CODE,'XXX') =  NVL(IN_ROW.COST_CENTER_CODE,NVL(COST_CENTER_CODE,'XXX'))
          AND NVL(TERMINAL_NUMBER,'XXX')  = NVL(IN_ROW.TERMINAL_NUMBER,NVL(TERMINAL_NUMBER,'XXX'))   
          AND NVL(TRANSACTION_NUMBER,'XXX')  = NVL(IN_ROW.TRANSACTION_NUMBER,NVL(TRANSACTION_NUMBER,'XXX')) 
          AND NVL(TRANSACTION_DATE,TRUNC(SYSDATE)) = NVL(IN_ROW.TRANSACTION_DATE,NVL(TRANSACTION_DATE,TRUNC(SYSDATE)));
    END IF;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUSTOMER_DEPOSIT_DETAILS_U_SP', SUBSTR(SQLERRM,1,500));
END CUSTOMER_DEPOSIT_DETAILS_U_SP;

PROCEDURE CUST_DEPOSIT_HDR_HIST_I_SP (
/*******************************************************************************
    This procedure is intended to insert records into CUSTOMER_DEPOSIT_HEADER_HST table

Created : 04/23/2018 sxh487 CCN Project....
Changed : 
*******************************************************************************/
IN_ROW     IN     CUSTOMER_DEPOSIT_HEADER_HST%ROWTYPE)
IS
BEGIN
    INSERT INTO CUSTOMER_DEPOSIT_HEADER_HST VALUES IN_ROW;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEPOSIT_HDR_HIST_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEPOSIT_HDR_HIST_I_SP;

PROCEDURE CUST_DEPOSIT_HEADER_I_SP(
/*******************************************************************************
CUST_DEPOSIT_HEADER_I_SP

This procedure is intended to insert into  CUSTOMER_DEPOSIT_HEADER
created : 02/27/2018 SXH487 -- ccn project
changed :

*******************************************************************************/
IN_ROW   IN     CUSTOMER_DEPOSIT_HEADER%ROWTYPE
)
IS
BEGIN
    INSERT INTO CUSTOMER_DEPOSIT_HEADER VALUES IN_ROW;
EXCEPTION
    WHEN DUP_VAL_ON_INDEX THEN
        CUST_DEPOSIT_HEADER_U_SP(IN_ROW);
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEPOSIT_HEADER_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEPOSIT_HEADER_I_SP;

PROCEDURE CUST_DEP_HDR_CLOSING_HST_I_SP(
/*******************************************************************************
CUST_DEP_HDR_CLOSING_HST_I_SP

This procedure is intended to insert into CUST_DEP_HDR_CLOSING_HIST table
to track closing of an Account
created : 05/04/2018 SXH487 -- ccn project

*******************************************************************************/
IN_ROW   IN     CUST_DEP_HDR_CLOSING_HIST%ROWTYPE
)
IS
BEGIN
   INSERT INTO CUST_DEP_HDR_CLOSING_HIST VALUES IN_ROW;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_HDR_CLOSING_HST_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEP_HDR_CLOSING_HST_I_SP;

PROCEDURE CUST_DEP_DET_CLOSING_HST_I_SP(
/*******************************************************************************
CUST_DEP_DET_CLOSING_HST_I_SP
 This procedure is intended to insert records into CUSTOMER_DEPOSIT_DETAILS_HIST table
created : 02/27/2018 SXH487 -- ccn project
changed :
*******************************************************************************/
IN_ROW     IN     CUSTOMER_DEPOSIT_DETAILS_HIST%ROWTYPE)
IS
BEGIN
    INSERT INTO CUST_DEP_DET_CLOSING_HIST VALUES IN_ROW;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_DET_CLOSING_HST_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEP_DET_CLOSING_HST_I_SP;

PROCEDURE CUST_DEPOSIT_I_SP(
/*******************************************************************************
CUST_DEPOSIT_I_SP

This procedure is intended to insert new customer_deposit records
created : 02/27/2018 SXH487 -- ccn project
changed :

*******************************************************************************/
IN_CUST_DEPOSIT_rec   IN     CUSTOMER_DEPOSIT%ROWTYPE,
OUT_TRANGUID          OUT    CUSTOMER_DEPOSIT.TRANSACTION_GUID%TYPE)
IS
BEGIN
   --insert new customer_deposit corrections row
   INSERT INTO CUSTOMER_DEPOSIT VALUES IN_CUST_DEPOSIT_rec RETURNING TRANSACTION_GUID INTO OUT_TRANGUID;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEPOSIT_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEPOSIT_I_SP;

PROCEDURE CUST_DEPOSIT_CREDIT_I_SP(
/*******************************************************************************
CUST_DEPOSIT_CREDIT_I_SP

This procedure is intended to insert into  CUST_DEP_CREDIT_DETAILS
created : 02/27/2018 SXH487 -- ccn project
changed :

*******************************************************************************/
IN_CUST_DEP_CREDIT_rec   IN     CUST_DEP_CREDIT_DETAILS%ROWTYPE
)
IS
BEGIN
   INSERT INTO CUST_DEP_CREDIT_DETAILS VALUES IN_CUST_DEP_CREDIT_rec;
EXCEPTION
    WHEN DUP_VAL_ON_INDEX THEN
        CUST_DEP_CREDIT_U_SP(IN_CUST_DEP_CREDIT_rec);
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEPOSIT_CREDIT_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEPOSIT_CREDIT_I_SP;

PROCEDURE CUST_DEPOSIT_REDEMPTION_I_SP(
/*******************************************************************************
CUST_DEPOSIT_REDEMPTION_I_SP

This procedure is intended to insert into  CUST_DEP_REDEMPTION_DETAILS
created : 02/27/2018 SXH487 -- ccn project
changed :

*******************************************************************************/
IN_CUST_DEP_REDEMPTION_rec   IN     CUST_DEP_REDEMPTION_DETAILS%ROWTYPE
)
IS
BEGIN
   INSERT INTO CUST_DEP_REDEMPTION_DETAILS VALUES IN_CUST_DEP_REDEMPTION_rec;
EXCEPTION
    WHEN DUP_VAL_ON_INDEX THEN
        CUST_DEP_REDEMPTION_U_SP(IN_CUST_DEP_REDEMPTION_rec);
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEPOSIT_REDEMPTION_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEPOSIT_REDEMPTION_I_SP;

PROCEDURE CUST_DEP_DET_I_SP(
/*******************************************************************************
CUST_DEP_DET_I_SP

This procedure is intended to insert new customer_deposit_details records
created : 02/27/2018 SXH487 -- ccn project
changed :

*******************************************************************************/
IN_CUST_DEPOSIT_DETAILS_rec   IN     CUSTOMER_DEPOSIT_DETAILS%ROWTYPE)
IS
BEGIN
   INSERT INTO CUSTOMER_DEPOSIT_DETAILS VALUES IN_CUST_DEPOSIT_DETAILS_rec;
EXCEPTION
    WHEN DUP_VAL_ON_INDEX THEN
        CUSTOMER_DEPOSIT_DETAILS_U_SP(IN_CUST_DEPOSIT_DETAILS_rec);
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_DET_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEP_DET_I_SP;

PROCEDURE CUST_DEP_DET_HST_I_SP (
/*******************************************************************************
 This procedure is intended to insert records into CUSTOMER_DEPOSIT_DETAILS_HIST table
created : 02/27/2018 SXH487 -- ccn project
changed :
*******************************************************************************/
IN_ROW     IN     CUSTOMER_DEPOSIT_DETAILS_HIST%ROWTYPE)
IS
BEGIN
    INSERT INTO CUSTOMER_DEPOSIT_DETAILS_HIST VALUES IN_ROW;
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'CUST_DEP_DET_HST_I_SP', SUBSTR(SQLERRM,1,500));
END CUST_DEP_DET_HST_I_SP;

END TABLE_IU_PKG;