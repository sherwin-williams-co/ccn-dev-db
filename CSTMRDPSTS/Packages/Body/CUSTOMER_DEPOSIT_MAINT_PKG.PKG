create or replace PACKAGE BODY CUSTOMER_DEPOSIT_MAINT_PKG AS 
/*********************************************************** 
This package BODY is intended to hold reuseable objects that are 
available to be used by the entire schema

created : 09/27/2017 sxh487 CCN project.....
************************************************************/
PROCEDURE ORIG_DEP_REM_BAL_UPD(
/**********************************************************
This function updates the original references for a Redemption
and updates the Deposit with remaining balance
 
Created : 03/26/2018 SXH487
**********************************************************/
IN_TOTAL_SALES             IN     CUSTOMER_DEPOSIT_DETAILS.TOTAL_SALES%TYPE, 
IN_CUSTOMER_ACCOUNT_NUMBER IN     CUSTOMER_DEPOSIT_DETAILS.CUSTOMER_ACCOUNT_NUMBER%TYPE, 
IN_TRANSACTION_DATE        IN     CUSTOMER_DEPOSIT_DETAILS.TRANSACTION_DATE%TYPE,
OUT_TRANSACTION_NUMBER        OUT CUSTOMER_DEPOSIT_DETAILS.TRANSACTION_NUMBER%TYPE,
OUT_TERMINAL_NUMBER           OUT CUSTOMER_DEPOSIT_DETAILS.TERMINAL_NUMBER%TYPE,
OUT_TRANSACTION_DATE          OUT CUSTOMER_DEPOSIT_DETAILS.TRANSACTION_DATE%TYPE
)
IS
    CURSOR ALL_DEPS IS
        SELECT a.*, rowid, count(*) over () TOT_DEP_CNT
          FROM CUST_DEP_CREDIT_DETAILS a
         WHERE CUSTOMER_ACCOUNT_NUMBER = IN_CUSTOMER_ACCOUNT_NUMBER
           AND TRANSACTION_DATE <= IN_TRANSACTION_DATE
           AND DEPOSIT_REMAINING_BAL > 0
           ORDER BY TRAN_TIMESTAMP;
    
    V_CNT           NUMBER :=0;
    V_DEP_REM_BAL   CUST_DEP_CREDIT_DETAILS.DEPOSIT_REMAINING_BAL%TYPE;
    V_TOTAL_SALES   CUSTOMER_DEPOSIT_DETAILS.TOTAL_SALES%TYPE := -(IN_TOTAL_SALES);
BEGIN  
    FOR each_dep IN ALL_DEPS LOOP
        V_CNT := V_CNT +1;
        OUT_TRANSACTION_NUMBER := each_dep.TRANSACTION_NUMBER;
        OUT_TERMINAL_NUMBER    := each_dep.TERMINAL_NUMBER;
        OUT_TRANSACTION_DATE   := each_dep.TRANSACTION_DATE;
        V_DEP_REM_BAL          := NULL;

        --if the redemption is greater than the balance on the deposit
        --then update the Deposit with -ve bal
        IF V_TOTAL_SALES > each_dep.DEPOSIT_REMAINING_BAL THEN
           --update only the last DEPOSIT with remaining balance       
           IF V_CNT = each_dep.TOT_DEP_CNT THEN
              V_DEP_REM_BAL := each_dep.DEPOSIT_REMAINING_BAL - V_TOTAL_SALES;
           ELSE
              V_DEP_REM_BAL :=0;
           END IF;
           V_TOTAL_SALES   := V_TOTAL_SALES - each_dep.DEPOSIT_REMAINING_BAL;
        ELSE
            V_DEP_REM_BAL := each_dep.DEPOSIT_REMAINING_BAL - V_TOTAL_SALES;
            V_TOTAL_SALES := 0;
        END IF;
        UPDATE CUST_DEP_CREDIT_DETAILS
           SET DEPOSIT_REMAINING_BAL = V_DEP_REM_BAL
          WHERE ROWID      = each_dep.rowid;

        IF V_TOTAL_SALES = 0 THEN
            EXIT;
        END IF;
    END LOOP;
END ORIG_DEP_REM_BAL_UPD;

END CUSTOMER_DEPOSIT_MAINT_PKG;