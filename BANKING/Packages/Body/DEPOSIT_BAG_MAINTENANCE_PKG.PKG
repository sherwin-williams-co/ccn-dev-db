create or replace PACKAGE BODY DEPOSIT_BAG_MAINTENANCE_PKG AS
/**********************************************************
This Package contains all the procedures used in
deposit bag update maintenance window

Created : 07/15/2015 axk326/jxc517 CCN Project....
Changed :
**********************************************************/
PROCEDURE BANK_DEP_BAG_TICK_UI_SP (
/*******************************************************************************
	This procedure is intended to RETURN a ref cursor with data from BANK_DEP_BAG_TICK table

Inputs:
    IN_COST_CENTER_CODE

Created : 07/15/2015 axk326/jxc517 CCN Project....
Changed : 
*******************************************************************************/
IN_COST_CENTER_CODE IN     VARCHAR2
,OUT_REF_CUR           OUT REF_CURSOR)
IS
BEGIN
    OPEN OUT_REF_CUR FOR
        SELECT *
          FROM BANK_DEP_BAG_TICK
         WHERE COST_CENTER_CODE = IN_COST_CENTER_CODE;
END BANK_DEP_BAG_TICK_UI_SP;

PROCEDURE BUILD_TABLE_TYPES(
/******************************************************************************
This procedure is intended to build all the table types from input xml
for this process

Created : 07/17/2015 axk326/jxc517 CCN Project....
Changed : 
*******************************************************************************/
    IN_XML                         IN     CLOB,
    O_BANK_DEP_BAG_TICK_REC           OUT BANK_DEP_BAG_TICK%ROWTYPE)
IS
    V_XML_BANK_DEP_BAG_TICK_FRAG    CLOB;
    V_ROW_DATA                      XMLTYPE := SYS.XMLTYPE(IN_XML);
BEGIN
    IF (V_ROW_DATA.EXISTSNODE('/BANK_DEP_BAG_TICK_UI/BANK_DEP_BAG_TICK') = 1) THEN --Single Record
        V_XML_BANK_DEP_BAG_TICK_FRAG := V_ROW_DATA.EXTRACT('/BANK_DEP_BAG_TICK_UI/BANK_DEP_BAG_TICK').GETCLOBVAL();
    END IF; 
    IF V_XML_BANK_DEP_BAG_TICK_FRAG IS NOT NULL  THEN
        TABLE_IU_PKG.BANK_DEP_BAG_TICK_ROWTYPE_SP('BANK_DEP_BAG_TICK',
                                                   V_XML_BANK_DEP_BAG_TICK_FRAG,
                                                   O_BANK_DEP_BAG_TICK_REC);
    END IF;
EXCEPTION
    WHEN OTHERS THEN                        
        ERRPKG.RAISE_ERR(SQLCODE, 'BUILD_TABLE_TYPES ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
END BUILD_TABLE_TYPES;

PROCEDURE DEPOSIT_BAG_UPDT_UI_UPSERT_SP ( 
/******************************************************************************
This procedure is intended to update following tables used in the Deposit Bag Update Window
    Tables: 
       BANK_DEP_BAG_TICK

Created : 07/15/2015 axk326/jxc517 CCN Project....
Changed : 08/13/2015 nxk927 CCN Project....
          removed trunc to take the time stamp
*******************************************************************************/
    IN_ROW_DATA  IN  CLOB)
IS
    V_BANK_DEP_BAG_TICK_REC         BANK_DEP_BAG_TICK%ROWTYPE;
    V_HIST_ROW                      BANK_DEP_BAG_TICK%ROWTYPE;
BEGIN
    BUILD_TABLE_TYPES(IN_ROW_DATA,
                      V_BANK_DEP_BAG_TICK_REC);
    V_BANK_DEP_BAG_TICK_REC.EFFECTIVE_DATE  := SYSDATE;
    BUSINESS_RULES_PKG.DEPOSIT_BAG_TICKET_MNTNC_BR_SP(V_BANK_DEP_BAG_TICK_REC);
    BEGIN
        SELECT *
          INTO V_HIST_ROW
          FROM BANK_DEP_BAG_TICK
         WHERE COST_CENTER_CODE = V_BANK_DEP_BAG_TICK_REC.COST_CENTER_CODE;
        TABLE_IU_PKG.BANK_DEP_BAG_TICK_HIST_I_SP(V_HIST_ROW);
    EXCEPTION
        WHEN OTHERS THEN
            NULL;
    END;
    TABLE_IU_PKG.BANK_DEP_BAG_TICK_I_SP(V_BANK_DEP_BAG_TICK_REC);
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE, 'DEPOSIT_BAG_UPDT_UI_UPSERT_SP ' || SUBSTR(SQLERRM,1,500) || ' IN OTHERS ' );
END DEPOSIT_BAG_UPDT_UI_UPSERT_SP;

END DEPOSIT_BAG_MAINTENANCE_PKG;