create or replace PACKAGE BODY BANKING_BATCH_PKG
AS
/**********************************************************
  This Package is a wrapper for all batch processes of banking application

Created : 07/21/2015 axk326/jxc517 CCN Project....
Changed :
**********************************************************/
PROCEDURE CURRENT_TO_HISTORY_PROCESS
/******************************************************************************
  This procedure is a wrapper to call CURRENT_TO_HISTORY_PKG.PROCESS

Created : 07/21/2015 axk326/jxc517 CCN Project....
Changed :
*******************************************************************************/
IS
BEGIN
   CURRENT_TO_HISTORY_PKG.PROCESS();
END CURRENT_TO_HISTORY_PROCESS;

PROCEDURE FUTURE_TO_CURRENT_PROCESS
/******************************************************************************
  This procedure is a wrapper to call FUTURE_TO_CURRENT_PKG.PROCESS

Created : 07/21/2015 axk326/jxc517 CCN Project....
Changed :
*******************************************************************************/
IS
BEGIN
    FUTURE_TO_CURRENT_PKG.PROCESS();
END FUTURE_TO_CURRENT_PROCESS;

PROCEDURE LOAD_MEMER_BANK_CONCENT_CC(
/*******************************************************************
  This procedure will load the data in MEMBER_BANK_CONCENTRATION_CC table.

Created : sxg151 10/30/2017
Changed :
********************************************************************/
IN_LOAD_DATE  IN DATE)
IS
   CURSOR member_bank_cc_cur IS
      SELECT LEAD_STORE_NBR,
             Member_Store_Nbr
        FROM Member_Bank_Cc;
BEGIN
DELETE  FROM MEMBER_BANK_CONCENTRATION_CC WHERE LOAD_DATE = IN_LOAD_DATE;

   FOR member_bank_cc_cur_rec IN member_bank_cc_cur LOOP
       INSERT INTO MEMBER_BANK_CONCENTRATION_CC(LEAD_STORE_NBR,
                                                MEMBER_STORE_NBR,
                                                LOAD_DATE )
       VALUES(member_bank_cc_cur_rec.LEAD_STORE_NBR,
              member_bank_cc_cur_rec.MEMBER_STORE_NBR,
              IN_LOAD_DATE );
   END LOOP;
   Commit;
END LOAD_MEMER_BANK_CONCENT_CC;

PROCEDURE STORE_BANK_DPST_RECON_DATA(
/**********************************************************
This procedure is intended to generate a file with the
Store Bank  Deposit Daily Reconciliation data for the last
2 weeks

Created  : 09/12/2016 mxk766 CCN project....
modified : 10/05/2016 nxk927 CCN project....
           passing banking_auto_rec_ind from the bank_account table
         : 03/01/2018 sxg151 Added Cursor instead Added SQL into the FOR Loop
         : Renamed Procedure STORE_BANK_DPST_RECON_DT To STORE_BANK_DPST_RECON_DATA
**********************************************************/
    IN_RUN_PERIOD   IN     NUMBER,
    IN_SERVER_NAME  IN     VARCHAR2,
    IN_LOG_FILE     IN     VARCHAR2)
IS
    V_CLOB                         CLOB;
    V_BATCH_NUMBER                 BATCH_JOB.BATCH_JOB_NUMBER%TYPE;
    V_TRANS_STATUS                 BATCH_JOB.TRANS_STATUS%TYPE;
    C_STRING                       VARCHAR2(1) := '"';
    C_DLMTR                        VARCHAR2(1) := ',';
    C_DIS_STRING                   VARCHAR2(1) := '=';
    
    -- Passing banking_auto_rec_ind from the bank_account table
    CURSOR cur_ach_drfts_extrct_cntrl IS  
        SELECT A.*,(SELECT BANK_AUTO_RECON_IND
                      FROM BANK_ACCOUNT
                     WHERE LPAD(REPLACE(BANK_ACCOUNT_NBR, '-',''),17,0) = A.BANK_ACCOUNT_NBR
                       AND ROWNUM = 1
                    ) BANK_AUTO_REC_IND
         FROM  ACH_DRFTS_EXTRCT_CNTRL_FL_VW A
         WHERE TRUNC(TRANSACTION_DATE) >= (TRUNC(SYSDATE)-IN_RUN_PERIOD);
BEGIN
    CCN_BATCH_PKG.INSERT_BATCH_JOB('STOREBANK_DPST_RECON', V_BATCH_NUMBER,IN_SERVER_NAME,IN_LOG_FILE);
    CCN_BATCH_PKG.LOCK_DATABASE_SP();

    FOR rec IN cur_ach_drfts_extrct_cntrl LOOP
        V_CLOB :=                  C_STRING || rec.COST_CENTER_CODE      || C_STRING || C_DLMTR ||
                                               rec.CENTURY                           || C_DLMTR ||
                                               rec.BANK_DEP_AMT                      || C_DLMTR ||
                   C_DIS_STRING || C_STRING || rec.BANK_ACCOUNT_NBR      || C_STRING || C_DLMTR ||
                                               rec.BANK_AUTO_REC_IND                 || C_DLMTR ||
                                               rec.TRANSACTION_DATE                  || C_DLMTR ||
                                               rec.LOAD_DATE             || CHR(10)
                 || V_CLOB;
    END LOOP;
    IF V_CLOB <> EMPTY_CLOB() THEN
        MAIL_PKG.SEND_MAIL('STORE_BANK_DPST_RECON_DT', NULL, NULL,V_CLOB);
    END IF;

    CCN_BATCH_PKG.UPDATE_BATCH_JOB('STOREBANK_DPST_RECON', V_BATCH_NUMBER, V_TRANS_STATUS);
    CCN_BATCH_PKG.UNLOCK_DATABASE_SP();

EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.RAISE_ERR(SQLCODE,'ERROR IN STORE_BANK_DPST_RECON_DATA ' || SUBSTR(SQLERRM, 1, 500) || ' IN OTHERS ');
END STORE_BANK_DPST_RECON_DATA;

END BANKING_BATCH_PKG;