CREATE OR REPLACE PACKAGE BODY BANKING_COMMON_TOOLS
AS
/**************************************************************** 
This package will have Banking specific tools

created : 07/02/2015 sxh487 CCN Project....
changed :
*****************************************************************/
FUNCTION EXTRACT_TABLE_CLOB (
/*******************************************************************************
	This procedure will extract the required clob from passed XML clob
  and sends back the result as clob

Created : 03/30/2015 jxc517 CCN Project....
Changed : 
*******************************************************************************/
IN_TABLE_NAME IN     VARCHAR2
,IN_ROW_DATA  IN     CLOB) RETURN CLOB
IS
BEGIN
    RETURN SYS.XMLTYPE(IN_ROW_DATA).EXTRACT('/' || IN_TABLE_NAME ||'_UI_SP/'|| IN_TABLE_NAME).getCLOBVal();
EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END EXTRACT_TABLE_CLOB;

FUNCTION GET_LEAD_STORE_NBR_FOR_MBR_STR(
/*******************************************************************************
  This function will get the current lead store for the passed member store

Created : 06/19/2015 sxh487/jxc517 CCN Project....
Changed : 
*******************************************************************************/
  IN_MEMBER_STORE_NBR   IN MEMBER_BANK_CC.MEMBER_STORE_NBR%TYPE) RETURN VARCHAR2
IS
    V_LEAD_STORE_NBR  MEMBER_BANK_CC.LEAD_STORE_NBR%TYPE;
BEGIN
    SELECT LEAD_STORE_NBR 
      INTO V_LEAD_STORE_NBR
      FROM MEMBER_BANK_CC
     WHERE MEMBER_STORE_NBR = IN_MEMBER_STORE_NBR;
    RETURN V_LEAD_STORE_NBR;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_LEAD_STORE_NBR;
END GET_LEAD_STORE_NBR_FOR_MBR_STR;

FUNCTION GET_LEAD_BANK_CC_REC(
/***********************************************************
	This function will get the lead bank record for passed lead
  store number
  
Created : 06/08/2015 nxk927
Changed : 
************************************************************/
    IN_LEAD_STORE_NBR           IN     VARCHAR2) RETURN LEAD_BANK_CC%ROWTYPE
IS
    V_LEAD_BANK_CC_REC LEAD_BANK_CC%ROWTYPE;
BEGIN
    SELECT *
      INTO V_LEAD_BANK_CC_REC
      FROM LEAD_BANK_CC
     WHERE LEAD_STORE_NBR = IN_LEAD_STORE_NBR;
    RETURN V_LEAD_BANK_CC_REC;
EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END GET_LEAD_BANK_CC_REC;

FUNCTION GET_LEAD_BANK_CC_HIST_REC(
/***********************************************************
	This function will get the lead bank record from current tables
  for the passed lead store number and effective date
  If not found it will try to get the details from the history tables
  based on lead store number and effective date
  
Created : 07/22/2015 sxh487/jxc517
Changed : 
************************************************************/
    IN_LEAD_STORE_NBR           IN     VARCHAR2,
    IN_EFFECTIVE_DATE           IN     LEAD_BANK_CC.EFFECTIVE_DATE%TYPE) RETURN LEAD_BANK_CC%ROWTYPE
IS
    V_LEAD_BANK_CC_REC LEAD_BANK_CC%ROWTYPE;
BEGIN
    BEGIN
        SELECT *
         INTO V_LEAD_BANK_CC_REC
         FROM LEAD_BANK_CC
        WHERE LEAD_STORE_NBR = IN_LEAD_STORE_NBR
          AND IN_EFFECTIVE_DATE BETWEEN EFFECTIVE_DATE AND NVL(EXPIRATION_DATE, IN_EFFECTIVE_DATE);
    EXCEPTION
        WHEN OTHERS THEN
            NULL;
    END;    
    IF V_LEAD_BANK_CC_REC.LEAD_BANK_ACCOUNT_NBR IS NULL THEN
        SELECT *
          INTO V_LEAD_BANK_CC_REC
          FROM LEAD_BANK_CC_HIST
         WHERE LEAD_STORE_NBR = IN_LEAD_STORE_NBR
           AND IN_EFFECTIVE_DATE BETWEEN EFFECTIVE_DATE AND NVL(EXPIRATION_DATE, IN_EFFECTIVE_DATE);
    END IF;
    RETURN V_LEAD_BANK_CC_REC;
EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END GET_LEAD_BANK_CC_HIST_REC;

FUNCTION GET_LEAD_BANK_CC_FUTURE_REC(
/***********************************************************
	This function will return the lead bank future record
  based on bank account number, lead store number and future id
  
Created : 07/20/2015 jxc517 CCN Project....
Changed  : 
************************************************************/
    IN_LEAD_BANK_ACCOUNT_NBR IN     VARCHAR2,
    IN_LEAD_STORE_NBR        IN     VARCHAR2,
    IN_FUTURE_ID             IN     NUMBER) RETURN LEAD_BANK_CC_FUTURE%ROWTYPE
IS
    V_LEAD_BANK_CC_FUTURE_REC LEAD_BANK_CC_FUTURE%ROWTYPE;
BEGIN
    SELECT *
      INTO V_LEAD_BANK_CC_FUTURE_REC
      FROM LEAD_BANK_CC_FUTURE
     WHERE LEAD_BANK_ACCOUNT_NBR = IN_LEAD_BANK_ACCOUNT_NBR
       AND LEAD_STORE_NBR        = IN_LEAD_STORE_NBR
       AND FUTURE_ID             = IN_FUTURE_ID;
    RETURN V_LEAD_BANK_CC_FUTURE_REC;
EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END GET_LEAD_BANK_CC_FUTURE_REC;

FUNCTION GET_BANK_ACCOUNT_REC(
/***********************************************************
	This function will get the bank account record for passed
  bank account number
  
Created : 07/23/2015 jxc517 CCN Project....
Changed : 
************************************************************/
    IN_BANK_ACCOUNT_NBR IN     VARCHAR2) RETURN BANK_ACCOUNT%ROWTYPE
IS
    V_BANK_ACCOUNT_REC BANK_ACCOUNT%ROWTYPE;
BEGIN
    SELECT *
      INTO V_BANK_ACCOUNT_REC
      FROM BANK_ACCOUNT
     WHERE BANK_ACCOUNT_NBR = IN_BANK_ACCOUNT_NBR;
    RETURN V_BANK_ACCOUNT_REC;
EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END GET_BANK_ACCOUNT_REC;

FUNCTION GET_BANK_ACCOUNT_HIST_REC(
/***********************************************************
	This function will get the bank account record from current tables
  for the passed bank account number and effective date
  If not found it will try to get the details from the history tables
  based on bank account number and effective date
  
Created : 07/22/2015 sxh487/jxc517
Changed : 
************************************************************/
    IN_BANK_ACCOUNT_NBR           IN     VARCHAR2,
    IN_EFFECTIVE_DATE             IN     BANK_ACCOUNT.EFFECTIVE_DATE%TYPE) RETURN BANK_ACCOUNT%ROWTYPE
IS
    V_BANK_ACCOUNT_REC BANK_ACCOUNT%ROWTYPE;
BEGIN
   BEGIN
       SELECT *
         INTO V_BANK_ACCOUNT_REC
         FROM BANK_ACCOUNT
        WHERE BANK_ACCOUNT_NBR = IN_BANK_ACCOUNT_NBR
          AND IN_EFFECTIVE_DATE BETWEEN EFFECTIVE_DATE AND NVL(EXPIRATION_DATE, IN_EFFECTIVE_DATE);
   EXCEPTION
       WHEN OTHERS THEN
           NULL;
   END;
    IF V_BANK_ACCOUNT_REC.BANK_ACCOUNT_NBR IS NULL THEN
       SELECT *
         INTO V_BANK_ACCOUNT_REC
         FROM BANK_ACCOUNT_HIST
        WHERE BANK_ACCOUNT_NBR = IN_BANK_ACCOUNT_NBR
          AND IN_EFFECTIVE_DATE BETWEEN EFFECTIVE_DATE AND NVL(EXPIRATION_DATE, IN_EFFECTIVE_DATE);
    END IF;
    
    RETURN V_BANK_ACCOUNT_REC;
EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END GET_BANK_ACCOUNT_HIST_REC;

FUNCTION GET_BANK_TYPE_CODE(
/*******************************************************************************
  This function will get the bank type code for the passed store number

Created : 07/13/2015 jxc517 CCN Project....
Changed : 
*******************************************************************************/
IN_COST_CENTER    IN     LEAD_BANK_CC.LEAD_STORE_NBR%TYPE) RETURN VARCHAR2
IS
    V_BANK_TYPE_CODE VARCHAR2(1);
BEGIN
    SELECT BANK_TYPE_CODE 
      INTO V_BANK_TYPE_CODE
      FROM (SELECT BANK_TYPE_CODE
              FROM LEAD_BANK_CC
             WHERE LEAD_STORE_NBR = IN_COST_CENTER
             UNION
            SELECT 'M'
              FROM MEMBER_BANK_CC
             WHERE MEMBER_STORE_NBR = IN_COST_CENTER);
    RETURN V_BANK_TYPE_CODE;
EXCEPTION
    WHEN OTHERS THEN
        RETURN V_BANK_TYPE_CODE;
END GET_BANK_TYPE_CODE;

FUNCTION GET_FUTURE_ID(
/***********************************************************
	This fucntion will get next future id for the 
  passed bank account number

Created : 07/08/2015 nxk927/jxc517 CCN Project Team....
Changed : 
************************************************************/
IN_BANK_ACCOUNT_NBR IN VARCHAR2) RETURN NUMBER
IS
    V_FUTURE_ID NUMBER := 1;
BEGIN
    SELECT NVL(MAX(FUTURE_ID), 0) + 1
      INTO V_FUTURE_ID
      FROM BANK_ACCOUNT_FUTURE
     WHERE BANK_ACCOUNT_NBR = IN_BANK_ACCOUNT_NBR;
    RETURN V_FUTURE_ID;
EXCEPTION
    WHEN OTHERS THEN
        RETURN NULL;
END GET_FUTURE_ID;

END BANKING_COMMON_TOOLS;

