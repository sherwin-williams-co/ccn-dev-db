CREATE OR REPLACE PACKAGE BODY DPST_BAGS_UPDATE_BATCH_PKG
AS
/**********************************************************
  This Package contains procedured to update the deposit bag tickets on a daily
  basis based on the files we recieve from POS

Created : 10/20/2015 jxc517 CCN Project....
Changed :
**********************************************************/
PROCEDURE PROCESS
/******************************************************************************
  This procedure will update the deposit bag tickets on a daily
  basis based on the files we recieve from POS

Created : 10/20/2015 jxc517 CCN Project....
Changed : 10/21/2015 jxc517 CCN Project....
          Added code to reset ytd deposit tickets on january 1st of every year
Changed : 11/04/2015 nxk927 CCN Project....
          added update for the orders that reach today's date 
        : 11/05/2015 nxk927 CCN Project....
          added LAST_MAINT_USER_ID <> 'BATCH' so there won't be multiple updates  
        : 11/13/2015 nxk927 CCN Project....
          Added condition to take the actual current record
*******************************************************************************/
IS
V_BATCH_NUMBER              BATCH_JOB.BATCH_JOB_NUMBER%TYPE;
V_TRANS_STATUS              BATCH_JOB.TRANS_STATUS%TYPE := 'SUCCESSFUL';
V_DEPBAG_REORDER_QTY        BANK_DEP_BAG_TICK.DEPBAG_REORDER_QTY%TYPE; 
BEGIN
    CCN_BATCH_PKG.INSERT_BATCH_JOB('DPST_BAGS_UPDT_BTCH', V_BATCH_NUMBER);
    CCN_BATCH_PKG.LOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION;
    BEGIN
        IF TO_CHAR(SYSDATE,'DDMON') = '01JAN' THEN
            FOR rec IN (SELECT * FROM BANK_DEP_BAG_TICK) LOOP
                TABLE_IU_PKG.BANK_DEP_BAG_TICK_HIST_I_SP(rec);
                rec.DEPBAG_YTD_ORDERED_QTY := 0;
                rec.EFFECTIVE_DATE  := LEAST(NVL(rec.EXPIRATION_DATE, SYSDATE), SYSDATE);
                TABLE_IU_PKG.BANK_DEP_BAG_TICK_I_SP(rec);
            END LOOP;
        END IF;
        FOR rec IN (SELECT COST_CENTER_CODE, SUM(TO_NUMBER(POS_COUNT)) POS_COUNT
                      FROM TEMP_DPST_TCKT_POS_CNTS
                     GROUP BY COST_CENTER_CODE
                     HAVING SUM(TO_NUMBER(POS_COUNT)) > 0) LOOP
            BEGIN
               --TBD as we have multiple records in this file for a store
                UPDATE BANK_DEP_BAG_TICK
                   SET DEPBAG_ONHAND_QTY      = NVL(DEPBAG_ONHAND_QTY,0) - rec.POS_COUNT
                 WHERE SUBSTR(COST_CENTER_CODE, 3) = rec.COST_CENTER_CODE
                   AND TRUNC(EFFECTIVE_DATE)      <= TRUNC(SYSDATE);
            EXCEPTION
                WHEN OTHERS THEN
                    ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'DPST_BAGS_UPDATE_BATCH_PKG.PROCESS', SQLERRM, '1111111111111111' , rec.COST_CENTER_CODE, 'BANK_ACCOUNT');
            END;
        END LOOP;
        FOR rec IN (SELECT *
                      FROM BANK_DEP_BAG_TICKORD A
                     WHERE (COST_CENTER_CODE, BANK_ACCOUNT_NBR) IN (SELECT COST_CENTER_CODE, BANK_ACCOUNT_NBR
                                                  FROM BANK_DEP_BAG_TICK
                                                 WHERE DEPBAG_ONHAND_QTY     <= DEP_BAG_REORDER_POINT
                                                   AND TRUNC(EFFECTIVE_DATE) <= TRUNC(SYSDATE))) LOOP
                                                   --Only get active record not the virtual
            BEGIN
                DEPOSIT_BAG_MAINTENANCE_PKG.PLACE_DEPOSIT_BAG_TICKET_ORDER(rec.COST_CENTER_CODE,
                                                                           TRUNC(SYSDATE),
                                                                           NULL,
                                                                           'BATCH');
            EXCEPTION
                WHEN OTHERS THEN
                    ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'DPST_BAGS_UPDATE_BATCH_PKG.PROCESS', SQLERRM, '1111111111111111' , rec.COST_CENTER_CODE, 'BANK_ACCOUNT');
            END;
        END LOOP;
        V_DEPBAG_REORDER_QTY                       := BANKING_COMMON_TOOLS.GET_STANDARD_VAL_DEPOSIT_ORD('DEPBAG_REORDER_QTY');
        FOR rec IN (SELECT *
                      FROM BANK_DEP_BAG_TICKORD
                     WHERE ORDER_DATE = TRUNC(SYSDATE)
                       AND LAST_MAINT_USER_ID <> 'BATCH') LOOP --exclude batch as is already taken care before this call
            --This will update all the deposit bag if the order hits today's date
            BEGIN
                   UPDATE BANK_DEP_BAG_TICK
                      SET DEPBAG_ONHAND_QTY      = NVL(DEPBAG_ONHAND_QTY,0) + (NVL(DEPBAG_REORDER_QTY,V_DEPBAG_REORDER_QTY)),
                          DEPBAG_YTD_ORDERED_QTY = NVL(DEPBAG_YTD_ORDERED_QTY,0) + (NVL(DEPBAG_REORDER_QTY,V_DEPBAG_REORDER_QTY))
                    WHERE COST_CENTER_CODE       = rec.COST_CENTER_CODE
                      AND BANK_ACCOUNT_NBR       = rec.BANK_ACCOUNT_NBR
                      AND TRUNC(EFFECTIVE_DATE) <= TRUNC(SYSDATE);
            EXCEPTION
                WHEN OTHERS THEN
                    ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'DPST_BAGS_UPDATE_BATCH_PKG.PROCESS', SQLERRM, '1111111111111111' , rec.COST_CENTER_CODE, 'BANK_ACCOUNT');
            END;
        END LOOP;
    EXCEPTION
        WHEN OTHERS THEN
            V_TRANS_STATUS := 'ERROR';
            ROLLBACK;
    END;
    COMMIT;
    CCN_BATCH_PKG.UPDATE_BATCH_JOB('DPST_BAGS_UPDT_BTCH', V_BATCH_NUMBER, V_TRANS_STATUS);
    CCN_BATCH_PKG.UNLOCK_DATABASE_SP(); -- PRAGMA AUTONOMOUS_TRANSACTION; 
EXCEPTION
    WHEN OTHERS THEN
        ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'DPST_BAGS_UPDATE_BATCH_PKG.PROCESS', SQLERRM, '1111111111', '111111', 'OTHER');
END PROCESS;

END DPST_BAGS_UPDATE_BATCH_PKG;

