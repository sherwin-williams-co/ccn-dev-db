create or replace PACKAGE BODY CURRENT_TO_HISTORY_PKG
AS
/**********************************************************
  This Package contains procedured to move the current table records into
  history tables

Created : 05/26/2015 jxc517 CCN Project....
Changed :
**********************************************************/

PROCEDURE PROCESS_BANK_DEPOSIT_TABLES
/******************************************************************************
  This procedure will move the current table records into history tables
  Tables:
      BANK_DEP_TICK
      BANK_DEP_BAG_TICK
      BANK_DEP_BAG_TICKORD
      BANK_DEP_TICKORD

Created : 08/13/2015 jxc517 CCN Project....
Changed : 10/30/2015 nxk927 CCN Project....
           added BANK_DEP_BAG_TICKORD
        : 01/28/2019 pxa852 CCN Project Team...
          Modified code to pass null value to bank account number
          when inserting the exception record into error log table
*******************************************************************************/
IS
BEGIN
    FOR rec IN (SELECT *
                  FROM STORE_MICR_FORMAT_DTLS
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            TABLE_IU_PKG.STORE_MICR_FORMAT_DTLS_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_DEPOSIT_TABLES', SQLERRM, rec.BANK_ACCOUNT_NBR, rec.COST_CENTER_CODE, 'STORE_MICR_FORMAT_DTLS');
        END;
    END LOOP;
    FOR rec IN (SELECT *
                  FROM BANK_DEP_TICK
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            TABLE_IU_PKG.BANK_DEP_TICK_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_DEPOSIT_TABLES', SQLERRM, rec.BANK_ACCOUNT_NBR, rec.COST_CENTER_CODE, 'BANK_DEP_TICK');
        END;
    END LOOP;
    FOR rec IN (SELECT *
                  FROM BANK_DEP_BAG_TICK
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            TABLE_IU_PKG.BANK_DEP_BAG_TICK_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_DEPOSIT_TABLES', SQLERRM, NULL, rec.COST_CENTER_CODE, 'BANK_DEP_BAG_TICK');
        END;
    END LOOP;
    FOR rec IN (SELECT *
                  FROM BANK_DEP_TICKORD
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            TABLE_IU_PKG.BANK_DEP_TICKORD_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_DEPOSIT_TABLES', SQLERRM, rec.BANK_ACCOUNT_NBR, rec.COST_CENTER_CODE, 'BANK_DEP_TICKORD');
        END;
    END LOOP;
    FOR rec IN (SELECT *
                  FROM BANK_DEP_BAG_TICKORD
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            TABLE_IU_PKG.BANK_DEP_BAG_TICKORD_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_DEPOSIT_TABLES', SQLERRM, NULL, rec.COST_CENTER_CODE, 'BANK_DEP_TICKORD');
        END;
    END LOOP;
END PROCESS_BANK_DEPOSIT_TABLES;

PROCEDURE PROCESS_MEMBER_BANK_CC
/******************************************************************************
  This procedure will move the current table records into history tables
  Tables:
      MEMBER_BANK_CC_HIST

Created : 07/22/2015 jxc517 CCN Project....
Changed : 08/13/2015 nxk927 CCN Project....
          (sysdate -1 changed to take sysdate) batch should run for all data before now
Changed : 10/20/2015 jxc517 CCN Project....
          Code has been changed to deal with Deposit Bag futures
Changed : 06/08/2018 nxk927 CCN Project....
          call has been removed to deal with Deposit Bag as bags should not be deleted
*******************************************************************************/
IS
    V_BANK_ACCOUNT_TEMP_REC    BANK_ACCOUNT%ROWTYPE;
    V_LEAD_BANK_HIST_REC       LEAD_BANK_CC%ROWTYPE;
BEGIN
    FOR rec IN (SELECT DISTINCT LEAD_BANK_ACCOUNT_NBR,
                                LEAD_STORE_NBR,
                                EFFECTIVE_DATE
                  FROM MEMBER_BANK_CC
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            V_BANK_ACCOUNT_TEMP_REC := BANKING_COMMON_TOOLS.GET_BANK_ACCOUNT_HIST_REC(rec.LEAD_BANK_ACCOUNT_NBR,
                                                                                      rec.EFFECTIVE_DATE);
            V_LEAD_BANK_HIST_REC    := BANKING_COMMON_TOOLS.GET_LEAD_BANK_CC_HIST_REC(rec.LEAD_STORE_NBR,
                                                                                      rec.EFFECTIVE_DATE);
            TABLE_IU_PKG.BANK_ACCOUNT_HIST_I_SP(V_BANK_ACCOUNT_TEMP_REC);
            TABLE_IU_PKG.LEAD_BANK_CC_HIST_I_SP(V_LEAD_BANK_HIST_REC);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_MEMBER_BANK_CC', SQLERRM, rec.LEAD_BANK_ACCOUNT_NBR, rec.LEAD_STORE_NBR, 'BNK_ACCNT_LEAD_HIST');
        END;
    END LOOP;
    FOR rec IN (SELECT *
                  FROM MEMBER_BANK_CC
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            --store MICR format details needs to be removed for these members
            DEPOSIT_TICKET_MAINTENANCE_PKG.DELETE_STORE_MICR_FORMAT_DTLS(rec.MEMBER_STORE_NBR, NULL, NULL);
            TABLE_IU_PKG.MEMBER_BANK_CC_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_MEMBER_BANK_CC', SQLERRM, rec.LEAD_BANK_ACCOUNT_NBR, rec.MEMBER_STORE_NBR, 'MEMBER_BANK_CC_HIST');
        END;
    END LOOP;
END PROCESS_MEMBER_BANK_CC;

PROCEDURE PROCESS_LEAD_BANK_CC
/******************************************************************************
  This procedure will move the current table records into history tables
  Tables:
      LEAD_BANK_CC_HIST

Created : 07/22/2015 jxc517 CCN Project....
Changed : 08/13/2015 nxk927 CCN Project....
          (sysdate -1 changed to take sysdate) batch should run for all data before now
Changed : 10/20/2015 jxc517 CCN Project....
          Code has been changed to deal with Deposit Bag futures
Changed : 06/08/2018 nxk927 CCN Project....
          Code has been changed to deal with Deposit Bag as bags should not be deleted
*******************************************************************************/
IS
    V_BANK_ACCOUNT_TEMP_REC    BANK_ACCOUNT%ROWTYPE;
BEGIN
    FOR rec IN (SELECT DISTINCT LEAD_BANK_ACCOUNT_NBR,
                                LEAD_STORE_NBR,
                                EFFECTIVE_DATE
                  FROM LEAD_BANK_CC
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            V_BANK_ACCOUNT_TEMP_REC := BANKING_COMMON_TOOLS.GET_BANK_ACCOUNT_HIST_REC(rec.LEAD_BANK_ACCOUNT_NBR,
                                                                                      rec.EFFECTIVE_DATE);
            TABLE_IU_PKG.BANK_ACCOUNT_HIST_I_SP(V_BANK_ACCOUNT_TEMP_REC);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_LEAD_BANK_CC', SQLERRM, rec.LEAD_BANK_ACCOUNT_NBR, rec.LEAD_STORE_NBR, 'BNK_ACCNT_HIST');
        END;
    END LOOP;
    FOR rec IN (SELECT *
                  FROM LEAD_BANK_CC
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            --store MICR format details needs to be removed for these leads
            DEPOSIT_TICKET_MAINTENANCE_PKG.DELETE_STORE_MICR_FORMAT_DTLS(rec.LEAD_STORE_NBR, NULL, NULL);
            TABLE_IU_PKG.LEAD_BANK_CC_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_LEAD_BANK_CC', SQLERRM, rec.LEAD_BANK_ACCOUNT_NBR, rec.LEAD_STORE_NBR, 'LEAD_BANK_CC_HIST');
        END;
    END LOOP;
END PROCESS_LEAD_BANK_CC;

PROCEDURE PROCESS_BANK_MICR_FORMAT
/******************************************************************************
  This procedure will move the current table records into history tables
  Tables:
      BANK_MICR_FORMAT_HIST

Created : 07/22/2015 jxc517 CCN Project....
Changed : 08/13/2015 nxk927 CCN Project....
          (sysdate -1 changed to take sysdate) batch should run for all data before now
*******************************************************************************/
IS
    V_BANK_ACCOUNT_TEMP_REC    BANK_ACCOUNT%ROWTYPE;
BEGIN
    FOR rec IN (SELECT DISTINCT BANK_ACCOUNT_NBR,
                                EFFECTIVE_DATE
                  FROM BANK_MICR_FORMAT
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            V_BANK_ACCOUNT_TEMP_REC := BANKING_COMMON_TOOLS.GET_BANK_ACCOUNT_HIST_REC(rec.BANK_ACCOUNT_NBR,
                                                                                      rec.EFFECTIVE_DATE);
            TABLE_IU_PKG.BANK_ACCOUNT_HIST_I_SP(V_BANK_ACCOUNT_TEMP_REC);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_MICR_FORMAT', SQLERRM, rec.BANK_ACCOUNT_NBR, 'XXXXXX', 'BNK_ACCNT_HIST');
        END;
    END LOOP;
    FOR rec IN (SELECT *
                  FROM BANK_MICR_FORMAT
                 WHERE EXPIRATION_DATE <TRUNC(SYSDATE)) LOOP
        BEGIN
            --store MICR format details needs to be removed for these bank account/MICR format id combination
            DEPOSIT_TICKET_MAINTENANCE_PKG.DELETE_STORE_MICR_FORMAT_DTLS(NULL, rec.BANK_ACCOUNT_NBR, rec.MICR_FORMAT_ID);
            TABLE_IU_PKG.BANK_MICR_FORMAT_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_MICR_FORMAT', SQLERRM, rec.BANK_ACCOUNT_NBR, rec.FORMAT_NAME, 'BANK_MICR_FORMAT_HIST');
        END;
    END LOOP;
END PROCESS_BANK_MICR_FORMAT;

PROCEDURE PROCESS_BANK_ACCOUNT
/******************************************************************************
  This procedure will move the current table records into history tables
  Tables:
      BANK_ACCOUNT_HIST

Created : 07/22/2015 jxc517 CCN Project....
Changed : 08/13/2015 nxk927 CCN Project....
          (sysdate -1 changed to take sysdate) batch should run for all data before now
*******************************************************************************/
IS
BEGIN
    FOR rec IN (SELECT *
                  FROM BANK_ACCOUNT
                 WHERE EXPIRATION_DATE < TRUNC(SYSDATE)) LOOP
        BEGIN
            TABLE_IU_PKG.BANK_ACCOUNT_D_SP(rec);
        EXCEPTION
            WHEN OTHERS THEN
                ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS_BANK_ACCOUNT', SQLERRM, rec.BANK_ACCOUNT_NBR, '000000', 'BANK_ACCOUNT_HIST');
        END;
    END LOOP;
END PROCESS_BANK_ACCOUNT;

PROCEDURE PROCESS
/******************************************************************************
  This procedure will move the current table records into history tables
  Tables:
      LEAD_BANK_CC_HIST
      MEMBER_BANK_CC_HIST
      BANK_MICR_FORMAT_HIST
      BANK_ACCOUNT_HIST

Created : 05/26/2015 jxc517 CCN Project....
Changed : 07/22/2015 jxc517 CCN Project....
          Modified to move childs along with parents into history even if parent is active
        : 01/13/2017 gxg192 CCN Project....
          Changes to Raise the exception so that the process which is calling this procedure
          can take necessary action..
        : 01/26/2017 gxg192 CCN Project....
          Changes to fetch error number from errnums package and call ERRPKG.RAISE_ERR to raise the exception
*******************************************************************************/
IS
V_BATCH_NUMBER      BATCH_JOB.BATCH_JOB_NUMBER%TYPE;
V_TRANS_STATUS      BATCH_JOB.TRANS_STATUS%TYPE := 'SUCCESSFUL';
V_ERROR_MESSAGE     VARCHAR2(500);
BEGIN
    CCN_BATCH_PKG.INSERT_BATCH_JOB('CURR_TO_HIST_LOAD', V_BATCH_NUMBER);
    CCN_BATCH_PKG.LOCK_DATABASE_SP();
    BEGIN
        PROCESS_BANK_DEPOSIT_TABLES();
        PROCESS_BANK_MICR_FORMAT();
        PROCESS_MEMBER_BANK_CC();
        PROCESS_LEAD_BANK_CC();
        PROCESS_BANK_ACCOUNT();
    EXCEPTION
        WHEN OTHERS THEN
            V_ERROR_MESSAGE := SUBSTR(SQLERRM,1,500);
            V_TRANS_STATUS := 'ERROR';
            ROLLBACK;
    END;
    
    CCN_BATCH_PKG.UPDATE_BATCH_JOB('CURR_TO_HIST_LOAD', V_BATCH_NUMBER, V_TRANS_STATUS);
    CCN_BATCH_PKG.UNLOCK_DATABASE_SP();
    COMMIT;
    
    IF V_TRANS_STATUS = 'ERROR'
    THEN
       ERRPKG.RAISE_ERR(errnums.en_curr_to_hist_process_err, V_ERROR_MESSAGE);
    END IF;

EXCEPTION
    WHEN OTHERS THEN
        V_ERROR_MESSAGE := SUBSTR(SQLERRM,1,500);
        ERRPKG.INSERT_ERROR_LOG_SP(SQLCODE, 'CURRENT_TO_HISTORY_PKG.PROCESS', V_ERROR_MESSAGE, '2222222222', '222222', 'OTHER');
        ERRPKG.RAISE_ERR(errnums.en_curr_to_hist_process_err, 'CURRENT_TO_HISTORY_PKG.PROCESS', V_ERROR_MESSAGE);
END PROCESS;

END CURRENT_TO_HISTORY_PKG;