create or replace PACKAGE BODY CCN_BATCH_PKG AS
/*********************************************************** 
This package "BODY" has procedures and functions related to the batch
job table

created : 04/10/2013 SH CCN project
revisions: 
************************************************************/
  
PROCEDURE INSERT_BATCH_JOB (
/**********************************************************
	INSERT_BATCH_JOB

	This procedure is intended to insert a record in the Batch_Job
        table with the details related to the batch run

created : 04/10/2013 SH CCN project....
Modified: 08/19/2016 nxk927 CCN project....
          changed signature. Added servername and logfile name
**********************************************************/
    IN_BATCH_JOB_NAME      IN  BATCH_JOB.BATCH_JOB_NAME%TYPE,
    OUT_BATCH_NUMBER       OUT BATCH_JOB.BATCH_JOB_NUMBER%TYPE,
    IN_SERVER_NAME         IN  BATCH_JOB.SERVER_NAME%TYPE DEFAULT NULL,
    IN_LOG_FILE            IN  BATCH_JOB.LOG_FILE%TYPE DEFAULT NULL
 )
IS

V_CONTEXT	    VARCHAR2(200);
--V_BATCH_JOB_NUMBER  BATCH_JOB.BATCH_JOB_NUMBER%TYPE;
SQ		    INTEGER;
SE		    VARCHAR2(1000);

BEGIN
  INSERT
  INTO BATCH_JOB
    (
      BATCH_JOB_NAME,
      BATCH_JOB_NUMBER,
      BATCH_JOB_STATUS,
      BATCH_JOB_START_DATE,
      BATCH_JOB_END_DATE,
      BATCH_JOB_LAST_RUN_DATE,
      TRANS_STATUS,
      SERVER_NAME,
      LOG_FILE
    )
    VALUES
    (
      IN_BATCH_JOB_NAME,
      BATCH_JOB_NUMBER_SEQ.NEXTVAL,
      'PROCESSING',
      SYSDATE,
      NULL,
      NULL,
      NULL,
      IN_SERVER_NAME,
      IN_LOG_FILE
    ) RETURNING BATCH_JOB_NUMBER INTO OUT_BATCH_NUMBER;

END INSERT_BATCH_JOB;

PROCEDURE UPDATE_BATCH_JOB(
/**********************************************************
	UPDATE_BATCH_JOB

	This procedure is intended to update the record in the Batch_Job
        table with the Status of - IN_PROCESSING, COMPLETED OR ERROR

created : 04/10/2013 SH CCN project....
**********************************************************/
    IN_BATCH_JOB_NAME      IN BATCH_JOB.BATCH_JOB_NAME%TYPE,
    IN_BATCH_JOB_NUMBER    IN BATCH_JOB.BATCH_JOB_NUMBER%TYPE,
    IN_TRANS_STATUS        IN BATCH_JOB.TRANS_STATUS%TYPE
                          )
IS

V_CONTEXT	VARCHAR2(200);
SQ		INTEGER;
SE		VARCHAR2(1000);

BEGIN

      UPDATE BATCH_JOB
         SET BATCH_JOB_STATUS = 'COMPLETED',
             TRANS_STATUS = IN_TRANS_STATUS,
             BATCH_JOB_END_DATE  =  SYSDATE,
             BATCH_JOB_LAST_RUN_DATE = SYSDATE
       WHERE BATCH_JOB_NAME      =  IN_BATCH_JOB_NAME
         AND BATCH_JOB_NUMBER    =  IN_BATCH_JOB_NUMBER;
        
END UPDATE_BATCH_JOB;

PROCEDURE LOCK_DATABASE_SP
/*****************************************************************************
Name:   : LOCK_DATABASE_SP
created : 11/15/2013 CCN KDP Project....
	adding commit since this will only be used for batch jobs
revised : 
******************************************************************************/
IS

PRAGMA AUTONOMOUS_TRANSACTION;

BEGIN
    UPDATE ROLE_DETAILS
       SET INSERT_FLAG = 'Y',
           UPDATE_FLAG = 'Y'
     WHERE ROLE_CODE = 'batusr';

    COMMIT;
END LOCK_DATABASE_SP;


PROCEDURE UNLOCK_DATABASE_SP
/*****************************************************************************
Name:   : unLOCK_DATABASE_SP
created : 11/15/2013 CCN KDP Project....
revised : 
******************************************************************************/
IS

PRAGMA AUTONOMOUS_TRANSACTION;

BEGIN
    UPDATE ROLE_DETAILS
       SET INSERT_FLAG = 'N',
           UPDATE_FLAG = 'N'
     WHERE ROLE_CODE = 'batusr';

    COMMIT;
END UNLOCK_DATABASE_SP;

PROCEDURE FILE_UPLOAD(
/******************************************************************************
FILE_UPLOAD

This procedure will create file on server and dump the passed BLOB data Into the file created

IN_BLOB          - blob value delimited by , and termianted by new line character
IN_FILE_NAME     - name of the file on server

******************************************************************************/
IN_BLOB        IN       BLOB,
IN_FILE_NAME   IN       VARCHAR2,
IN_DIRECTORY   IN       VARCHAR2 DEFAULT 'CCN_LOAD_FILES')
IS
    -- Define output file directory
    V_OUTPUT_FILE     UTL_FILE.FILE_TYPE;
    V_LENGTH          NUMBER := DBMS_LOB.GETLENGTH(IN_BLOB);
    V_START           NUMBER := 1;
    V_BYTE_LENGTH     NUMBER := 32000;
    V_TEMP_LENGTH     NUMBER;
    V_TEMP_BLOB       BLOB; --RAW(32000);
    V_TEMP_CLOB       CLOB;
BEGIN
    V_OUTPUT_FILE := UTL_FILE.FOPEN(IN_DIRECTORY,
                                    IN_FILE_NAME,
                                    'w',
                                    32760);

    -- Save blob length in temporary variable
    V_TEMP_LENGTH := V_LENGTH;
    IF V_LENGTH < 32760 THEN
        V_TEMP_CLOB := CCN_COMMON_TOOLS.convert_to_clob(IN_BLOB);
        UTL_FILE.PUT_LINE(V_OUTPUT_FILE,
                          V_TEMP_CLOB);
        UTL_FILE.FFLUSH(V_OUTPUT_FILE);
    ELSE -- write in pieces
        V_START := 1;
        WHILE V_START < V_LENGTH AND V_BYTE_LENGTH > 0 LOOP
            DBMS_LOB.READ(IN_BLOB,
                          V_BYTE_LENGTH,
                          V_START,
                          V_TEMP_BLOB);
            V_TEMP_CLOB := CCN_COMMON_TOOLS.convert_to_clob(V_TEMP_BLOB);
            UTL_FILE.PUT_LINE(V_OUTPUT_FILE,
                              V_TEMP_CLOB);
            UTL_FILE.FFLUSH(V_OUTPUT_FILE); 
            -- Set the start position for the next cut
            V_START       := V_START + V_BYTE_LENGTH;
            -- Set the end position if less than 32000 bytes
            V_TEMP_LENGTH := V_TEMP_LENGTH - V_BYTE_LENGTH;
            IF V_TEMP_LENGTH < 32000 THEN
                V_BYTE_LENGTH := V_TEMP_LENGTH;
            END IF;
        END LOOP;
    END IF;
    
    UTL_FILE.FCLOSE(V_OUTPUT_FILE);    
END FILE_UPLOAD;

FUNCTION FILE_EXISTS(
/******************************************************************************
Created : 10/04/2017 axt754 CCN Project Team....
FILE_EXISTS

This procedure will check if the file exists in directory and returns true or false
******************************************************************************/
IN_FILE_NAME    IN       VARCHAR2,
IN_DIRECTORY    IN       VARCHAR2) RETURN VARCHAR2
IS
    v_exists     BOOLEAN;
    v_length     NUMBER;
    v_blocksize  NUMBER;
    v_return_val VARCHAR2(1) := 'N';
BEGIN
     IF (NVL(IN_FILE_NAME,'XXX') <> 'XXX') THEN
       UTL_FILE.FGETATTR(IN_DIRECTORY,IN_FILE_NAME,v_exists,v_length,v_blocksize);
       IF v_exists THEN
           v_return_val := 'Y';
       END IF;
    END IF;
    RETURN v_return_val;
END FILE_EXISTS;

PROCEDURE FILE_RENAME(
/******************************************************************************
Created : 09/29/2017 axt754 CCN Project Team....
FILE_RENAME

This procedure will rename file on server by taking the old name and new name.
******************************************************************************/
IN_OLD_FILE    IN       VARCHAR2,
IN_NEW_FILE    IN       VARCHAR2,
IN_DIRECTORY   IN       VARCHAR2 DEFAULT 'CCN_LOAD_FILES')
IS
    V_FILE_EXISTS VARCHAR2(1);
BEGIN
    IF (NVL(IN_OLD_FILE,'XXX') <> 'XXX' AND NVL(IN_NEW_FILE,'XXX') <> 'XXX') THEN
        V_FILE_EXISTS := FILE_EXISTS(IN_OLD_FILE,IN_DIRECTORY);
        IF (V_FILE_EXISTS = 'Y') THEN -- Check if the file exists in the directory
            UTL_FILE.FRENAME(IN_DIRECTORY,IN_OLD_FILE,IN_DIRECTORY,IN_NEW_FILE,TRUE);
        END IF;
    END IF;
END FILE_RENAME;

FUNCTION GET_LAST_RUNDATE(
/******************************************************************
This function will return the LAST BATCH RUN DATE for the batch name passed

created : 09/27/2017 nxk927 CCN Project....
changed :
******************************************************************/
IN_BATCH_NAME       IN   VARCHAR2) RETURN DATE
IS
    V_RETURN_VALUE       DATE;
BEGIN
   SELECT MAX(BATCH_JOB_END_DATE)
     INTO V_RETURN_VALUE
     FROM BATCH_JOB
    WHERE BATCH_JOB_NAME = IN_BATCH_NAME
      AND TRANS_STATUS   = 'SUCCESSFUL';

   RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
         RETURN V_RETURN_VALUE;
END GET_LAST_RUNDATE;

FUNCTION GET_LAST_STARTDATE(
/******************************************************************
This function will return the LAST BATCH START DATE for the batch name passed

created : 09/28/2017 nxk927 CCN Project....
changed :
******************************************************************/
IN_BATCH_NAME       IN   VARCHAR2) RETURN DATE
IS
    V_RETURN_VALUE       DATE;
BEGIN
   SELECT MAX(BATCH_JOB_START_DATE)
     INTO V_RETURN_VALUE
     FROM BATCH_JOB
    WHERE BATCH_JOB_NAME = IN_BATCH_NAME
      AND TRANS_STATUS   = 'SUCCESSFUL';

   RETURN V_RETURN_VALUE;
EXCEPTION
    WHEN OTHERS THEN
         RETURN V_RETURN_VALUE;
END GET_LAST_STARTDATE;

PROCEDURE READ_CATEGORY_FROM_BLOB(
/******************************************************************************
READ_CATEGORY_FROM_BLOB

This procedure will get the category info for the uploaded file from UI

IN_BLOB          - blob value delimited by , and termianted by new line character
created : 03/09/2018 mxv711 added to read category from blob ASP-824

******************************************************************************/
    IN_BLOB        IN       BLOB,
    OUT_CATEGORY   OUT      COST_CENTER.CATEGORY%TYPE)
IS
    V_LENGTH          NUMBER := DBMS_LOB.GETLENGTH(IN_BLOB);
    V_START           NUMBER := 1;
    V_BYTE_LENGTH     NUMBER := 32000;
    V_TEMP_LENGTH     NUMBER;
    V_TEMP_BLOB       BLOB; --RAW(32000);
    V_TEMP_CLOB       CLOB;
    V_POS             NUMBER  := 0;
    V_POS1            NUMBER  := 0;
    V_POS2            NUMBER  := 0;
    V_DELIM           CHAR(1) := ',';

    V_CARRIAGE_RETURN   CONSTANT CHAR(1) := CHR(13);
    V_LINE_FEED         CONSTANT CHAR(1) := CHR(10);

BEGIN
    -- Save blob length in temporary variable
    V_TEMP_LENGTH := V_LENGTH;
    IF V_LENGTH < 32760 THEN
        V_TEMP_CLOB := CCN_COMMON_TOOLS.convert_to_clob(IN_BLOB);
    ELSE -- write in pieces
        V_START := 1;
        WHILE V_START < V_LENGTH AND V_BYTE_LENGTH > 0 LOOP
            DBMS_LOB.READ(IN_BLOB,
                          V_BYTE_LENGTH,
                          V_START,
                          V_TEMP_BLOB);
            V_TEMP_CLOB := V_TEMP_CLOB || CCN_COMMON_TOOLS.convert_to_clob(V_TEMP_BLOB);

            -- Set the start position for the next cut
            V_START       := V_START + V_BYTE_LENGTH;
            -- Set the end position if less than 32000 bytes
            V_TEMP_LENGTH := V_TEMP_LENGTH - V_BYTE_LENGTH;
            IF V_TEMP_LENGTH < 32000 THEN
                V_BYTE_LENGTH := V_TEMP_LENGTH;
            END IF;
        END LOOP;
    END IF;

    V_POS :=  DBMS_LOB.INSTR( V_TEMP_CLOB,   V_LINE_FEED, 1);

    IF V_POS = 0 THEN
        V_POS :=  DBMS_LOB.INSTR( V_TEMP_CLOB,   V_CARRIAGE_RETURN, 1);
    END IF;

    V_POS1 := DBMS_LOB.INSTR( V_TEMP_CLOB,   V_DELIM, V_POS, 3);
    V_POS2 := DBMS_LOB.INSTR( V_TEMP_CLOB,   V_DELIM, V_POS, 2)+1;

    OUT_CATEGORY := SUBSTR(TRIM(DBMS_LOB.SUBSTR(V_TEMP_CLOB, V_POS1-V_POS2, V_POS2 )),1,1);

    IF OUT_CATEGORY IS NULL THEN
        OUT_CATEGORY := 'I';
    END IF;
END READ_CATEGORY_FROM_BLOB;

END CCN_BATCH_PKG;